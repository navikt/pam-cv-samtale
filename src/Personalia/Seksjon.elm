module Personalia.Seksjon exposing
    ( Model
    , Msg
    , SamtaleStatus(..)
    , init
    , meldingsLogg
    , subscriptions
    , update
    , viewBrukerInput
    )

import Api
import Browser.Dom as Dom
import Browser.Events exposing (Visibility(..))
import DebugStatus exposing (DebugStatus)
import ErrorHandtering as ErrorH친ndtering exposing (OperasjonEtterError(..))
import Feilmelding
import FrontendModuler.BrukerInput as BrukerInput exposing (BrukerInput, KnapperLayout(..))
import FrontendModuler.BrukerInputMedGaVidereKnapp as BrukerInputMedG친VidereKnapp
import FrontendModuler.Input as Input
import FrontendModuler.Knapp as Knapp
import FrontendModuler.LoggInnLenke as LoggInnLenke
import Html exposing (..)
import Html.Attributes exposing (..)
import Html.Events exposing (onBlur, onInput)
import Http exposing (Error(..))
import Json.Encode
import LagreStatus exposing (LagreStatus)
import Meldinger.Melding as Melding exposing (Melding(..))
import Meldinger.MeldingsLogg as MeldingsLogg exposing (FerdigAnimertMeldingsLogg, FerdigAnimertStatus(..), MeldingsLogg)
import Meldinger.SamtaleAnimasjon as SamtaleAnimasjon
import Meldinger.SamtaleOppdatering exposing (SamtaleOppdatering(..))
import Personalia.Personalia as Personalia exposing (Personalia)
import Personalia.PersonaliaId as PersonaliaId
import Personalia.Poststed exposing (Poststed)
import Personalia.Skjema as Skjema exposing (PersonaliaSkjema, ValidertPersonaliaSkjema)
import Process
import Task



--- MODEL ---


type Model
    = Model ModelInfo


type alias ModelInfo =
    { seksjonsMeldingsLogg : MeldingsLogg
    , aktivSamtale : Samtale
    , personalia : Personalia
    , debugStatus : DebugStatus
    }


type BekreftPersonaliaState
    = OpprinneligPersonalia Personalia
    | EndretPersonalia ValidertPersonaliaSkjema
    | LagtTilMerKontaktInfo ValidertPersonaliaSkjema


type EndreStatus
    = EndrerFrivillig
    | EndrerPgaValideringsfeil


type Samtale
    = BekreftPersonalia BekreftPersonaliaState
    | EndrerPersonalia EndreStatus PersonaliaSkjema
    | LeggerTilEpost PersonaliaSkjema
    | LeggerTilTelefonnummer { harLagtTilEpost : Bool } PersonaliaSkjema
    | LagrerPersonalia ValidertPersonaliaSkjema LagreStatus
    | LagringFeilet Http.Error ValidertPersonaliaSkjema
    | VenterP친AnimasjonF칮rFullf칮ring Personalia Fullf칮ringStatus


type Fullf칮ringStatus
    = LagringLyktesF칮rsteGang
    | LagringLyktesEtterFlereFors칮k
    | BrukerGikkVidere


type SamtaleStatus
    = IkkeFerdig ( Model, Cmd Msg )
    | Ferdig Personalia FerdigAnimertMeldingsLogg


meldingsLogg : Model -> MeldingsLogg
meldingsLogg (Model model) =
    model.seksjonsMeldingsLogg



--- UPDATE ---


type Msg
    = BrukerVilEndrePersonalia
    | PersonaliaSkjemaEndret Skjema.Felt String
    | PersonaliaSkjemaFeltMistetFokus Skjema.Felt
    | PoststedHentet String (Result Http.Error Poststed)
    | PoststedfeltEndretSelvOmDetErDisabled String
    | PersonaliaskjemaLagreknappTrykket
    | EpostOppdatert String
    | EpostFeltMistetFokus
    | VilG친VidereMedEpost
    | TelefonnummerOppdatert String
    | TelefonnummerFeltMistetFokus
    | VilG친VidereMedTelefonnummer
    | PersonaliaOppdatert (Result Http.Error Personalia)
    | VilLagreBekreftetPersonalia
    | BrukerVilG친VidereUten칀Lagre
    | BrukerVilPr칮ve칀LagreP친Nytt
    | WindowEndrerVisibility Visibility
    | SamtaleAnimasjonMsg SamtaleAnimasjon.Msg
    | FokusSatt (Result Dom.Error ())
    | ErrorLogget


update : Msg -> Model -> SamtaleStatus
update msg (Model model) =
    case msg of
        BrukerVilEndrePersonalia ->
            case model.aktivSamtale of
                BekreftPersonalia bekreftPersonaliaState ->
                    case bekreftPersonaliaState of
                        OpprinneligPersonalia personalia ->
                            ( personalia
                                |> Skjema.init
                                |> EndrerPersonalia EndrerFrivillig
                                |> oppdaterSamtale model (SvarFraMsg msg)
                            , lagtTilSp칮rsm친lCmd model.debugStatus
                            )
                                |> IkkeFerdig

                        EndretPersonalia validertSkjema ->
                            ( validertSkjema
                                |> Skjema.tilUvalidertSkjema
                                |> EndrerPersonalia EndrerFrivillig
                                |> oppdaterSamtale model (SvarFraMsg msg)
                            , lagtTilSp칮rsm친lCmd model.debugStatus
                            )
                                |> IkkeFerdig

                        LagtTilMerKontaktInfo validertSkjema ->
                            ( validertSkjema
                                |> Skjema.tilUvalidertSkjema
                                |> EndrerPersonalia EndrerFrivillig
                                |> oppdaterSamtale model (SvarFraMsg msg)
                            , lagtTilSp칮rsm친lCmd model.debugStatus
                            )
                                |> IkkeFerdig

                _ ->
                    IkkeFerdig ( Model model, Cmd.none )

        PersonaliaSkjemaEndret felt string ->
            case model.aktivSamtale of
                EndrerPersonalia endreGrunn skjema ->
                    ( Skjema.oppdaterFelt felt skjema string
                        |> EndrerPersonalia endreGrunn
                        |> oppdaterSamtale model IngenNyeMeldinger
                    , case felt of
                        Skjema.Postnummer ->
                            if String.length string == 4 && String.toInt string /= Nothing then
                                Api.hentPoststed (PoststedHentet string) string

                            else
                                Cmd.none

                        _ ->
                            Cmd.none
                    )
                        |> IkkeFerdig

                _ ->
                    ( Model model, Cmd.none )
                        |> IkkeFerdig

        PersonaliaSkjemaFeltMistetFokus felt ->
            case model.aktivSamtale of
                EndrerPersonalia endreGrunn skjema ->
                    ( skjema
                        |> Skjema.gj칮rFeilmeldingSynligForFelt felt
                        |> EndrerPersonalia endreGrunn
                        |> oppdaterSamtale model IngenNyeMeldinger
                    , Cmd.none
                    )
                        |> IkkeFerdig

                _ ->
                    ( Model model, Cmd.none )
                        |> IkkeFerdig

        PoststedHentet postnummer result ->
            case result of
                Ok poststed ->
                    case model.aktivSamtale of
                        EndrerPersonalia endreGrunn skjema ->
                            ( skjema
                                |> Skjema.oppdaterPoststed poststed
                                |> EndrerPersonalia endreGrunn
                                |> oppdaterSamtale model IngenNyeMeldinger
                            , Cmd.none
                            )
                                |> IkkeFerdig

                        _ ->
                            IkkeFerdig ( Model model, Cmd.none )

                Err error ->
                    case error of
                        Http.BadStatus 404 ->
                            IkkeFerdig ( Model model, Cmd.none )

                        _ ->
                            IkkeFerdig
                                ( Model model
                                , error
                                    |> Feilmelding.feilmelding ("Hent poststed for postnummer: \"" ++ postnummer ++ "\"")
                                    |> Maybe.map (Api.logError (always ErrorLogget))
                                    |> Maybe.withDefault Cmd.none
                                )

        PoststedfeltEndretSelvOmDetErDisabled _ ->
            IkkeFerdig ( Model model, Cmd.none )

        PersonaliaskjemaLagreknappTrykket ->
            case model.aktivSamtale of
                EndrerPersonalia endreGrunn skjema ->
                    case Skjema.validerSkjema skjema of
                        Just validertSkjema ->
                            ( validertSkjema
                                |> EndretPersonalia
                                |> BekreftPersonalia
                                |> oppdaterSamtale model (ManueltSvar (Melding.svar (personaliaSkjemaOppsummering skjema)))
                            , lagtTilSp칮rsm친lCmd model.debugStatus
                            )
                                |> IkkeFerdig

                        Nothing ->
                            ( skjema
                                |> Skjema.gj칮rAlleFeilmeldingerSynlig
                                |> EndrerPersonalia endreGrunn
                                |> oppdaterSamtale model IngenNyeMeldinger
                            , Cmd.none
                            )
                                |> IkkeFerdig

                _ ->
                    ( Model model, Cmd.none )
                        |> IkkeFerdig

        EpostOppdatert string ->
            case model.aktivSamtale of
                LeggerTilEpost skjema ->
                    ( string
                        |> Skjema.oppdaterFelt Skjema.Epost skjema
                        |> LeggerTilEpost
                        |> oppdaterSamtale model IngenNyeMeldinger
                    , Cmd.none
                    )
                        |> IkkeFerdig

                _ ->
                    IkkeFerdig ( Model model, Cmd.none )

        EpostFeltMistetFokus ->
            case model.aktivSamtale of
                LeggerTilEpost skjema ->
                    ( skjema
                        |> Skjema.gj칮rFeilmeldingSynligForFelt Skjema.Epost
                        |> LeggerTilEpost
                        |> oppdaterSamtale model IngenNyeMeldinger
                    , Cmd.none
                    )
                        |> IkkeFerdig

                _ ->
                    IkkeFerdig ( Model model, Cmd.none )

        VilG친VidereMedEpost ->
            case model.aktivSamtale of
                LeggerTilEpost skjema ->
                    case (Skjema.gj칮rFeilmeldingSynligForFelt Skjema.Epost >> Skjema.epostFeilmelding) skjema of
                        Just feilmelding ->
                            ( skjema
                                |> Skjema.gj칮rFeilmeldingSynligForFelt Skjema.Epost
                                |> LeggerTilEpost
                                |> oppdaterSamtale model IngenNyeMeldinger
                            , Cmd.none
                            )
                                |> IkkeFerdig

                        Nothing ->
                            if (Personalia.telefon >> Maybe.withDefault "" >> String.trim >> String.isEmpty) model.personalia then
                                ( skjema
                                    |> LeggerTilTelefonnummer { harLagtTilEpost = True }
                                    |> oppdaterSamtale model (SvarFraMsg msg)
                                , lagtTilSp칮rsm친lCmd model.debugStatus
                                )
                                    |> IkkeFerdig

                            else
                                case Skjema.validerSkjema skjema of
                                    Just validertSkjema ->
                                        ( validertSkjema
                                            |> LagtTilMerKontaktInfo
                                            |> BekreftPersonalia
                                            |> oppdaterSamtale model (SvarFraMsg msg)
                                        , lagtTilSp칮rsm친lCmd model.debugStatus
                                        )
                                            |> IkkeFerdig

                                    Nothing ->
                                        g친TilEndreSkjemaEtterValideringsFeil model msg skjema

                _ ->
                    IkkeFerdig ( Model model, Cmd.none )

        TelefonnummerOppdatert string ->
            case model.aktivSamtale of
                LeggerTilTelefonnummer harLagtTilEpost skjema ->
                    ( string
                        |> Skjema.oppdaterFelt Skjema.Telefon skjema
                        |> LeggerTilTelefonnummer harLagtTilEpost
                        |> oppdaterSamtale model IngenNyeMeldinger
                    , Cmd.none
                    )
                        |> IkkeFerdig

                _ ->
                    IkkeFerdig ( Model model, Cmd.none )

        TelefonnummerFeltMistetFokus ->
            case model.aktivSamtale of
                LeggerTilTelefonnummer harLagtTilEpost skjema ->
                    ( skjema
                        |> Skjema.gj칮rFeilmeldingSynligForFelt Skjema.Telefon
                        |> LeggerTilTelefonnummer harLagtTilEpost
                        |> oppdaterSamtale model IngenNyeMeldinger
                    , Cmd.none
                    )
                        |> IkkeFerdig

                _ ->
                    IkkeFerdig ( Model model, Cmd.none )

        VilG친VidereMedTelefonnummer ->
            case model.aktivSamtale of
                LeggerTilTelefonnummer harLagtTilEpost skjema ->
                    case (Skjema.gj칮rFeilmeldingSynligForFelt Skjema.Telefon >> Skjema.telefonFeilmelding) skjema of
                        Just feilmelding ->
                            ( skjema
                                |> Skjema.gj칮rFeilmeldingSynligForFelt Skjema.Telefon
                                |> LeggerTilTelefonnummer harLagtTilEpost
                                |> oppdaterSamtale model IngenNyeMeldinger
                            , Cmd.none
                            )
                                |> IkkeFerdig

                        Nothing ->
                            case Skjema.validerSkjema skjema of
                                Just validertSkjema ->
                                    ( validertSkjema
                                        |> LagtTilMerKontaktInfo
                                        |> BekreftPersonalia
                                        |> oppdaterSamtale model (SvarFraMsg msg)
                                    , lagtTilSp칮rsm친lCmd model.debugStatus
                                    )
                                        |> IkkeFerdig

                                Nothing ->
                                    g친TilEndreSkjemaEtterValideringsFeil model msg skjema

                _ ->
                    IkkeFerdig ( Model model, Cmd.none )

        PersonaliaOppdatert result ->
            case model.aktivSamtale of
                LagrerPersonalia skjema lagreStatus ->
                    case result of
                        Ok personalia ->
                            if LagreStatus.lagrerEtterUtlogging lagreStatus then
                                lagreStatus
                                    |> fullf칮rtStatusEtterOkLagring
                                    |> VenterP친AnimasjonF칮rFullf칮ring personalia
                                    |> oppdaterSamtale model (SvarFraMsg msg)
                                    |> fullf칮rSeksjonHvisMeldingsloggErFerdig personalia

                            else
                                lagreStatus
                                    |> fullf칮rtStatusEtterOkLagring
                                    |> VenterP친AnimasjonF칮rFullf칮ring personalia
                                    |> oppdaterSamtale model UtenSvar
                                    |> fullf칮rSeksjonHvisMeldingsloggErFerdig personalia

                        Err error ->
                            if LagreStatus.lagrerEtterUtlogging lagreStatus then
                                if LagreStatus.fors칮kP친Nytt lagreStatus then
                                    ( error
                                        |> LagreStatus.fraError
                                        |> LagrerPersonalia skjema
                                        |> oppdaterSamtale model IngenNyeMeldinger
                                    , Api.endrePersonalia PersonaliaOppdatert skjema
                                    )
                                        |> IkkeFerdig

                                else
                                    ( skjema
                                        |> LagringFeilet error
                                        |> oppdaterSamtale model IngenNyeMeldinger
                                    , Cmd.none
                                    )
                                        |> IkkeFerdig

                            else
                                ( skjema
                                    |> LagringFeilet error
                                    |> oppdaterSamtale model UtenSvar
                                , Cmd.batch
                                    [ lagtTilSp칮rsm친lCmd model.debugStatus
                                    , model.personalia
                                        |> Personalia.id
                                        |> PersonaliaId.encode
                                        |> Tuple.pair "id"
                                        |> List.singleton
                                        |> Json.Encode.object
                                        |> Api.logErrorWithRequestBody ErrorLogget "Lagre personalia" error
                                    ]
                                )
                                    |> IkkeFerdig

                _ ->
                    IkkeFerdig ( Model model, Cmd.none )

        VilLagreBekreftetPersonalia ->
            case model.aktivSamtale of
                LagringFeilet error feiletPersonalia ->
                    ( error
                        |> LagreStatus.fraError
                        |> LagrerPersonalia feiletPersonalia
                        |> oppdaterSamtale model (SvarFraMsg msg)
                    , Cmd.batch
                        [ Api.endrePersonalia PersonaliaOppdatert feiletPersonalia
                        , lagtTilSp칮rsm친lCmd model.debugStatus
                        ]
                    )
                        |> IkkeFerdig

                BekreftPersonalia bekreftPersonaliaState ->
                    case bekreftPersonaliaState of
                        OpprinneligPersonalia personalia ->
                            if (Personalia.epost >> Maybe.withDefault "" >> String.trim >> String.isEmpty) personalia then
                                ( personalia
                                    |> Skjema.init
                                    |> LeggerTilEpost
                                    |> oppdaterSamtale model (SvarFraMsg msg)
                                , lagtTilSp칮rsm친lCmd model.debugStatus
                                )
                                    |> IkkeFerdig

                            else if (Personalia.telefon >> Maybe.withDefault "" >> String.trim >> String.isEmpty) model.personalia then
                                ( personalia
                                    |> Skjema.init
                                    |> LeggerTilTelefonnummer { harLagtTilEpost = False }
                                    |> oppdaterSamtale model (SvarFraMsg msg)
                                , lagtTilSp칮rsm친lCmd model.debugStatus
                                )
                                    |> IkkeFerdig

                            else
                                LagringLyktesF칮rsteGang
                                    |> VenterP친AnimasjonF칮rFullf칮ring model.personalia
                                    |> oppdaterSamtale model (SvarFraMsg msg)
                                    |> fullf칮rSeksjonHvisMeldingsloggErFerdig model.personalia

                        EndretPersonalia validertSkjema ->
                            lagreSkjema model msg validertSkjema

                        LagtTilMerKontaktInfo validertSkjema ->
                            lagreSkjema model msg validertSkjema

                _ ->
                    IkkeFerdig ( Model model, Cmd.none )

        BrukerVilPr칮ve칀LagreP친Nytt ->
            case model.aktivSamtale of
                LagringFeilet error skjema ->
                    ( error
                        |> LagreStatus.fraError
                        |> LagrerPersonalia skjema
                        |> oppdaterSamtale model (SvarFraMsg msg)
                    , Cmd.batch
                        [ Api.endrePersonalia PersonaliaOppdatert skjema
                        , lagtTilSp칮rsm친lCmd model.debugStatus
                        ]
                    )
                        |> IkkeFerdig

                _ ->
                    IkkeFerdig ( Model model, Cmd.none )

        BrukerVilG친VidereUten칀Lagre ->
            BrukerGikkVidere
                |> VenterP친AnimasjonF칮rFullf칮ring model.personalia
                |> oppdaterSamtale model (SvarFraMsg msg)
                |> fullf칮rSeksjonHvisMeldingsloggErFerdig model.personalia

        WindowEndrerVisibility visibility ->
            case visibility of
                Visible ->
                    case model.aktivSamtale of
                        LagringFeilet error skjema ->
                            ( error
                                |> LagreStatus.fraError
                                |> LagrerPersonalia skjema
                                |> oppdaterSamtale model IngenNyeMeldinger
                            , Api.endrePersonalia PersonaliaOppdatert skjema
                            )
                                |> IkkeFerdig

                        LagrerPersonalia skjema lagreStatus ->
                            ( lagreStatus
                                |> LagreStatus.setFors칮kP친Nytt
                                |> LagrerPersonalia skjema
                                |> oppdaterSamtale model IngenNyeMeldinger
                            , Cmd.none
                            )
                                |> IkkeFerdig

                        _ ->
                            IkkeFerdig ( Model model, Cmd.none )

                Hidden ->
                    IkkeFerdig ( Model model, Cmd.none )

        SamtaleAnimasjonMsg samtaleAnimasjonMsg ->
            SamtaleAnimasjon.update model.debugStatus samtaleAnimasjonMsg model.seksjonsMeldingsLogg
                |> updateEtterFullf칮rtMelding model

        FokusSatt _ ->
            IkkeFerdig ( Model model, Cmd.none )

        ErrorLogget ->
            IkkeFerdig ( Model model, Cmd.none )


fullf칮rtStatusEtterOkLagring : LagreStatus -> Fullf칮ringStatus
fullf칮rtStatusEtterOkLagring lagreStatus =
    if LagreStatus.lagrerP친F칮rsteFors칮k lagreStatus then
        LagringLyktesF칮rsteGang

    else
        LagringLyktesEtterFlereFors칮k


lagreSkjema : ModelInfo -> Msg -> ValidertPersonaliaSkjema -> SamtaleStatus
lagreSkjema model msg validertSkjema =
    ( LagreStatus.init
        |> LagrerPersonalia validertSkjema
        |> oppdaterSamtale model (SvarFraMsg msg)
    , Cmd.batch
        [ Api.endrePersonalia PersonaliaOppdatert validertSkjema
        , lagtTilSp칮rsm친lCmd model.debugStatus
        ]
    )
        |> IkkeFerdig


g친TilEndreSkjemaEtterValideringsFeil : ModelInfo -> Msg -> PersonaliaSkjema -> SamtaleStatus
g친TilEndreSkjemaEtterValideringsFeil model msg skjema =
    ( skjema
        |> Skjema.gj칮rAlleFeilmeldingerSynlig
        |> EndrerPersonalia EndrerPgaValideringsfeil
        |> oppdaterSamtale model (SvarFraMsg msg)
    , lagtTilSp칮rsm친lCmd model.debugStatus
    )
        |> IkkeFerdig


updateEtterFullf칮rtMelding : ModelInfo -> ( MeldingsLogg, Cmd SamtaleAnimasjon.Msg ) -> SamtaleStatus
updateEtterFullf칮rtMelding model ( nyMeldingsLogg, cmd ) =
    case MeldingsLogg.ferdigAnimert nyMeldingsLogg of
        FerdigAnimert ferdigAnimertSamtale ->
            case model.aktivSamtale of
                VenterP친AnimasjonF칮rFullf칮ring personalia _ ->
                    Ferdig personalia ferdigAnimertSamtale

                _ ->
                    ( Model { model | seksjonsMeldingsLogg = nyMeldingsLogg }
                    , Cmd.batch
                        [ Cmd.map SamtaleAnimasjonMsg cmd
                        , settFokus model.aktivSamtale
                        ]
                    )
                        |> IkkeFerdig

        MeldingerGjenst친r ->
            ( Model { model | seksjonsMeldingsLogg = nyMeldingsLogg }
            , Cmd.map SamtaleAnimasjonMsg cmd
            )
                |> IkkeFerdig


fullf칮rSeksjonHvisMeldingsloggErFerdig : Personalia -> Model -> SamtaleStatus
fullf칮rSeksjonHvisMeldingsloggErFerdig personalia (Model model) =
    case MeldingsLogg.ferdigAnimert model.seksjonsMeldingsLogg of
        FerdigAnimert ferdigAnimertMeldingsLogg ->
            Ferdig personalia ferdigAnimertMeldingsLogg

        MeldingerGjenst친r ->
            ( Model model, lagtTilSp칮rsm친lCmd model.debugStatus )
                |> IkkeFerdig


settFokus : Samtale -> Cmd Msg
settFokus samtale =
    case samtale of
        LeggerTilEpost _ ->
            settFokusCmd EpostId

        LeggerTilTelefonnummer _ _ ->
            settFokusCmd TelefonnummerId

        BekreftPersonalia _ ->
            settFokusCmd BekreftPersonaliaId

        EndrerPersonalia _ _ ->
            settFokusCmd FornavnId

        _ ->
            Cmd.none


settFokusCmd : InputId -> Cmd Msg
settFokusCmd inputId =
    Process.sleep 200
        |> Task.andThen (\_ -> (inputIdTilString >> Dom.focus) inputId)
        |> Task.attempt FokusSatt


lagtTilSp칮rsm친lCmd : DebugStatus -> Cmd Msg
lagtTilSp칮rsm친lCmd debugStatus =
    SamtaleAnimasjon.startAnimasjon debugStatus
        |> Cmd.map SamtaleAnimasjonMsg


svarFraBrukerInput : ModelInfo -> Msg -> Melding
svarFraBrukerInput modelInfo msg =
    modelInfo
        |> modelTilBrukerInput
        |> BrukerInput.tilSvarMelding msg


oppdaterSamtale : ModelInfo -> SamtaleOppdatering Msg -> Samtale -> Model
oppdaterSamtale model meldingsoppdatering samtale =
    Model
        { model
            | aktivSamtale = samtale
            , seksjonsMeldingsLogg =
                case meldingsoppdatering of
                    IngenNyeMeldinger ->
                        model.seksjonsMeldingsLogg

                    SvarFraMsg msg ->
                        model.seksjonsMeldingsLogg
                            |> MeldingsLogg.leggTilSvar (svarFraBrukerInput model msg)
                            |> MeldingsLogg.leggTilSp칮rsm친l (samtaleTilMeldingsLogg samtale)

                    ManueltSvar melding ->
                        model.seksjonsMeldingsLogg
                            |> MeldingsLogg.leggTilSvar melding
                            |> MeldingsLogg.leggTilSp칮rsm친l (samtaleTilMeldingsLogg samtale)

                    UtenSvar ->
                        model.seksjonsMeldingsLogg
                            |> MeldingsLogg.leggTilSp칮rsm친l (samtaleTilMeldingsLogg samtale)
        }


samtaleTilMeldingsLogg : Samtale -> List Melding
samtaleTilMeldingsLogg personaliaSeksjon =
    case personaliaSeksjon of
        BekreftPersonalia bekreftState ->
            case bekreftState of
                OpprinneligPersonalia personalia ->
                    [ Melding.sp칮rsm친l [ "Da setter vi i gang 游땕" ]
                    , Melding.sp칮rsm친l [ "Jeg har hentet inn kontaktinformasjonen din. Den vises p친 CV-en. Sjekk at den er riktig, slik at arbeidsgivere kan kontakte deg." ]
                    , Melding.sp칮rsm친l
                        (List.concat
                            [ personalia
                                |> Skjema.init
                                |> personaliaSkjemaOppsummering
                            , [ Melding.tomLinje
                              , "Er kontaktinformasjonen riktig?"
                              ]
                            ]
                        )
                    ]

                EndretPersonalia _ ->
                    [ Melding.sp칮rsm친l [ "Da har du endret 游녨 Er det riktig n친?" ] ]

                LagtTilMerKontaktInfo validertSkjema ->
                    [ Melding.sp칮rsm친l
                        (List.concat
                            [ validertSkjema
                                |> Skjema.tilUvalidertSkjema
                                |> personaliaSkjemaOppsummering
                            , [ Melding.tomLinje
                              , "Er kontaktinformasjonen riktig n친?"
                              ]
                            ]
                        )
                    ]

        EndrerPersonalia endreGrunn _ ->
            case endreGrunn of
                EndrerFrivillig ->
                    [ Melding.sp칮rsm친l [ "Ok! Skriv inn riktig informasjon i feltene under." ] ]

                EndrerPgaValideringsfeil ->
                    [ Melding.sp칮rsm친l [ "Det skjedde en feil 游땟 Du m친 rette feilen og lagre p친 nytt." ] ]

        LeggerTilEpost _ ->
            [ Melding.sp칮rsm친l [ "Jeg ser at vi mangler e-postadressen din. Kan du fylle inn den?" ] ]

        LeggerTilTelefonnummer { harLagtTilEpost } _ ->
            if harLagtTilEpost then
                [ Melding.sp칮rsm친l [ "Supert! Jeg trenger ogs친 telefonnummeret ditt. Da kan arbeidsgivere ringe deg." ] ]

            else
                [ Melding.sp칮rsm친l [ "Jeg ser at vi mangler telefonnummeret ditt. Kan du fylle inn det?" ] ]

        LagrerPersonalia _ _ ->
            []

        LagringFeilet error _ ->
            [ ErrorH친ndtering.errorMelding { operasjon = "lagre kontaktinformasjonen", error = error } ]

        VenterP친AnimasjonF칮rFullf칮ring _ fullf칮ringStatus ->
            case fullf칮ringStatus of
                LagringLyktesF칮rsteGang ->
                    [ Melding.sp칮rsm친l [ "S친 bra! 游땕 N친 kan arbeidsgivere kontakte deg." ]
                    , Melding.sp칮rsm친l [ "Da g친r vi videre til utdanning." ]
                    ]

                LagringLyktesEtterFlereFors칮k ->
                    [ Melding.sp칮rsm친l [ "Supert! N친 fikk jeg det til. Kontaktinformasjonen er lagret. La oss fortsette 游땕" ] ]

                BrukerGikkVidere ->
                    [ Melding.sp칮rsm친l [ "Da g친r vi videre." ] ]


personaliaSkjemaOppsummering : PersonaliaSkjema -> List String
personaliaSkjemaOppsummering skjema =
    [ "Navn: " ++ Skjema.fornavn skjema ++ " " ++ Skjema.etternavn skjema
    , "F칮dselsdato: " ++ (skjema |> Skjema.fodselsdato |> viewDatoString)
    , "E-post: " ++ Skjema.epost skjema
    , "Telefonnummer: " ++ Skjema.telefon skjema
    , "Adresse: "
        ++ Skjema.gateadresse skjema
        ++ " "
        ++ Skjema.postnummer skjema
        ++ " "
        ++ Skjema.poststed skjema
    ]


viewDatoString : String -> String
viewDatoString datoString =
    case String.split "-" datoString of
        친r :: m친ned :: dag :: [] ->
            dag ++ "." ++ m친ned ++ "." ++ 친r

        _ ->
            datoString



--- VIEW ---


type InputId
    = EpostId
    | TelefonnummerId
    | BekreftPersonaliaId
    | FornavnId


inputIdTilString : InputId -> String
inputIdTilString inputId =
    case inputId of
        EpostId ->
            "personalia-epost"

        TelefonnummerId ->
            "personalia-telefonnummer"

        BekreftPersonaliaId ->
            "personalia-bekreft-id"

        FornavnId ->
            "personalia-fornavn-id"


viewBrukerInput : Model -> Html Msg
viewBrukerInput (Model model) =
    model
        |> modelTilBrukerInput
        |> BrukerInput.toHtml


modelTilBrukerInput : ModelInfo -> BrukerInput Msg
modelTilBrukerInput { aktivSamtale, seksjonsMeldingsLogg } =
    if MeldingsLogg.visBrukerInput seksjonsMeldingsLogg then
        case aktivSamtale of
            BekreftPersonalia _ ->
                BrukerInput.knapper Flytende
                    [ Knapp.knapp VilLagreBekreftetPersonalia "Ja, det er riktig"
                        |> Knapp.withId (inputIdTilString BekreftPersonaliaId)
                    , Knapp.knapp BrukerVilEndrePersonalia "Nei, jeg vil endre"
                    ]

            EndrerPersonalia _ personaliaSkjema ->
                BrukerInput.skjema { lagreMsg = PersonaliaskjemaLagreknappTrykket, lagreKnappTekst = "Lagre endringer" }
                    [ personaliaSkjema
                        |> Skjema.fornavn
                        |> Input.input { label = "Fornavn", msg = PersonaliaSkjemaEndret Skjema.Fornavn }
                        |> Input.withFeilmelding (Skjema.fornavnFeilmelding personaliaSkjema)
                        |> Input.withOnBlur (PersonaliaSkjemaFeltMistetFokus Skjema.Fornavn)
                        |> Input.withErObligatorisk
                        |> Input.withId (inputIdTilString FornavnId)
                        |> Input.toHtml
                    , personaliaSkjema
                        |> Skjema.etternavn
                        |> Input.input { label = "Etternavn", msg = PersonaliaSkjemaEndret Skjema.Etternavn }
                        |> Input.withFeilmelding (Skjema.etternavnFeilmelding personaliaSkjema)
                        |> Input.withOnBlur (PersonaliaSkjemaFeltMistetFokus Skjema.Etternavn)
                        |> Input.withErObligatorisk
                        |> Input.toHtml
                    , personaliaSkjema
                        |> Skjema.epost
                        |> Input.input { label = "E-post", msg = PersonaliaSkjemaEndret Skjema.Epost }
                        |> Input.withFeilmelding (Skjema.epostFeilmelding personaliaSkjema)
                        |> Input.withOnBlur (PersonaliaSkjemaFeltMistetFokus Skjema.Epost)
                        |> Input.withErObligatorisk
                        |> Input.toHtml
                    , viewTelefonISkjema personaliaSkjema
                    , personaliaSkjema
                        |> Skjema.gateadresse
                        |> Input.input { label = "Gateadresse", msg = PersonaliaSkjemaEndret Skjema.Gateadresse }
                        |> Input.toHtml
                    , div [ class "PersonaliaSeksjon-poststed" ]
                        [ personaliaSkjema
                            |> Skjema.postnummer
                            |> Input.input { label = "Postnummer", msg = PersonaliaSkjemaEndret Skjema.Postnummer }
                            |> Input.withFeilmelding (Skjema.postnummerFeilmelding personaliaSkjema)
                            |> Input.withOnBlur (PersonaliaSkjemaFeltMistetFokus Skjema.Postnummer)
                            |> Input.toHtml
                        , personaliaSkjema
                            |> Skjema.poststed
                            |> Input.input { label = "Poststed", msg = PoststedfeltEndretSelvOmDetErDisabled }
                            |> Input.withEnabled Input.Disabled
                            |> Input.toHtml
                        ]
                    ]

            LeggerTilEpost skjema ->
                skjema
                    |> Skjema.epost
                    |> Input.input { label = "E-postadresse", msg = EpostOppdatert }
                    |> Input.withErObligatorisk
                    |> Input.withFeilmelding (Skjema.epostFeilmelding skjema)
                    |> Input.withId (inputIdTilString EpostId)
                    |> Input.withOnBlur EpostFeltMistetFokus
                    |> Input.withOnEnter VilG친VidereMedEpost
                    |> BrukerInputMedG친VidereKnapp.input VilG친VidereMedEpost
                    |> BrukerInput.brukerInputMedG친VidereKnapp

            LeggerTilTelefonnummer _ skjema ->
                skjema
                    |> Skjema.telefon
                    |> Input.input { label = "Telefonnummer", msg = TelefonnummerOppdatert }
                    |> Input.withErObligatorisk
                    |> Input.withFeilmelding (Skjema.telefonFeilmelding skjema)
                    |> Input.withId (inputIdTilString TelefonnummerId)
                    |> Input.withOnBlur TelefonnummerFeltMistetFokus
                    |> Input.withOnEnter VilG친VidereMedTelefonnummer
                    |> BrukerInputMedG친VidereKnapp.input VilG친VidereMedTelefonnummer
                    |> BrukerInput.brukerInputMedG친VidereKnapp

            -- Lenken for 친 logge seg inn skal alltid v칝re synlig hvis man har blitt utlogget, selv under lagring
            LagrerPersonalia _ lagreStatus ->
                if LagreStatus.lagrerEtterUtlogging lagreStatus then
                    LoggInnLenke.viewLoggInnLenke

                else
                    BrukerInput.utenInnhold

            LagringFeilet error _ ->
                case ErrorH친ndtering.operasjonEtterError error of
                    GiOpp ->
                        BrukerInput.knapper Flytende
                            [ Knapp.knapp BrukerVilG친VidereUten칀Lagre "G친 videre"
                            ]

                    Pr칮vP친Nytt ->
                        BrukerInput.knapper Flytende
                            [ Knapp.knapp BrukerVilPr칮ve칀LagreP친Nytt "Pr칮v p친 nytt"
                            , Knapp.knapp BrukerVilG친VidereUten칀Lagre "G친 videre"
                            ]

                    LoggInn ->
                        LoggInnLenke.viewLoggInnLenke

            VenterP친AnimasjonF칮rFullf칮ring _ _ ->
                BrukerInput.utenInnhold

    else
        BrukerInput.utenInnhold


viewTelefonISkjema : PersonaliaSkjema -> Html Msg
viewTelefonISkjema personaliaSkjema =
    div [ class "skjemaelement" ]
        [ label []
            [ span [ class "skjemaelement__label" ]
                [ text "Telefon"
                , span [ class "skjemaelement__m친FyllesUt" ] [ text " - m친 fylles ut" ]
                ]
            , div [ class "PersonaliaSeksjon--telefonnummer" ]
                [ p [ class "PersonaliaSeksjon--telefonnummer--country-code" ] [ text "+47" ]
                , input
                    [ value (Skjema.telefon personaliaSkjema)
                    , onInput (PersonaliaSkjemaEndret Skjema.Telefon)
                    , onBlur (PersonaliaSkjemaFeltMistetFokus Skjema.Telefon)
                    , classList
                        [ ( "skjemaelement__input", True )
                        , ( "PersonaliaSeksjon--telefonnummer--input", True )
                        , ( "skjemaelement__input--harFeil", Skjema.telefonFeilmelding personaliaSkjema /= Nothing )
                        ]
                    ]
                    []
                ]
            , case Skjema.telefonFeilmelding personaliaSkjema of
                Just feilmelding ->
                    div [ class "skjemaelement__feilmelding" ] [ text feilmelding ]

                Nothing ->
                    text ""
            ]
        ]



--- INIT ---


init : DebugStatus -> Personalia -> MeldingsLogg -> ( Model, Cmd Msg )
init debugStatus personalia gammelMeldingsLogg =
    let
        aktivSamtale =
            BekreftPersonalia (OpprinneligPersonalia personalia)
    in
    ( Model
        { seksjonsMeldingsLogg =
            MeldingsLogg.leggTilSp칮rsm친l
                (samtaleTilMeldingsLogg aktivSamtale)
                gammelMeldingsLogg
        , aktivSamtale = aktivSamtale
        , personalia = personalia
        , debugStatus = debugStatus
        }
    , lagtTilSp칮rsm친lCmd debugStatus
    )


subscriptions : Model -> Sub Msg
subscriptions (Model model) =
    Sub.batch
        [ Browser.Events.onVisibilityChange WindowEndrerVisibility
        , model.seksjonsMeldingsLogg
            |> SamtaleAnimasjon.subscriptions
            |> Sub.map SamtaleAnimasjonMsg
        ]
