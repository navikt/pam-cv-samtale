module Personalia.Seksjon exposing
    ( Model
    , Msg
    , SamtaleStatus(..)
    , init
    , meldingsLogg
    , subscriptions
    , update
    , viewBrukerInput
    )

import Api
import Browser.Dom as Dom
import Browser.Events exposing (Visibility(..))
import DebugStatus exposing (DebugStatus)
import ErrorMelding exposing (OperasjonEtterError(..))
import Feilmelding
import FrontendModuler.Containers as Containers exposing (KnapperLayout(..))
import FrontendModuler.Input as Input
import FrontendModuler.Knapp as Knapp
import FrontendModuler.Lenke as Lenke
import Html exposing (..)
import Html.Attributes exposing (..)
import Html.Events exposing (onBlur, onInput)
import Http exposing (Error(..))
import Json.Encode
import Melding exposing (Melding(..))
import MeldingsLogg exposing (FerdigAnimertMeldingsLogg, FerdigAnimertStatus(..), MeldingsLogg)
import Personalia exposing (Personalia)
import Personalia.Skjema as Skjema exposing (PersonaliaSkjema, ValidertPersonaliaSkjema)
import Poststed exposing (Poststed)
import Process
import SamtaleAnimasjon
import Task



--- MODEL ---


type Model
    = Model ModelInfo


type alias ModelInfo =
    { seksjonsMeldingsLogg : MeldingsLogg
    , aktivSamtale : Samtale
    , personalia : Personalia
    , debugStatus : DebugStatus
    }


type Samtale
    = BekreftOriginal Personalia
    | EndreOriginal PersonaliaSkjema
    | LagrerEndring LagreStatus ValidertPersonaliaSkjema
    | LagringFeilet Http.Error ValidertPersonaliaSkjema
    | VenterP친AnimasjonF칮rFullf칮ring Personalia Fullf칮ringStatus


type LagreStatus
    = LagrerF칮rsteGang
    | LagrerP친NyttEtterError Http.Error
    | Fors칮k칀LagreP친NyttEtterDetteFors칮ket


type Fullf칮ringStatus
    = BekreftetOriginal
    | LagringLyktesF칮rsteGang
    | LagringLyktesEtterFlereFors칮k
    | BrukerGikkVidere


type SamtaleStatus
    = IkkeFerdig ( Model, Cmd Msg )
    | Ferdig Personalia FerdigAnimertMeldingsLogg


meldingsLogg : Model -> MeldingsLogg
meldingsLogg (Model model) =
    model.seksjonsMeldingsLogg



--- UPDATE ---


type Msg
    = OriginalPersonaliaBekreftet
    | BrukerVilEndreOriginalPersonalia
    | PersonaliaSkjemaEndret Skjema.Felt String
    | PersonaliaSkjemaFeltMistetFokus Skjema.Felt
    | PoststedHentet String (Result Http.Error Poststed)
    | PoststedfeltEndretSelvOmDetErDisabled String
    | PersonaliaskjemaLagreknappTrykket
    | PersonaliaOppdatert (Result Http.Error Personalia)
    | BrukerVilG친VidereUten칀Lagre
    | BrukerVilPr칮ve칀LagreP친Nytt
    | WindowEndrerVisibility Visibility
    | ViewportSatt (Result Dom.Error ())
    | Start칀Skrive
    | Fullf칮rMelding
    | ErrorLogget


update : Msg -> Model -> SamtaleStatus
update msg (Model model) =
    case msg of
        OriginalPersonaliaBekreftet ->
            BekreftetOriginal
                |> VenterP친AnimasjonF칮rFullf칮ring model.personalia
                |> nesteSamtaleSteg model (Melding.svar [ "Ja, informasjonen er riktig" ])
                |> fullf칮rSeksjonHvisMeldingsloggErFerdig model.personalia

        BrukerVilEndreOriginalPersonalia ->
            ( model.personalia
                |> Skjema.init
                |> EndreOriginal
                |> nesteSamtaleSteg model (Melding.svar [ "Nei, jeg vil endre" ])
            , lagtTilSp칮rsm친lCmd model.debugStatus
            )
                |> IkkeFerdig

        PersonaliaSkjemaEndret felt string ->
            case model.aktivSamtale of
                EndreOriginal skjema ->
                    ( Skjema.oppdaterFelt felt skjema string
                        |> EndreOriginal
                        |> oppdaterSamtaleSteg model
                    , case felt of
                        Skjema.Postnummer ->
                            if String.length string == 4 && String.toInt string /= Nothing then
                                Api.hentPoststed (PoststedHentet string) string

                            else
                                Cmd.none

                        _ ->
                            Cmd.none
                    )
                        |> IkkeFerdig

                _ ->
                    ( Model model, Cmd.none )
                        |> IkkeFerdig

        PersonaliaSkjemaFeltMistetFokus felt ->
            case model.aktivSamtale of
                EndreOriginal skjema ->
                    ( felt
                        |> Skjema.gj칮rFeilmeldingSynligForFelt skjema
                        |> EndreOriginal
                        |> oppdaterSamtaleSteg model
                    , Cmd.none
                    )
                        |> IkkeFerdig

                _ ->
                    ( Model model, Cmd.none )
                        |> IkkeFerdig

        PoststedHentet postnummer result ->
            case result of
                Ok poststed ->
                    case model.aktivSamtale of
                        EndreOriginal skjema ->
                            ( skjema
                                |> Skjema.oppdaterPoststed poststed
                                |> EndreOriginal
                                |> oppdaterSamtaleSteg model
                            , Cmd.none
                            )
                                |> IkkeFerdig

                        _ ->
                            IkkeFerdig ( Model model, Cmd.none )

                Err error ->
                    case error of
                        Http.BadStatus 404 ->
                            IkkeFerdig ( Model model, Cmd.none )

                        _ ->
                            IkkeFerdig
                                ( Model model
                                , error
                                    |> Feilmelding.feilmelding ("Hent poststed for postnummer: \"" ++ postnummer ++ "\"")
                                    |> Maybe.map (Api.logError (always ErrorLogget))
                                    |> Maybe.withDefault Cmd.none
                                )

        PoststedfeltEndretSelvOmDetErDisabled _ ->
            IkkeFerdig ( Model model, Cmd.none )

        PersonaliaskjemaLagreknappTrykket ->
            case model.aktivSamtale of
                EndreOriginal skjema ->
                    case Skjema.validerSkjema skjema of
                        Just validertSkjema ->
                            ( validertSkjema
                                |> LagrerEndring LagrerF칮rsteGang
                                |> nesteSamtaleSteg model
                                    (skjema
                                        |> personaliaSkjemaOppsummering
                                        |> Melding.svar
                                    )
                            , Cmd.batch
                                [ model.personalia
                                    |> Personalia.id
                                    |> Api.putPersonalia PersonaliaOppdatert validertSkjema
                                , lagtTilSp칮rsm친lCmd model.debugStatus
                                ]
                            )
                                |> IkkeFerdig

                        Nothing ->
                            ( skjema
                                |> Skjema.gj칮rAlleFeilmeldingerSynlig
                                |> EndreOriginal
                                |> oppdaterSamtaleSteg model
                            , Cmd.none
                            )
                                |> IkkeFerdig

                _ ->
                    ( Model model, Cmd.none )
                        |> IkkeFerdig

        PersonaliaOppdatert result ->
            case model.aktivSamtale of
                LagrerEndring lagreStatus skjema ->
                    case result of
                        Ok personalia ->
                            if lagringFeiletTidligereP친GrunnAvInnlogging lagreStatus then
                                lagreStatus
                                    |> fullf칮rtStatusEtterOkLagring
                                    |> VenterP친AnimasjonF칮rFullf칮ring personalia
                                    |> nesteSamtaleSteg model (Melding.svar [ "Ja, jeg vil logge inn" ])
                                    |> fullf칮rSeksjonHvisMeldingsloggErFerdig personalia

                            else
                                lagreStatus
                                    |> fullf칮rtStatusEtterOkLagring
                                    |> VenterP친AnimasjonF칮rFullf칮ring personalia
                                    |> nesteSamtaleStegUtenSvar model
                                    |> fullf칮rSeksjonHvisMeldingsloggErFerdig personalia

                        Err error ->
                            case lagreStatus of
                                LagrerP친NyttEtterError (BadStatus 401) ->
                                    ( skjema
                                        |> LagringFeilet error
                                        |> oppdaterSamtaleSteg model
                                    , Cmd.none
                                    )
                                        |> IkkeFerdig

                                Fors칮k칀LagreP친NyttEtterDetteFors칮ket ->
                                    ( skjema
                                        |> LagrerEndring (LagrerP친NyttEtterError error)
                                        |> oppdaterSamtaleSteg model
                                    , model.personalia
                                        |> Personalia.id
                                        |> Api.putPersonalia PersonaliaOppdatert skjema
                                    )
                                        |> IkkeFerdig

                                _ ->
                                    ( skjema
                                        |> LagringFeilet error
                                        |> nesteSamtaleStegUtenSvar model
                                    , Cmd.batch
                                        [ lagtTilSp칮rsm친lCmd model.debugStatus
                                        , model.personalia
                                            |> Personalia.id
                                            |> Json.Encode.string
                                            |> Tuple.pair "id"
                                            |> List.singleton
                                            |> Json.Encode.object
                                            |> Api.logErrorWithRequestBody ErrorLogget "Lagre personalia" error
                                        ]
                                    )
                                        |> IkkeFerdig

                _ ->
                    IkkeFerdig ( Model model, Cmd.none )

        BrukerVilPr칮ve칀LagreP친Nytt ->
            case model.aktivSamtale of
                LagringFeilet error skjema ->
                    ( skjema
                        |> LagrerEndring (LagrerP친NyttEtterError error)
                        |> nesteSamtaleSteg model (Melding.svar [ "Pr칮v p친 nytt" ])
                    , Cmd.batch
                        [ model.personalia
                            |> Personalia.id
                            |> Api.putPersonalia PersonaliaOppdatert skjema
                        , lagtTilSp칮rsm친lCmd model.debugStatus
                        ]
                    )
                        |> IkkeFerdig

                _ ->
                    IkkeFerdig ( Model model, Cmd.none )

        BrukerVilG친VidereUten칀Lagre ->
            VenterP친AnimasjonF칮rFullf칮ring model.personalia BrukerGikkVidere
                |> nesteSamtaleSteg model (Melding.svar [ "G친 videre" ])
                |> fullf칮rSeksjonHvisMeldingsloggErFerdig model.personalia

        WindowEndrerVisibility visibility ->
            case visibility of
                Visible ->
                    case model.aktivSamtale of
                        LagringFeilet ((BadStatus 401) as error) skjema ->
                            ( skjema
                                |> LagrerEndring (LagrerP친NyttEtterError error)
                                |> nesteSamtaleStegUtenSvar model
                            , model.personalia
                                |> Personalia.id
                                |> Api.putPersonalia PersonaliaOppdatert skjema
                            )
                                |> IkkeFerdig

                        LagrerEndring (LagrerP친NyttEtterError (BadStatus 401)) skjema ->
                            ( skjema
                                |> LagrerEndring Fors칮k칀LagreP친NyttEtterDetteFors칮ket
                                |> oppdaterSamtaleSteg model
                            , Cmd.none
                            )
                                |> IkkeFerdig

                        _ ->
                            IkkeFerdig ( Model model, Cmd.none )

                Hidden ->
                    IkkeFerdig ( Model model, Cmd.none )

        ViewportSatt _ ->
            IkkeFerdig ( Model model, Cmd.none )

        Start칀Skrive ->
            ( Model
                { model
                    | seksjonsMeldingsLogg =
                        MeldingsLogg.start칀Skrive model.seksjonsMeldingsLogg
                }
            , Cmd.batch
                [ SamtaleAnimasjon.scrollTilBunn ViewportSatt
                , MeldingsLogg.nesteMeldingToString model.seksjonsMeldingsLogg
                    * 1000.0
                    |> DebugStatus.meldingsTimeout model.debugStatus
                    |> Process.sleep
                    |> Task.perform (always Fullf칮rMelding)
                ]
            )
                |> IkkeFerdig

        Fullf칮rMelding ->
            model.seksjonsMeldingsLogg
                |> MeldingsLogg.fullf칮rMelding
                |> updateEtterFullf칮rtMelding model

        ErrorLogget ->
            IkkeFerdig ( Model model, Cmd.none )


fullf칮rtStatusEtterOkLagring : LagreStatus -> Fullf칮ringStatus
fullf칮rtStatusEtterOkLagring lagreStatus =
    case lagreStatus of
        LagrerF칮rsteGang ->
            LagringLyktesF칮rsteGang

        LagrerP친NyttEtterError error ->
            LagringLyktesEtterFlereFors칮k

        Fors칮k칀LagreP친NyttEtterDetteFors칮ket ->
            LagringLyktesEtterFlereFors칮k


updateEtterFullf칮rtMelding : ModelInfo -> MeldingsLogg -> SamtaleStatus
updateEtterFullf칮rtMelding model nyMeldingsLogg =
    case MeldingsLogg.ferdigAnimert nyMeldingsLogg of
        FerdigAnimert ferdigAnimertSamtale ->
            case model.aktivSamtale of
                VenterP친AnimasjonF칮rFullf칮ring personalia _ ->
                    Ferdig personalia ferdigAnimertSamtale

                _ ->
                    ( Model
                        { model
                            | seksjonsMeldingsLogg =
                                nyMeldingsLogg
                        }
                    , SamtaleAnimasjon.scrollTilBunn ViewportSatt
                    )
                        |> IkkeFerdig

        MeldingerGjenst친r ->
            ( Model
                { model
                    | seksjonsMeldingsLogg =
                        nyMeldingsLogg
                }
            , lagtTilSp칮rsm친lCmd model.debugStatus
            )
                |> IkkeFerdig


fullf칮rSeksjonHvisMeldingsloggErFerdig : Personalia -> Model -> SamtaleStatus
fullf칮rSeksjonHvisMeldingsloggErFerdig personalia (Model model) =
    case MeldingsLogg.ferdigAnimert model.seksjonsMeldingsLogg of
        FerdigAnimert ferdigAnimertMeldingsLogg ->
            Ferdig personalia ferdigAnimertMeldingsLogg

        MeldingerGjenst친r ->
            ( Model model, lagtTilSp칮rsm친lCmd model.debugStatus )
                |> IkkeFerdig


lagtTilSp칮rsm친lCmd : DebugStatus -> Cmd Msg
lagtTilSp칮rsm친lCmd debugStatus =
    Cmd.batch
        [ SamtaleAnimasjon.scrollTilBunn ViewportSatt
        , 200
            |> DebugStatus.meldingsTimeout debugStatus
            |> Process.sleep
            |> Task.perform (always Start칀Skrive)
        ]


nesteSamtaleSteg : ModelInfo -> Melding -> Samtale -> Model
nesteSamtaleSteg model melding samtaleSeksjon =
    Model
        { model
            | aktivSamtale = samtaleSeksjon
            , seksjonsMeldingsLogg =
                model.seksjonsMeldingsLogg
                    |> MeldingsLogg.leggTilSvar melding
                    |> MeldingsLogg.leggTilSp칮rsm친l (samtaleTilMeldingsLogg samtaleSeksjon)
        }


nesteSamtaleStegUtenSvar : ModelInfo -> Samtale -> Model
nesteSamtaleStegUtenSvar model samtaleSeksjon =
    Model
        { model
            | aktivSamtale = samtaleSeksjon
            , seksjonsMeldingsLogg =
                model.seksjonsMeldingsLogg
                    |> MeldingsLogg.leggTilSp칮rsm친l (samtaleTilMeldingsLogg samtaleSeksjon)
        }


oppdaterSamtaleSteg : ModelInfo -> Samtale -> Model
oppdaterSamtaleSteg model samtaleSeksjon =
    Model
        { model
            | aktivSamtale = samtaleSeksjon
        }


lagringFeiletTidligereP친GrunnAvInnlogging : LagreStatus -> Bool
lagringFeiletTidligereP친GrunnAvInnlogging lagreStatus =
    case lagreStatus of
        LagrerF칮rsteGang ->
            False

        LagrerP친NyttEtterError error ->
            ErrorMelding.errorOperasjon error == LoggInn

        Fors칮k칀LagreP친NyttEtterDetteFors칮ket ->
            True


samtaleTilMeldingsLogg : Samtale -> List Melding
samtaleTilMeldingsLogg personaliaSeksjon =
    case personaliaSeksjon of
        BekreftOriginal personalia ->
            [ Melding.sp칮rsm친l [ "Da setter vi i gang 游땕" ]
            , Melding.sp칮rsm친l [ "Jeg har hentet inn kontaktinformasjonen din, den vises p친 CV-en. Det er viktig at informasjonen er riktig, slik at arbeidsgivere kan kontakte deg." ]
            , Melding.sp칮rsm친l
                (List.concat
                    [ personalia
                        |> Skjema.init
                        |> personaliaSkjemaOppsummering
                    , [ Melding.tomLinje
                      , "Er kontaktinformasjonen riktig?"
                      ]
                    ]
                )
            ]

        EndreOriginal _ ->
            [ Melding.sp칮rsm친l [ "Ok! Vennligst skriv inn riktig informasjon i feltene under:" ] ]

        LagrerEndring _ _ ->
            []

        LagringFeilet error _ ->
            [ ErrorMelding.errorMelding { operasjon = "lagre kontaktinformasjonen", error = error } ]

        VenterP친AnimasjonF칮rFullf칮ring _ fullf칮ringStatus ->
            case fullf칮ringStatus of
                BekreftetOriginal ->
                    [ Melding.sp칮rsm친l [ "S친 bra! 游땕 N친 kan arbeidsgivere kontakte deg." ]
                    , Melding.sp칮rsm친l [ "Da kan vi g친 videre til utfylling av CV-en." ]
                    ]

                LagringLyktesF칮rsteGang ->
                    [ Melding.sp칮rsm친l [ "Godt jobbet! Da tar jeg vare p친 den nye infoen!" ] ]

                LagringLyktesEtterFlereFors칮k ->
                    [ Melding.sp칮rsm친l [ "Supert! N친 fikk jeg det til. Kontaktinformasjonen er lagret. La oss fortsette 游땕" ] ]

                BrukerGikkVidere ->
                    [ Melding.sp칮rsm친l [ "Da g친r vi videre" ] ]


personaliaSkjemaOppsummering : PersonaliaSkjema -> List String
personaliaSkjemaOppsummering skjema =
    [ "Navn: " ++ Skjema.fornavn skjema ++ " " ++ Skjema.etternavn skjema
    , "F칮dselsdato: " ++ (skjema |> Skjema.fodselsdato |> viewDatoString)
    , "E-post: " ++ Skjema.epost skjema
    , "Telefonnummer: " ++ Skjema.telefon skjema
    , "Adresse: "
        ++ Skjema.gateadresse skjema
        ++ " "
        ++ Skjema.postnummer skjema
        ++ " "
        ++ Skjema.poststed skjema
    ]


viewDatoString : String -> String
viewDatoString datoString =
    case String.split "-" datoString of
        친r :: m친ned :: dag :: [] ->
            dag ++ "." ++ m친ned ++ "." ++ 친r

        _ ->
            datoString



--- VIEW ---


viewBrukerInput : Model -> Html Msg
viewBrukerInput (Model { aktivSamtale, seksjonsMeldingsLogg }) =
    case MeldingsLogg.ferdigAnimert seksjonsMeldingsLogg of
        FerdigAnimert _ ->
            case aktivSamtale of
                BekreftOriginal _ ->
                    Containers.knapper Flytende
                        [ Knapp.knapp OriginalPersonaliaBekreftet "Ja, informasjonen er riktig"
                            |> Knapp.toHtml
                        , Knapp.knapp BrukerVilEndreOriginalPersonalia "Nei, jeg vil endre"
                            |> Knapp.toHtml
                        ]

                EndreOriginal personaliaSkjema ->
                    Containers.skjema { lagreMsg = PersonaliaskjemaLagreknappTrykket, lagreKnappTekst = "Lagre endringer" }
                        [ personaliaSkjema
                            |> Skjema.fornavn
                            |> Input.input { label = "Fornavn*", msg = PersonaliaSkjemaEndret Skjema.Fornavn }
                            |> Input.withMaybeFeilmelding (Skjema.fornavnFeilmelding personaliaSkjema)
                            |> Input.withOnBlur (PersonaliaSkjemaFeltMistetFokus Skjema.Fornavn)
                            |> Input.toHtml
                        , personaliaSkjema
                            |> Skjema.etternavn
                            |> Input.input { label = "Etternavn*", msg = PersonaliaSkjemaEndret Skjema.Etternavn }
                            |> Input.withMaybeFeilmelding (Skjema.etternavnFeilmelding personaliaSkjema)
                            |> Input.withOnBlur (PersonaliaSkjemaFeltMistetFokus Skjema.Etternavn)
                            |> Input.toHtml
                        , personaliaSkjema
                            |> Skjema.epost
                            |> Input.input { label = "E-post*", msg = PersonaliaSkjemaEndret Skjema.Epost }
                            |> Input.withMaybeFeilmelding (Skjema.epostFeilmelding personaliaSkjema)
                            |> Input.withOnBlur (PersonaliaSkjemaFeltMistetFokus Skjema.Epost)
                            |> Input.toHtml
                        , viewTelefonISkjema personaliaSkjema
                        , personaliaSkjema
                            |> Skjema.gateadresse
                            |> Input.input { label = "Gateadresse", msg = PersonaliaSkjemaEndret Skjema.Gateadresse }
                            |> Input.toHtml
                        , div [ class "PersonaliaSeksjon-poststed" ]
                            [ personaliaSkjema
                                |> Skjema.postnummer
                                |> Input.input { label = "Postnummer", msg = PersonaliaSkjemaEndret Skjema.Postnummer }
                                |> Input.withMaybeFeilmelding (Skjema.postnummerFeilmelding personaliaSkjema)
                                |> Input.withOnBlur (PersonaliaSkjemaFeltMistetFokus Skjema.Postnummer)
                                |> Input.toHtml
                            , personaliaSkjema
                                |> Skjema.poststed
                                |> Input.input { label = "Poststed", msg = PoststedfeltEndretSelvOmDetErDisabled }
                                |> Input.withEnabled Input.Disabled
                                |> Input.toHtml
                            ]
                        ]

                -- Lenken for 친 logge seg inn skal alltid v칝re synlig hvis man har blitt utlogget, selv under lagring
                LagrerEndring error _ ->
                    if lagringFeiletTidligereP친GrunnAvInnlogging error then
                        viewLoggInnLenke

                    else
                        text ""

                LagringFeilet error _ ->
                    case ErrorMelding.errorOperasjon error of
                        GiOpp ->
                            Containers.knapper Flytende
                                [ Knapp.knapp BrukerVilG친VidereUten칀Lagre "G친 videre"
                                    |> Knapp.toHtml
                                ]

                        Pr칮vP친Nytt ->
                            Containers.knapper Flytende
                                [ Knapp.knapp BrukerVilPr칮ve칀LagreP친Nytt "Pr칮v p친 nytt"
                                    |> Knapp.toHtml
                                , Knapp.knapp BrukerVilG친VidereUten칀Lagre "G친 videre"
                                    |> Knapp.toHtml
                                ]

                        LoggInn ->
                            viewLoggInnLenke

                VenterP친AnimasjonF칮rFullf칮ring _ _ ->
                    text ""

        MeldingerGjenst친r ->
            text ""


viewLoggInnLenke : Html msg
viewLoggInnLenke =
    Containers.lenke
        (Lenke.lenke { tekst = "Ja, jeg vil logge inn ", url = "/cv-samtale/login?redirect=/logget-inn" }
            |> Lenke.withTargetBlank
            |> Lenke.toHtml
        )


viewTelefonISkjema : PersonaliaSkjema -> Html Msg
viewTelefonISkjema personaliaSkjema =
    div [ class "skjemaelement" ]
        [ label []
            [ span [ class "skjemaelement__label" ] [ text "Telefon*" ]
            , div [ class "PersonaliaSeksjon--telefonnummer" ]
                [ p [ class "PersonaliaSeksjon--telefonnummer--country-code" ] [ text "+47" ]
                , input
                    [ value (Skjema.telefon personaliaSkjema)
                    , onInput (PersonaliaSkjemaEndret Skjema.Telefon)
                    , onBlur (PersonaliaSkjemaFeltMistetFokus Skjema.Telefon)
                    , classList
                        [ ( "skjemaelement__input", True )
                        , ( "PersonaliaSeksjon--telefonnummer--input", True )
                        , ( "skjemaelement__input--harFeil", Skjema.telefonFeilmelding personaliaSkjema /= Nothing )
                        ]
                    ]
                    []
                ]
            , case Skjema.telefonFeilmelding personaliaSkjema of
                Just feilmelding ->
                    div [ class "skjemaelement__feilmelding" ] [ text feilmelding ]

                Nothing ->
                    text ""
            ]
        ]



--- INIT ---


init : DebugStatus -> Personalia -> MeldingsLogg -> ( Model, Cmd Msg )
init debugStatus personalia gammelMeldingsLogg =
    let
        aktivSamtale =
            BekreftOriginal personalia
    in
    ( Model
        { seksjonsMeldingsLogg =
            MeldingsLogg.leggTilSp칮rsm친l
                (samtaleTilMeldingsLogg aktivSamtale)
                gammelMeldingsLogg
        , aktivSamtale = aktivSamtale
        , personalia = personalia
        , debugStatus = debugStatus
        }
    , lagtTilSp칮rsm친lCmd debugStatus
    )


subscriptions : Model -> Sub Msg
subscriptions model =
    Browser.Events.onVisibilityChange WindowEndrerVisibility
