module Utdanning.Seksjon exposing
    ( Model
    , Msg
    , SamtaleStatus(..)
    , init
    , meldingsLogg
    , subscriptions
    , update
    , viewBrukerInput
    )

import Api
import Browser.Dom as Dom
import Browser.Events exposing (Visibility(..))
import Cv.Utdanning as Utdanning exposing (Niv친(..), Utdanning)
import Dato.Dato as Dato exposing (TilDato(..), 칀r)
import Dato.Maned as M친ned exposing (M친ned(..))
import DebugStatus exposing (DebugStatus)
import ErrorHandtering as ErrorH친ndtering exposing (OperasjonEtterError(..))
import FrontendModuler.BrukerInput as BrukerInput exposing (BrukerInput, KnapperLayout(..))
import FrontendModuler.BrukerInputMedGaVidereKnapp as BrukerInputMedG친VidereKnapp
import FrontendModuler.Checkbox as Checkbox
import FrontendModuler.DatoInput as DatoInput
import FrontendModuler.Input as Input
import FrontendModuler.Knapp as Knapp exposing (Knapp, Type(..))
import FrontendModuler.LoggInnLenke as LoggInnLenke
import FrontendModuler.Select as Select
import FrontendModuler.Textarea as Textarea
import Html exposing (..)
import Html.Attributes exposing (..)
import Http exposing (Error)
import LagreStatus exposing (LagreStatus)
import Meldinger.Melding as Melding exposing (Melding, Tekstomr친de(..))
import Meldinger.MeldingsLogg as MeldingsLogg exposing (FerdigAnimertMeldingsLogg, FerdigAnimertStatus(..), MeldingsLogg)
import Meldinger.SamtaleAnimasjon as SamtaleAnimasjon
import Meldinger.SamtaleOppdatering exposing (SamtaleOppdatering(..))
import Process
import Task
import Utdanning.Skjema as Skjema exposing (Felt(..), UtdanningSkjema, ValidertUtdanningSkjema)
import Validering



--- MODEL ---


type Model
    = Model ModelInfo


type alias ModelInfo =
    { seksjonsMeldingsLogg : MeldingsLogg
    , aktivSamtale : Samtale
    , utdanningListe : List Utdanning
    , debugStatus : DebugStatus
    }


type AvsluttetGrunn
    = AvbruttP친begynt
    | SlettetP친begynt
    | EndretEksisterende
    | AnnenAvslutning


type OppsummeringsType
    = F칮rsteGang
    | EtterEndring
    | Avbr칮tSletting


type Samtale
    = Intro (List Utdanning)
    | VelgEnUtdanning칀Redigere
    | RegistrerNiv친
    | RegistrerSkole SkoleInfo
    | RegistrerRetning RetningInfo
    | RegistrerBeskrivelse Bool BeskrivelseInfo
    | RegistrereFraM친ned FraDatoInfo
    | RegistrereFra칀r FraDatoInfo
    | RegistrereN친v칝rende N친v칝rendeInfo
    | RegistrereTilM친ned TilDatoInfo
    | RegistrereTil칀r TilDatoInfo
    | Oppsummering OppsummeringsType ValidertUtdanningSkjema
    | EndrerOppsummering UtdanningSkjema
    | BekreftSlettingAvP친begynt ValidertUtdanningSkjema
    | LagrerSkjema ValidertUtdanningSkjema LagreStatus
    | LagringFeilet Http.Error ValidertUtdanningSkjema
    | LeggTilFlereUtdanninger (List Utdanning) AvsluttetGrunn
    | BekreftAvbrytingAvRegistreringen Samtale
    | VenterP친AnimasjonF칮rFullf칮ring (List Utdanning) AvsluttetGrunn


type SamtaleStatus
    = IkkeFerdig ( Model, Cmd Msg )
    | Ferdig (List Utdanning) FerdigAnimertMeldingsLogg


meldingsLogg : Model -> MeldingsLogg
meldingsLogg (Model model) =
    model.seksjonsMeldingsLogg


type alias SkoleInfo =
    { forrige : Niv친, skole : String }


type alias RetningInfo =
    { forrige : SkoleInfo, retning : String }


type alias BeskrivelseInfo =
    { forrige : RetningInfo, beskrivelse : String }


type alias FraDatoInfo =
    { forrige : BeskrivelseInfo
    , fraM친ned : M친ned
    , fra칀r : String
    , vis칀rFeilmelding : Bool
    }


type alias N친v칝rendeInfo =
    { forrige : BeskrivelseInfo
    , fraM친ned : M친ned
    , fra칀r : 칀r
    }


type alias TilDatoInfo =
    { forrige : N친v칝rendeInfo
    , tilM친ned : M친ned
    , til칀r : String
    , vis칀rFeilmelding : Bool
    }


maxLengthBeskrivelse =
    2000


forrigeTilRetningInfo : SkoleInfo -> RetningInfo
forrigeTilRetningInfo skole =
    { forrige = skole, retning = "" }


forrigeTilSkoleInfo : Niv친 -> SkoleInfo
forrigeTilSkoleInfo niv친 =
    { forrige = niv친, skole = "" }


forrigeTilBeskrivelseInfo : RetningInfo -> BeskrivelseInfo
forrigeTilBeskrivelseInfo retning =
    { forrige = retning, beskrivelse = "" }


forrigeTilFradatoInfo : BeskrivelseInfo -> FraDatoInfo
forrigeTilFradatoInfo beskrivelse =
    { forrige = beskrivelse
    , fraM친ned = Januar
    , fra칀r = ""
    , vis칀rFeilmelding = False
    }


forrigeTilTildatoInfo : N친v칝rendeInfo -> TilDatoInfo
forrigeTilTildatoInfo n친v칝rendeInfo =
    { forrige = n친v칝rendeInfo
    , tilM친ned = Januar
    , til칀r = ""
    , vis칀rFeilmelding = False
    }


forrigeTilOppsummeringInfo : TilDatoInfo -> 칀r -> ValidertUtdanningSkjema
forrigeTilOppsummeringInfo tildatoInfo til칀r =
    Skjema.initValidertSkjema
        { niv친 = tildatoInfo.forrige.forrige.forrige.forrige.forrige
        , studiested = tildatoInfo.forrige.forrige.forrige.forrige.skole
        , utdanningsretning = tildatoInfo.forrige.forrige.forrige.retning
        , beskrivelse = tildatoInfo.forrige.forrige.beskrivelse
        , fraM친ned = tildatoInfo.forrige.fraM친ned
        , fra칀r = tildatoInfo.forrige.fra칀r
        , tilDato = Avsluttet tildatoInfo.tilM친ned til칀r
        , id = Nothing
        }


n친v칝rendeInfoTilUtdanningsSkjema : N친v칝rendeInfo -> ValidertUtdanningSkjema
n친v칝rendeInfoTilUtdanningsSkjema n친v칝rendeInfo =
    Skjema.initValidertSkjema
        { niv친 = n친v칝rendeInfo.forrige.forrige.forrige.forrige
        , studiested = n친v칝rendeInfo.forrige.forrige.forrige.skole
        , utdanningsretning = n친v칝rendeInfo.forrige.forrige.retning
        , beskrivelse = n친v칝rendeInfo.forrige.beskrivelse
        , fraM친ned = n친v칝rendeInfo.fraM친ned
        , fra칀r = n친v칝rendeInfo.fra칀r
        , tilDato = N친v칝rende
        , id = Nothing
        }



--- UPDATE ---


type Msg
    = BrukerVilRegistrereUtdanning
    | BrukerVilRedigereUtdanning
    | BrukerHarValgtUtdanning칀Redigere Utdanning
    | G친TilArbeidserfaring AvsluttetGrunn
    | BrukerVilRegistrereNiv친 Niv친
    | OppdaterSkole String
    | BrukerVilRegistrereSkole
    | OppdaterRetning String
    | BrukerVilRegistrereRetning
    | OppdaterBeskrivelse String
    | VilSeEksempel
    | BrukerVilRegistrereBeskrivelse
    | BrukerTrykketFraM친nedKnapp M친ned
    | OppdaterFra칀r String
    | BrukerVilG친VidereMedFra칀r
    | BrukerSvarerJaTilNaavarende
    | BrukerSvarerNeiTilNaavarende
    | BrukerTrykketTilM친nedKnapp M친ned
    | OppdaterTil칀r String
    | BrukerVilG친TilOppsummering
    | BrukerVilEndreOppsummering
    | VilSletteP친begynt
    | BekrefterSlettP친begynt
    | AngrerSlettP친begynt
    | OppsummeringBekreftet
    | OppsummeringEndret SkjemaEndring
    | OppsummeringSkjemaLagreknappTrykket
    | UtdanningSendtTilApi (Result Http.Error (List Utdanning))
    | BrukerVilPr칮ve칀LagreP친Nytt
    | BrukerVilAvbryteLagringen
    | BrukerVilAvbryteRegistreringen
    | BrukerBekrefterAvbrytingAvRegistrering
    | BrukerVilIkkeAvbryteRegistreringen
    | WindowEndrerVisibility Visibility
    | SamtaleAnimasjonMsg SamtaleAnimasjon.Msg
    | FokusSatt (Result Dom.Error ())
    | FeltMisterFokus
    | TimeoutEtterAtFeltMistetFokus
    | ErrorLogget


type SkjemaEndring
    = Tekst Felt String
    | N친v칝rendeToggled
    | FraM친ned String
    | TilM친ned String
    | Niv친 String
    | Fra칀rBlurred
    | Til칀rBlurred


update : Msg -> Model -> SamtaleStatus
update msg (Model model) =
    case msg of
        BrukerVilRegistrereUtdanning ->
            IkkeFerdig
                ( RegistrerNiv친
                    |> oppdaterSamtale model (SvarFraMsg msg)
                , lagtTilSp칮rsm친lCmd model.debugStatus
                )

        BrukerVilRedigereUtdanning ->
            case model.utdanningListe of
                enesteUtdanning :: [] ->
                    ( enesteUtdanning
                        |> Skjema.fraUtdanning
                        |> EndrerOppsummering
                        |> oppdaterSamtale model (SvarFraMsg msg)
                    , lagtTilSp칮rsm친lCmd model.debugStatus
                    )
                        |> IkkeFerdig

                [] ->
                    IkkeFerdig ( Model model, Cmd.none )

                _ ->
                    ( VelgEnUtdanning칀Redigere
                        |> oppdaterSamtale model (SvarFraMsg msg)
                    , lagtTilSp칮rsm친lCmd model.debugStatus
                    )
                        |> IkkeFerdig

        BrukerHarValgtUtdanning칀Redigere utdanning ->
            ( utdanning
                |> Skjema.fraUtdanning
                |> EndrerOppsummering
                |> oppdaterSamtale model (SvarFraMsg msg)
            , lagtTilSp칮rsm친lCmd model.debugStatus
            )
                |> IkkeFerdig

        G친TilArbeidserfaring avsluttetGrunn ->
            ( VenterP친AnimasjonF칮rFullf칮ring model.utdanningListe avsluttetGrunn
                |> oppdaterSamtale model (SvarFraMsg msg)
            , lagtTilSp칮rsm친lCmd model.debugStatus
            )
                |> IkkeFerdig

        BrukerVilRegistrereNiv친 niv친 ->
            IkkeFerdig
                ( niv친
                    |> forrigeTilSkoleInfo
                    |> RegistrerSkole
                    |> oppdaterSamtale model (SvarFraMsg msg)
                , lagtTilSp칮rsm친lCmd model.debugStatus
                )

        OppdaterSkole skole ->
            case model.aktivSamtale of
                RegistrerSkole skoleinfo ->
                    IkkeFerdig
                        ( { skoleinfo | skole = skole }
                            |> RegistrerSkole
                            |> oppdaterSamtale model IngenNyeMeldinger
                        , Cmd.none
                        )

                _ ->
                    IkkeFerdig ( Model model, Cmd.none )

        BrukerVilRegistrereSkole ->
            case model.aktivSamtale of
                RegistrerSkole skoleinfo ->
                    IkkeFerdig
                        ( skoleinfo
                            |> forrigeTilRetningInfo
                            |> RegistrerRetning
                            |> oppdaterSamtale model (SvarFraMsg msg)
                        , lagtTilSp칮rsm친lCmd model.debugStatus
                        )

                _ ->
                    IkkeFerdig ( Model model, Cmd.none )

        OppdaterRetning retning ->
            case model.aktivSamtale of
                RegistrerRetning retningsinfo ->
                    IkkeFerdig
                        ( { retningsinfo | retning = retning }
                            |> RegistrerRetning
                            |> oppdaterSamtale model IngenNyeMeldinger
                        , Cmd.none
                        )

                _ ->
                    IkkeFerdig ( Model model, Cmd.none )

        BrukerVilRegistrereRetning ->
            case model.aktivSamtale of
                RegistrerRetning retninginfo ->
                    IkkeFerdig
                        ( retninginfo
                            |> forrigeTilBeskrivelseInfo
                            |> RegistrerBeskrivelse (detFinnesEksemplerForNiv친 retninginfo.forrige.forrige)
                            |> oppdaterSamtale model (SvarFraMsg msg)
                        , lagtTilSp칮rsm친lCmd model.debugStatus
                        )

                _ ->
                    IkkeFerdig ( Model model, Cmd.none )

        VilSeEksempel ->
            case model.aktivSamtale of
                RegistrerBeskrivelse _ beskrivelseinfo ->
                    let
                        oppdatertMeldingslogg =
                            model.seksjonsMeldingsLogg
                                |> MeldingsLogg.leggTilSvar (svarFraBrukerInput model msg)
                                |> MeldingsLogg.leggTilSp칮rsm친l (eksemplerP친Utdanning beskrivelseinfo.forrige.forrige.forrige)
                    in
                    IkkeFerdig
                        ( beskrivelseinfo
                            |> RegistrerBeskrivelse False
                            |> oppdaterSamtale { model | seksjonsMeldingsLogg = oppdatertMeldingslogg } IngenNyeMeldinger
                        , lagtTilSp칮rsm친lCmd model.debugStatus
                        )

                _ ->
                    IkkeFerdig ( Model model, Cmd.none )

        OppdaterBeskrivelse beskrivelse ->
            case model.aktivSamtale of
                RegistrerBeskrivelse medEksempelKnapp beskrivelseinfo ->
                    IkkeFerdig
                        ( { beskrivelseinfo | beskrivelse = beskrivelse }
                            |> RegistrerBeskrivelse medEksempelKnapp
                            |> oppdaterSamtale model IngenNyeMeldinger
                        , Cmd.none
                        )

                _ ->
                    IkkeFerdig ( Model model, Cmd.none )

        BrukerVilRegistrereBeskrivelse ->
            case model.aktivSamtale of
                RegistrerBeskrivelse _ beskrivelseinfo ->
                    case Validering.feilmeldingMaxAntallTegn beskrivelseinfo.beskrivelse maxLengthBeskrivelse of
                        Nothing ->
                            IkkeFerdig
                                ( { beskrivelseinfo | beskrivelse = String.trim beskrivelseinfo.beskrivelse }
                                    |> forrigeTilFradatoInfo
                                    |> RegistrereFraM친ned
                                    |> oppdaterSamtale model (SvarFraMsg msg)
                                , lagtTilSp칮rsm친lCmd model.debugStatus
                                )

                        Just _ ->
                            IkkeFerdig ( Model model, Cmd.none )

                _ ->
                    IkkeFerdig ( Model model, Cmd.none )

        BrukerTrykketFraM친nedKnapp m친ned ->
            case model.aktivSamtale of
                RegistrereFraM친ned fraDatoInfo ->
                    ( { fraDatoInfo | fraM친ned = m친ned }
                        |> RegistrereFra칀r
                        |> oppdaterSamtale model (SvarFraMsg msg)
                    , lagtTilSp칮rsm친lCmd model.debugStatus
                    )
                        |> IkkeFerdig

                _ ->
                    IkkeFerdig ( Model model, Cmd.none )

        OppdaterFra칀r string ->
            case model.aktivSamtale of
                RegistrereFra칀r fraDatoInfo ->
                    ( { fraDatoInfo | fra칀r = string }
                        |> RegistrereFra칀r
                        |> oppdaterSamtale model IngenNyeMeldinger
                    , Cmd.none
                    )
                        |> IkkeFerdig

                _ ->
                    IkkeFerdig ( Model model, Cmd.none )

        BrukerVilG친VidereMedFra칀r ->
            case model.aktivSamtale of
                RegistrereFra칀r datoInfo ->
                    case Dato.stringTil칀r datoInfo.fra칀r of
                        Just fra칀r ->
                            ( { forrige = datoInfo.forrige
                              , fraM친ned = datoInfo.fraM친ned
                              , fra칀r = fra칀r
                              }
                                |> RegistrereN친v칝rende
                                |> oppdaterSamtale model (SvarFraMsg msg)
                            , lagtTilSp칮rsm친lCmd model.debugStatus
                            )
                                |> IkkeFerdig

                        Nothing ->
                            IkkeFerdig
                                ( { datoInfo | vis칀rFeilmelding = True }
                                    |> RegistrereFra칀r
                                    |> oppdaterSamtale model IngenNyeMeldinger
                                , Cmd.none
                                )

                _ ->
                    IkkeFerdig ( Model model, Cmd.none )

        BrukerSvarerJaTilNaavarende ->
            case model.aktivSamtale of
                RegistrereN친v칝rende n친v칝rendeInfo ->
                    ( n친v칝rendeInfo
                        |> n친v칝rendeInfoTilUtdanningsSkjema
                        |> Oppsummering F칮rsteGang
                        |> oppdaterSamtale model (SvarFraMsg msg)
                    , lagtTilSp칮rsm친lCmd model.debugStatus
                    )
                        |> IkkeFerdig

                _ ->
                    IkkeFerdig ( Model model, Cmd.none )

        BrukerSvarerNeiTilNaavarende ->
            case model.aktivSamtale of
                RegistrereN친v칝rende n친v칝rendeInfo ->
                    ( n친v칝rendeInfo
                        |> forrigeTilTildatoInfo
                        |> RegistrereTilM친ned
                        |> oppdaterSamtale model (SvarFraMsg msg)
                    , lagtTilSp칮rsm친lCmd model.debugStatus
                    )
                        |> IkkeFerdig

                _ ->
                    IkkeFerdig ( Model model, Cmd.none )

        BrukerTrykketTilM친nedKnapp m친ned ->
            case model.aktivSamtale of
                RegistrereTilM친ned tilDatoInfo ->
                    ( { tilDatoInfo | tilM친ned = m친ned }
                        |> RegistrereTil칀r
                        |> oppdaterSamtale model (SvarFraMsg msg)
                    , lagtTilSp칮rsm친lCmd model.debugStatus
                    )
                        |> IkkeFerdig

                _ ->
                    IkkeFerdig ( Model model, Cmd.none )

        OppdaterTil칀r string ->
            case model.aktivSamtale of
                RegistrereTil칀r tilDatoInfo ->
                    ( { tilDatoInfo | til칀r = string }
                        |> RegistrereTil칀r
                        |> oppdaterSamtale model IngenNyeMeldinger
                    , Cmd.none
                    )
                        |> IkkeFerdig

                _ ->
                    ( Model model, Cmd.none )
                        |> IkkeFerdig

        BrukerVilG친TilOppsummering ->
            case model.aktivSamtale of
                RegistrereTil칀r tilDatoInfo ->
                    case Dato.stringTil칀r tilDatoInfo.til칀r of
                        Just 친r ->
                            ( 친r
                                |> forrigeTilOppsummeringInfo tilDatoInfo
                                |> Oppsummering F칮rsteGang
                                |> oppdaterSamtale model (SvarFraMsg msg)
                            , lagtTilSp칮rsm친lCmd model.debugStatus
                            )
                                |> IkkeFerdig

                        Nothing ->
                            ( { tilDatoInfo | vis칀rFeilmelding = True }
                                |> RegistrereTil칀r
                                |> oppdaterSamtale model IngenNyeMeldinger
                            , Cmd.none
                            )
                                |> IkkeFerdig

                _ ->
                    IkkeFerdig ( Model model, Cmd.none )

        BrukerVilEndreOppsummering ->
            case model.aktivSamtale of
                Oppsummering _ utdanningskjema ->
                    updateEtterVilEndreSkjema model msg utdanningskjema

                _ ->
                    IkkeFerdig ( Model model, Cmd.none )

        VilSletteP친begynt ->
            case model.aktivSamtale of
                Oppsummering _ skjema ->
                    ( BekreftSlettingAvP친begynt skjema
                        |> oppdaterSamtale model (SvarFraMsg msg)
                    , lagtTilSp칮rsm친lCmd model.debugStatus
                    )
                        |> IkkeFerdig

                _ ->
                    IkkeFerdig ( Model model, Cmd.none )

        BekrefterSlettP친begynt ->
            case model.aktivSamtale of
                BekreftSlettingAvP친begynt _ ->
                    ( SlettetP친begynt
                        |> LeggTilFlereUtdanninger model.utdanningListe
                        |> oppdaterSamtale model (SvarFraMsg msg)
                    , lagtTilSp칮rsm친lCmd model.debugStatus
                    )
                        |> IkkeFerdig

                _ ->
                    IkkeFerdig ( Model model, Cmd.none )

        AngrerSlettP친begynt ->
            case model.aktivSamtale of
                BekreftSlettingAvP친begynt skjema ->
                    ( Oppsummering Avbr칮tSletting skjema
                        |> oppdaterSamtale model (SvarFraMsg msg)
                    , lagtTilSp칮rsm친lCmd model.debugStatus
                    )
                        |> IkkeFerdig

                _ ->
                    IkkeFerdig ( Model model, Cmd.none )

        OppsummeringBekreftet ->
            case model.aktivSamtale of
                Oppsummering _ ferdigskjema ->
                    updateEtterLagreKnappTrykket model msg ferdigskjema

                _ ->
                    IkkeFerdig ( Model model, Cmd.none )

        OppsummeringSkjemaLagreknappTrykket ->
            case model.aktivSamtale of
                EndrerOppsummering skjema ->
                    case Skjema.validerSkjema skjema of
                        Just validertSkjema ->
                            ( validertSkjema
                                |> Oppsummering EtterEndring
                                |> oppdaterSamtale model (ManueltSvar (Melding.svar (validertSkjemaTilSetninger validertSkjema)))
                            , lagtTilSp칮rsm친lCmd model.debugStatus
                            )
                                |> IkkeFerdig

                        Nothing ->
                            IkkeFerdig
                                ( skjema
                                    |> Skjema.gj칮rAlleFeilmeldingerSynlig
                                    |> EndrerOppsummering
                                    |> oppdaterSamtale model IngenNyeMeldinger
                                , Cmd.none
                                )

                LagringFeilet error feiletskjema ->
                    IkkeFerdig
                        ( error
                            |> LagreStatus.fraError
                            |> LagrerSkjema feiletskjema
                            |> oppdaterSamtale model (SvarFraMsg msg)
                        , Cmd.batch
                            [ postEllerPutUtdanning UtdanningSendtTilApi feiletskjema
                            , lagtTilSp칮rsm친lCmd model.debugStatus
                            ]
                        )

                _ ->
                    IkkeFerdig ( Model model, Cmd.none )

        UtdanningSendtTilApi result ->
            case model.aktivSamtale of
                LagrerSkjema skjema lagreStatus ->
                    case result of
                        Ok nyUtdanningsListe ->
                            let
                                avsluttetGrunn =
                                    if List.length model.utdanningListe == List.length nyUtdanningsListe then
                                        EndretEksisterende

                                    else
                                        AnnenAvslutning
                            in
                            ( if LagreStatus.lagrerEtterUtlogging lagreStatus then
                                avsluttetGrunn
                                    |> LeggTilFlereUtdanninger nyUtdanningsListe
                                    |> oppdaterSamtale { model | utdanningListe = nyUtdanningsListe } (ManueltSvar (Melding.svar [ LoggInnLenke.loggInnLenkeTekst ]))

                              else
                                avsluttetGrunn
                                    |> LeggTilFlereUtdanninger nyUtdanningsListe
                                    |> oppdaterSamtale { model | utdanningListe = nyUtdanningsListe } UtenSvar
                            , lagtTilSp칮rsm친lCmd model.debugStatus
                            )
                                |> IkkeFerdig

                        Err error ->
                            if LagreStatus.lagrerEtterUtlogging lagreStatus then
                                if LagreStatus.fors칮kP친Nytt lagreStatus then
                                    ( error
                                        |> LagreStatus.fraError
                                        |> LagrerSkjema skjema
                                        |> oppdaterSamtale model IngenNyeMeldinger
                                    , postEllerPutUtdanning UtdanningSendtTilApi skjema
                                    )
                                        |> IkkeFerdig

                                else
                                    ( skjema
                                        |> LagringFeilet error
                                        |> oppdaterSamtale model IngenNyeMeldinger
                                    , skjema
                                        |> Skjema.encode
                                        |> Api.logErrorWithRequestBody ErrorLogget "Lagre utdanning" error
                                    )
                                        |> IkkeFerdig

                            else
                                ( skjema
                                    |> LagringFeilet error
                                    |> oppdaterSamtale model UtenSvar
                                , Cmd.batch
                                    [ lagtTilSp칮rsm친lCmd model.debugStatus
                                    , skjema
                                        |> Skjema.encode
                                        |> Api.logErrorWithRequestBody ErrorLogget "Lagre utdanning" error
                                    ]
                                )
                                    |> IkkeFerdig

                Oppsummering _ skjema ->
                    case result of
                        Ok nyUtdanningsListe ->
                            let
                                avsluttetGrunn =
                                    if List.length model.utdanningListe == List.length nyUtdanningsListe then
                                        EndretEksisterende

                                    else
                                        AnnenAvslutning
                            in
                            ( avsluttetGrunn
                                |> LeggTilFlereUtdanninger nyUtdanningsListe
                                |> oppdaterSamtale { model | utdanningListe = nyUtdanningsListe } UtenSvar
                            , lagtTilSp칮rsm친lCmd model.debugStatus
                            )
                                |> IkkeFerdig

                        Err error ->
                            ( skjema
                                |> LagringFeilet error
                                |> oppdaterSamtale model UtenSvar
                            , skjema
                                |> Skjema.encode
                                |> Api.logErrorWithRequestBody ErrorLogget "Lagre utdanning" error
                            )
                                |> IkkeFerdig

                _ ->
                    IkkeFerdig ( Model model, Cmd.none )

        BrukerVilPr칮ve칀LagreP친Nytt ->
            case model.aktivSamtale of
                LagringFeilet error validertSkjema ->
                    IkkeFerdig
                        ( error
                            |> LagreStatus.fraError
                            |> LagrerSkjema validertSkjema
                            |> oppdaterSamtale model (SvarFraMsg msg)
                        , Cmd.batch
                            [ postEllerPutUtdanning UtdanningSendtTilApi validertSkjema
                            , lagtTilSp칮rsm친lCmd model.debugStatus
                            ]
                        )

                _ ->
                    IkkeFerdig ( Model model, Cmd.none )

        BrukerVilAvbryteLagringen ->
            ( model.utdanningListe
                |> Intro
                |> oppdaterSamtale model (SvarFraMsg msg)
            , lagtTilSp칮rsm친lCmd model.debugStatus
            )
                |> IkkeFerdig

        BrukerVilAvbryteRegistreringen ->
            case model.aktivSamtale of
                RegistrerNiv친 ->
                    avbrytRegistrering model msg

                RegistrerSkole _ ->
                    avbrytRegistrering model msg

                _ ->
                    ( model.aktivSamtale
                        |> BekreftAvbrytingAvRegistreringen
                        |> oppdaterSamtale model (SvarFraMsg msg)
                    , lagtTilSp칮rsm친lCmd model.debugStatus
                    )
                        |> IkkeFerdig

        BrukerBekrefterAvbrytingAvRegistrering ->
            avbrytRegistrering model msg

        BrukerVilIkkeAvbryteRegistreringen ->
            case model.aktivSamtale of
                BekreftAvbrytingAvRegistreringen samtaleStegF칮rAvbryting ->
                    ( Model
                        { model
                            | aktivSamtale = samtaleStegF칮rAvbryting
                            , seksjonsMeldingsLogg =
                                model.seksjonsMeldingsLogg
                                    |> MeldingsLogg.leggTilSvar (svarFraBrukerInput model msg)
                                    |> MeldingsLogg.leggTilSp칮rsm친l
                                        (List.concat
                                            [ [ Melding.sp칮rsm친l [ "Ok. Da fortsetter vi der vi slapp." ] ]
                                            , samtaleTilMeldingsLogg samtaleStegF칮rAvbryting
                                            ]
                                        )
                        }
                    , lagtTilSp칮rsm친lCmd model.debugStatus
                    )
                        |> IkkeFerdig

                _ ->
                    IkkeFerdig ( Model model, Cmd.none )

        WindowEndrerVisibility visibility ->
            case visibility of
                Visible ->
                    case model.aktivSamtale of
                        LagringFeilet error validertSkjema ->
                            if ErrorH친ndtering.operasjonEtterError error == LoggInn then
                                IkkeFerdig
                                    ( error
                                        |> LagreStatus.fraError
                                        |> LagrerSkjema validertSkjema
                                        |> oppdaterSamtale model IngenNyeMeldinger
                                    , postEllerPutUtdanning UtdanningSendtTilApi validertSkjema
                                    )

                            else
                                IkkeFerdig ( Model model, Cmd.none )

                        LagrerSkjema skjema lagreStatus ->
                            ( lagreStatus
                                |> LagreStatus.setFors칮kP친Nytt
                                |> LagrerSkjema skjema
                                |> oppdaterSamtale model IngenNyeMeldinger
                            , Cmd.none
                            )
                                |> IkkeFerdig

                        _ ->
                            IkkeFerdig ( Model model, Cmd.none )

                Hidden ->
                    IkkeFerdig ( Model model, Cmd.none )

        SamtaleAnimasjonMsg samtaleAnimasjonMsg ->
            SamtaleAnimasjon.update model.debugStatus samtaleAnimasjonMsg model.seksjonsMeldingsLogg
                |> updateEtterFullf칮rtMelding model

        OppsummeringEndret skjemaEndring ->
            case model.aktivSamtale of
                EndrerOppsummering skjema ->
                    ( skjema
                        |> oppdaterSkjema skjemaEndring
                        |> EndrerOppsummering
                        |> oppdaterSamtale model IngenNyeMeldinger
                    , Cmd.none
                    )
                        |> IkkeFerdig

                _ ->
                    IkkeFerdig ( Model model, Cmd.none )

        ErrorLogget ->
            IkkeFerdig ( Model model, Cmd.none )

        FokusSatt _ ->
            IkkeFerdig ( Model model, Cmd.none )

        FeltMisterFokus ->
            IkkeFerdig ( Model model, mistetFokusCmd )

        TimeoutEtterAtFeltMistetFokus ->
            case model.aktivSamtale of
                RegistrereFra칀r fraDatoInfo ->
                    ( { fraDatoInfo | vis칀rFeilmelding = True }
                        |> RegistrereFra칀r
                        |> oppdaterSamtale model IngenNyeMeldinger
                    , Cmd.none
                    )
                        |> IkkeFerdig

                RegistrereTil칀r tilDatoInfo ->
                    ( { tilDatoInfo | vis칀rFeilmelding = True }
                        |> RegistrereTil칀r
                        |> oppdaterSamtale model IngenNyeMeldinger
                    , Cmd.none
                    )
                        |> IkkeFerdig

                _ ->
                    IkkeFerdig ( Model model, Cmd.none )


avbrytRegistrering : ModelInfo -> Msg -> SamtaleStatus
avbrytRegistrering model msg =
    ( AvbruttP친begynt
        |> LeggTilFlereUtdanninger model.utdanningListe
        |> oppdaterSamtale model (SvarFraMsg msg)
    , lagtTilSp칮rsm친lCmd model.debugStatus
    )
        |> IkkeFerdig


niv친ToString : Niv친 -> String
niv친ToString niv친 =
    case niv친 of
        Grunnskole ->
            "Grunnskole"

        Videreg친endeYrkesskole ->
            "Videreg친ende/Yrkesskole"

        Fagskole ->
            "Fagskole"

        Folkeh칮yskole ->
            "Folkeh칮yskole"

        H칮yereUtdanning1til4 ->
            "H칮yere utdanning, 1-4 친r"

        H칮yereUtdanning4pluss ->
            "H칮yere utdanning, mer enn 4 친r"

        Doktorgrad ->
            "Doktorgrad"


oppdaterSkjema : SkjemaEndring -> UtdanningSkjema -> UtdanningSkjema
oppdaterSkjema skjemaEndring skjema =
    case skjemaEndring of
        Tekst felt innhold ->
            Skjema.oppdaterTekstFelt felt innhold skjema

        N친v칝rendeToggled ->
            Skjema.toggleNavarende skjema

        FraM친ned m친nedString ->
            m친nedString
                |> M친ned.stringTilM친ned
                |> Skjema.oppdaterFraM친ned skjema

        TilM친ned m친nedString ->
            m친nedString
                |> M친ned.stringTilM친ned
                |> Skjema.oppdaterTilM친ned skjema

        Niv친 niv친String ->
            niv친String
                |> stringToNiv친
                |> Maybe.map (Skjema.oppdaterNiv친 skjema)
                |> Maybe.withDefault skjema

        Fra칀rBlurred ->
            Skjema.gj칮rFeilmeldingFra칀rSynlig skjema

        Til칀rBlurred ->
            Skjema.gj칮rFeilmeldingTil칀rSynlig skjema


settFokus : Samtale -> Cmd Msg
settFokus samtale =
    case samtale of
        Intro _ ->
            settFokusCmd HarUtdanningId

        RegistrerNiv친 ->
            settFokusCmd VelgNiv친Input

        RegistrerSkole _ ->
            settFokusCmd RegistrerSkoleInput

        RegistrerRetning _ ->
            settFokusCmd RegistrerRetningInput

        RegistrerBeskrivelse _ _ ->
            settFokusCmd RegistrerBeskrivelseInput

        RegistrereFraM친ned _ ->
            settFokusCmd FraM친nedId

        RegistrereFra칀r _ ->
            settFokusCmd RegistrereFra칀rInput

        RegistrereN친v칝rende _ ->
            settFokusCmd N친v칝rendeId

        RegistrereTilM친ned _ ->
            settFokusCmd TilM친nedId

        RegistrereTil칀r _ ->
            settFokusCmd RegistrereTil칀rInput

        EndrerOppsummering _ ->
            settFokusCmd VelgNiv친ISkjemaId

        Oppsummering _ _ ->
            settFokusCmd BekreftOppsummeringId

        LeggTilFlereUtdanninger _ _ ->
            settFokusCmd LeggTilUtdanningId

        VelgEnUtdanning칀Redigere ->
            settFokusCmd RedigerUtdanningId

        LagringFeilet _ _ ->
            settFokusCmd LagringFeiletActionId

        BekreftSlettingAvP친begynt _ ->
            settFokusCmd SletteP친begyntId

        BekreftAvbrytingAvRegistreringen _ ->
            settFokusCmd AvbrytSlettingId

        _ ->
            Cmd.none


updateEtterFullf칮rtMelding : ModelInfo -> ( MeldingsLogg, Cmd SamtaleAnimasjon.Msg ) -> SamtaleStatus
updateEtterFullf칮rtMelding model ( nyMeldingsLogg, cmd ) =
    case MeldingsLogg.ferdigAnimert nyMeldingsLogg of
        FerdigAnimert ferdigAnimertSamtale ->
            case model.aktivSamtale of
                VenterP친AnimasjonF칮rFullf칮ring utdanningsListe _ ->
                    Ferdig utdanningsListe ferdigAnimertSamtale

                _ ->
                    ( Model { model | seksjonsMeldingsLogg = nyMeldingsLogg }
                    , Cmd.batch
                        [ Cmd.map SamtaleAnimasjonMsg cmd
                        , settFokus model.aktivSamtale
                        ]
                    )
                        |> IkkeFerdig

        MeldingerGjenst친r ->
            ( Model { model | seksjonsMeldingsLogg = nyMeldingsLogg }
            , Cmd.map SamtaleAnimasjonMsg cmd
            )
                |> IkkeFerdig


updateEtterVilEndreSkjema : ModelInfo -> Msg -> ValidertUtdanningSkjema -> SamtaleStatus
updateEtterVilEndreSkjema model msg skjema =
    ( skjema
        |> Skjema.tilUvalidertSkjema
        |> EndrerOppsummering
        |> oppdaterSamtale model (SvarFraMsg msg)
    , lagtTilSp칮rsm친lCmd model.debugStatus
    )
        |> IkkeFerdig


updateEtterLagreKnappTrykket : ModelInfo -> Msg -> ValidertUtdanningSkjema -> SamtaleStatus
updateEtterLagreKnappTrykket model msg skjema =
    ( LagreStatus.init
        |> LagrerSkjema skjema
        |> oppdaterSamtale model (SvarFraMsg msg)
    , postEllerPutUtdanning UtdanningSendtTilApi skjema
    )
        |> IkkeFerdig


lagtTilSp칮rsm친lCmd : DebugStatus -> Cmd Msg
lagtTilSp칮rsm친lCmd debugStatus =
    SamtaleAnimasjon.startAnimasjon debugStatus
        |> Cmd.map SamtaleAnimasjonMsg


mistetFokusCmd : Cmd Msg
mistetFokusCmd =
    Process.sleep 100
        |> Task.perform (\_ -> TimeoutEtterAtFeltMistetFokus)


settFokusCmd : InputId -> Cmd Msg
settFokusCmd inputId =
    Process.sleep 200
        |> Task.andThen (\_ -> (inputIdTilString >> Dom.focus) inputId)
        |> Task.attempt FokusSatt


stringToNiv친 : String -> Maybe Niv친
stringToNiv친 string =
    case string of
        "Grunnskole" ->
            Just Grunnskole

        "Videreg친endeYrkesskole" ->
            Just Videreg친endeYrkesskole

        "Fagskole" ->
            Just Fagskole

        "Folkeh칮yskole" ->
            Just Folkeh칮yskole

        "H칮yereUtdanning1til4" ->
            Just H칮yereUtdanning1til4

        "H칮yereUtdanning4pluss" ->
            Just H칮yereUtdanning4pluss

        "Doktorgrad" ->
            Just Doktorgrad

        _ ->
            Nothing


svarFraBrukerInput : ModelInfo -> Msg -> Melding
svarFraBrukerInput modelInfo msg =
    modelInfo
        |> modelTilBrukerInput
        |> BrukerInput.tilSvarMelding msg


oppdaterSamtale : ModelInfo -> SamtaleOppdatering Msg -> Samtale -> Model
oppdaterSamtale model meldingsoppdatering samtale =
    Model
        { model
            | aktivSamtale = samtale
            , seksjonsMeldingsLogg =
                case meldingsoppdatering of
                    IngenNyeMeldinger ->
                        model.seksjonsMeldingsLogg

                    SvarFraMsg msg ->
                        model.seksjonsMeldingsLogg
                            |> MeldingsLogg.leggTilSvar (svarFraBrukerInput model msg)
                            |> MeldingsLogg.leggTilSp칮rsm친l (samtaleTilMeldingsLogg samtale)

                    ManueltSvar melding ->
                        model.seksjonsMeldingsLogg
                            |> MeldingsLogg.leggTilSvar melding
                            |> MeldingsLogg.leggTilSp칮rsm친l (samtaleTilMeldingsLogg samtale)

                    UtenSvar ->
                        model.seksjonsMeldingsLogg
                            |> MeldingsLogg.leggTilSp칮rsm친l (samtaleTilMeldingsLogg samtale)
        }


utdanningslisteTilString : List Utdanning -> List Tekstomr친de
utdanningslisteTilString utdanninger =
    utdanninger
        |> List.map utdanningTilTekstomr친de
        |> List.intersperse (Avsnitt Melding.tomLinje)


utdanningTilTekstomr친de : Utdanning -> Tekstomr친de
utdanningTilTekstomr친de utdanning =
    Seksjon (Utdanning.utdanningsretning utdanning |> Maybe.withDefault "")
        [ Dato.periodeTilString (Utdanning.fraM친ned utdanning) (Utdanning.fra칀r utdanning) (Utdanning.tilDato utdanning)
        , (Utdanning.utdanningsretning utdanning |> Maybe.withDefault "")
            ++ " ved  "
            ++ (Utdanning.studiested utdanning |> Maybe.withDefault "")
        ]


samtaleTilMeldingsLogg : Samtale -> List Melding
samtaleTilMeldingsLogg utdanningSeksjon =
    case utdanningSeksjon of
        Intro utdanninger ->
            if List.isEmpty utdanninger then
                [ Melding.sp칮rsm친l [ "Har du utdanning du vil legge inn i CV-en?" ]
                , Melding.sp칮rsm친l [ "Legg inn videreg친ende og h칮yere utdanning hvis du har det. Hvis du kun har grunnskole, legger du inn det." ]
                ]

            else
                [ Melding.sp칮rsm친l [ "N친 skal vi legge til utdanning. Vi ser at du allerede har lagt inn disse utdanningene: " ]
                , Melding.sp칮rsm친lMedTekstomr친der (utdanningslisteTilString utdanninger)
                , Melding.sp칮rsm친l [ "Vil du legge inn flere utdanninger? " ]
                ]

        VelgEnUtdanning칀Redigere ->
            [ Melding.sp칮rsm친l [ "Hvilken registrerte utdanning 칮nsker du 친 redigere?" ] ]

        RegistrerNiv친 ->
            [ Melding.sp칮rsm친l [ "Legg inn 칠n utdanning av gangen." ]
            , Melding.sp칮rsm친l [ "Hvis du har en bachelorgrad, velg h칮yere utdanning 1-4 친r. Har du en mastergrad, velg h칮yere utdanning mer enn 4 친r." ]
            , Melding.sp칮rsm친l [ "Hvilket niv친 har utdanningen du skal legge inn?" ]
            ]

        RegistrerSkole skoleinfo ->
            case skoleinfo.forrige of
                Fagskole ->
                    [ Melding.sp칮rsm친l [ "Hvilken skole gikk du p친?" ]
                    , Melding.sp칮rsm친l [ "For eksempel Fagskolen i 칒stfold" ]
                    ]

                Videreg친endeYrkesskole ->
                    [ Melding.sp칮rsm친l [ "Hvilken skole gikk du p친?" ]
                    , Melding.sp칮rsm친l [ "For eksempel Drammen videreg친ende skole" ]
                    ]

                Grunnskole ->
                    [ Melding.sp칮rsm친l [ "Hvilken grunnskole gikk du p친?" ]
                    , Melding.sp칮rsm친l [ "For eksempel Huseby skole" ]
                    ]

                Folkeh칮yskole ->
                    [ Melding.sp칮rsm친l [ "Hvilken folkeh칮gskole gikk du p친?" ]
                    , Melding.sp칮rsm친l [ "For eksempel Nordfjordeid folkeh칮gskule" ]
                    ]

                _ ->
                    [ Melding.sp칮rsm친l [ "Hvilken skole gikk du p친?" ]
                    , Melding.sp칮rsm친l [ "For eksempel Universitet i Oslo" ]
                    ]

        RegistrerRetning _ ->
            [ Melding.sp칮rsm친l [ "Hvis du har fagbrev/svennebrev, mesterbrev eller autorisasjon, kan du legge inn dette senere." ]
            , Melding.sp칮rsm친l [ "Hva er navnet p친 graden din, og hvilken utdanningsretning gikk du?" ]
            , Melding.sp칮rsm친l [ "Kanskje du har en bachelor i historie, eller elektrofag fra videreg친ende?" ]
            ]

        RegistrerBeskrivelse _ _ ->
            [ Melding.sp칮rsm친l [ "Skriv noen ord om denne utdanningen. Har du fordypning i noen fag?" ] ]

        RegistrereFraM친ned _ ->
            [ Melding.sp칮rsm친l [ "Hvilken m친ned begynte du p친 utdanningen din?" ]
            , Melding.sp칮rsm친l [ "De fleste studiene i Norge begynner i august." ]
            ]

        RegistrereFra칀r _ ->
            [ Melding.sp칮rsm친l [ "Hvilket 친r begynte du p친 utdanningen din?" ] ]

        RegistrereN친v칝rende _ ->
            [ Melding.sp칮rsm친l [ "Holder du fortsatt p친 med utdanningen?" ] ]

        RegistrereTilM친ned _ ->
            [ Melding.sp칮rsm친l [ "Hvilken m친ned fullf칮rte du utdanningen din?" ]
            , Melding.sp칮rsm친l [ "De fleste studier i Norge er ferdig i juni." ]
            ]

        RegistrereTil칀r _ ->
            [ Melding.sp칮rsm친l [ "Hvilket 친r fullf칮rte du utdanningen din?" ] ]

        Oppsummering oppsummeringsType validertSkjema ->
            case oppsummeringsType of
                Avbr칮tSletting ->
                    [ Melding.sp칮rsm친l [ "Ok, da lar jeg utdanningen st친." ]
                    , oppsummeringsSp칮rsm친l validertSkjema
                    ]

                EtterEndring ->
                    [ Melding.sp칮rsm친l [ "Du har endret. Er det riktig n친?" ] ]

                F칮rsteGang ->
                    [ oppsummeringsSp칮rsm친l validertSkjema
                    ]

        EndrerOppsummering _ ->
            [ Melding.sp칮rsm친l [ "G친 gjennom og endre det du 칮nsker." ] ]

        BekreftSlettingAvP친begynt _ ->
            [ Melding.sp칮rsm친l [ "Er du sikker p친 at du vil slette denne utdanningen?" ] ]

        LeggTilFlereUtdanninger utdanninger avsluttetGrunn ->
            [ case avsluttetGrunn of
                AvbruttP친begynt ->
                    Melding.sp칮rsm친l [ "N친 har jeg avbrutt." ]

                SlettetP친begynt ->
                    Melding.sp칮rsm친l [ "N친 har jeg slettet utdanningen." ]

                EndretEksisterende ->
                    Melding.sp칮rsm친l [ "S친 bra! N친 er utdanningen endret游녨" ]

                AnnenAvslutning ->
                    Melding.sp칮rsm친l [ "S친 bra! N친 er utdanningen lagret游녨" ]
            , if List.isEmpty utdanninger then
                Melding.sp칮rsm친l [ "Har du utdanning du vil legge inn i CV-en?" ]

              else
                Melding.sp칮rsm친l [ "Vil du legge inn flere utdanninger?" ]
            ]

        BekreftAvbrytingAvRegistreringen _ ->
            [ Melding.sp칮rsm친l [ "Hvis du avbryter, blir ikke denne utdanningen lagret p친 CV-en din. Er du sikker p친 at du vil avbryte?" ] ]

        LagrerSkjema _ _ ->
            []

        LagringFeilet error _ ->
            [ ErrorH친ndtering.errorMelding { error = error, operasjon = "lagre utdanning" } ]

        VenterP친AnimasjonF칮rFullf칮ring liste avsluttetGrunn ->
            if List.isEmpty liste then
                if avsluttetGrunn == SlettetP친begynt then
                    [ Melding.sp칮rsm친l [ "Da g친r vi videre til arbeidserfaring." ] ]

                else
                    [ Melding.sp칮rsm친l [ "Siden du ikke har utdanning, g친r vi videre til arbeidserfaring." ] ]

            else
                [ Melding.sp칮rsm친l [ "Bra jobba! Da g친r vi videre." ] ]


validertSkjemaTilSetninger : ValidertUtdanningSkjema -> List String
validertSkjemaTilSetninger validertSkjema =
    let
        utdanningsskjema =
            Skjema.tilUvalidertSkjema validertSkjema
    in
    [ Dato.periodeTilString (Skjema.fraM친ned utdanningsskjema) (Skjema.fra칀rValidert validertSkjema) (Skjema.tilDatoValidert validertSkjema)
    , Melding.tomLinje
    , "Utdanningsniv친: " ++ niv친ToString (Skjema.niv친 utdanningsskjema)
    , "Grad og studieretning: " ++ Skjema.innholdTekstFelt Utdanningsretning utdanningsskjema
    , "Skole/studiested: " ++ Skjema.innholdTekstFelt Studiested utdanningsskjema
    , Melding.tomLinje
    , "Beskrivelse:"
    , Skjema.innholdTekstFelt Beskrivelse utdanningsskjema
    ]


oppsummeringsSp칮rsm친l : ValidertUtdanningSkjema -> Melding
oppsummeringsSp칮rsm친l skjema =
    [ [ "Du har lagt inn dette:"
      , Melding.tomLinje
      ]
    , validertSkjemaTilSetninger skjema
    , [ Melding.tomLinje
      , "Er informasjonen riktig?"
      ]
    ]
        |> List.concat
        |> Melding.sp칮rsm친l


detFinnesEksemplerForNiv친 : Niv친 -> Bool
detFinnesEksemplerForNiv친 niv친 =
    niv친
        |> eksemplerP친Utdanning
        |> (not << List.isEmpty)


eksemplerP친Utdanning : Niv친 -> List Melding
eksemplerP친Utdanning niv친 =
    case niv친 of
        Grunnskole ->
            []

        Videreg친endeYrkesskole ->
            [ Melding.eksempelMedTittel "Eksempel 1:" [ "Fordypning i matematikk og fysikk." ]
            , Melding.eksempelMedTittel "Eksempel 2:" [ "Elektrofag Vg1 og Vg2, spesialisering i datateknologi og elektronikk." ]
            ]

        Fagskole ->
            [ Melding.eksempel [ "Maskinteknikk i mekanisk industri, prosjekt- og kvalitetsledelse og 칮konomistyring." ]
            ]

        Folkeh칮yskole ->
            []

        H칮yereUtdanning1til4 ->
            [ Melding.eksempel [ "Fordypning i offentlig politikk og administrasjon. Bacheloroppgave om ulik politisk utvikling i de skandinaviske landene etter 1970." ]
            ]

        H칮yereUtdanning4pluss ->
            [ Melding.eksempel [ "Spesialisering i anvendt finans. Utvekslingsstudent p친 University of London (h칮stsemesteret 2017)." ]
            ]

        Doktorgrad ->
            []



--- VIEW ---


viewBrukerInput : Model -> Html Msg
viewBrukerInput (Model model) =
    model
        |> modelTilBrukerInput
        |> BrukerInput.toHtml


modelTilBrukerInput : ModelInfo -> BrukerInput Msg
modelTilBrukerInput model =
    if MeldingsLogg.visBrukerInput model.seksjonsMeldingsLogg then
        case model.aktivSamtale of
            Intro _ ->
                if List.isEmpty model.utdanningListe then
                    BrukerInput.knapper Flytende
                        [ Knapp.knapp BrukerVilRegistrereUtdanning "Ja, jeg har utdanning"
                            |> Knapp.withId (inputIdTilString HarUtdanningId)
                        , Knapp.knapp (G친TilArbeidserfaring AnnenAvslutning) "Nei, jeg har ikke utdanning"
                        ]

                else
                    BrukerInput.knapper Flytende
                        [ Knapp.knapp BrukerVilRegistrereUtdanning "Ja, legg til en utdanning"
                            |> Knapp.withId (inputIdTilString HarUtdanningId)
                        , Knapp.knapp (G친TilArbeidserfaring AnnenAvslutning) "Nei, jeg er ferdig"
                        , Knapp.knapp BrukerVilRedigereUtdanning "Nei, jeg vil endre det jeg har lagt inn"
                        ]

            VelgEnUtdanning칀Redigere ->
                BrukerInput.knapper Kolonne
                    (case model.utdanningListe of
                        first :: rest ->
                            (utdanningKnapp first
                                |> Knapp.withId (inputIdTilString RedigerUtdanningId)
                            )
                                :: List.map utdanningKnapp rest

                        _ ->
                            []
                    )

            RegistrerNiv친 ->
                BrukerInput.knapper Kolonne
                    [ velgNiv친Knapp Grunnskole
                        |> Knapp.withId (inputIdTilString VelgNiv친Input)
                    , velgNiv친Knapp Videreg친endeYrkesskole
                    , velgNiv친Knapp Fagskole
                    , velgNiv친Knapp Folkeh칮yskole
                    , velgNiv친Knapp H칮yereUtdanning1til4
                    , velgNiv친Knapp H칮yereUtdanning4pluss
                    , velgNiv친Knapp Doktorgrad
                    , Knapp.knapp BrukerVilAvbryteRegistreringen "Avbryt"
                        |> Knapp.withType Flat
                    ]

            RegistrerSkole skoleinfo ->
                BrukerInput.inputMedG친VidereKnapp { onAvbryt = BrukerVilAvbryteRegistreringen, onG친Videre = BrukerVilRegistrereSkole }
                    (skoleinfo.skole
                        |> Input.input { msg = OppdaterSkole, label = "Skole/studiested" }
                        |> Input.withOnEnter BrukerVilRegistrereSkole
                        |> Input.withId (inputIdTilString RegistrerSkoleInput)
                    )

            RegistrerRetning retningsinfo ->
                BrukerInput.inputMedG친VidereKnapp { onAvbryt = BrukerVilAvbryteRegistreringen, onG친Videre = BrukerVilRegistrereRetning }
                    (retningsinfo.retning
                        |> Input.input { msg = OppdaterRetning, label = "Grad og utdanningsretning" }
                        |> Input.withId (inputIdTilString RegistrerRetningInput)
                        |> Input.withOnEnter BrukerVilRegistrereRetning
                    )

            RegistrerBeskrivelse medEksempelKnapp beskrivelseinfo ->
                beskrivelseinfo.beskrivelse
                    |> Textarea.textarea { msg = OppdaterBeskrivelse, label = "Beskriv utdanningen" }
                    |> Textarea.withId (inputIdTilString RegistrerBeskrivelseInput)
                    |> Textarea.withFeilmelding (Validering.feilmeldingMaxAntallTegn beskrivelseinfo.beskrivelse maxLengthBeskrivelse)
                    |> BrukerInputMedG친VidereKnapp.textarea BrukerVilRegistrereBeskrivelse
                    |> BrukerInputMedG친VidereKnapp.withVisEksempelKnapp medEksempelKnapp VilSeEksempel
                    |> BrukerInputMedG친VidereKnapp.withAvbrytKnapp BrukerVilAvbryteRegistreringen
                    |> BrukerInput.brukerInputMedG친VidereKnapp

            RegistrereFraM친ned _ ->
                BrukerInput.m친nedKnapper
                    { onAvbryt = BrukerVilAvbryteRegistreringen
                    , onM친nedValg = BrukerTrykketFraM친nedKnapp
                    , fokusId = inputIdTilString FraM친nedId
                    }

            RegistrereFra칀r fraDatoInfo ->
                BrukerInput.inputMedG친VidereKnapp { onAvbryt = BrukerVilAvbryteRegistreringen, onG친Videre = BrukerVilG친VidereMedFra칀r }
                    (fraDatoInfo.fra칀r
                        |> Input.input { label = "칀r", msg = OppdaterFra칀r }
                        |> Input.withWrapperClass "친r-wrapper"
                        |> Input.withFeilmelding ((Dato.feilmelding칀r >> maybeHvisTrue fraDatoInfo.vis칀rFeilmelding) fraDatoInfo.fra칀r)
                        |> Input.withId (inputIdTilString RegistrereFra칀rInput)
                        |> Input.withOnEnter BrukerVilG친VidereMedFra칀r
                        |> Input.withOnBlur FeltMisterFokus
                        |> Input.withErObligatorisk
                    )

            RegistrereN친v칝rende _ ->
                BrukerInput.knapper Flytende
                    [ Knapp.knapp BrukerSvarerJaTilNaavarende "Ja, jeg holder fortsatt p친"
                        |> Knapp.withId (inputIdTilString N친v칝rendeId)
                    , Knapp.knapp BrukerSvarerNeiTilNaavarende "Nei, jeg er ferdig"
                    ]

            RegistrereTilM친ned _ ->
                BrukerInput.m친nedKnapper
                    { onAvbryt = BrukerVilAvbryteRegistreringen
                    , onM친nedValg = BrukerTrykketTilM친nedKnapp
                    , fokusId = inputIdTilString TilM친nedId
                    }

            RegistrereTil칀r tilDatoInfo ->
                BrukerInput.inputMedG친VidereKnapp { onAvbryt = BrukerVilAvbryteRegistreringen, onG친Videre = BrukerVilG친TilOppsummering }
                    (tilDatoInfo.til칀r
                        |> Input.input { label = "칀r", msg = OppdaterTil칀r }
                        |> Input.withWrapperClass "친r-wrapper"
                        |> Input.withFeilmelding ((Dato.feilmelding칀r >> maybeHvisTrue tilDatoInfo.vis칀rFeilmelding) tilDatoInfo.til칀r)
                        |> Input.withId (inputIdTilString RegistrereTil칀rInput)
                        |> Input.withOnEnter BrukerVilG친TilOppsummering
                        |> Input.withOnBlur FeltMisterFokus
                        |> Input.withErObligatorisk
                    )

            Oppsummering _ skjema ->
                case Skjema.id (Skjema.tilUvalidertSkjema skjema) of
                    Just _ ->
                        viewBekreftOppsummering False

                    Nothing ->
                        viewBekreftOppsummering True

            EndrerOppsummering utdanningsskjema ->
                viewSkjema utdanningsskjema

            BekreftSlettingAvP친begynt _ ->
                BrukerInput.knapper Flytende
                    [ Knapp.knapp BekrefterSlettP친begynt "Ja, jeg vil slette"
                        |> Knapp.withId (inputIdTilString SletteP친begyntId)
                    , Knapp.knapp AngrerSlettP친begynt "Nei, jeg vil ikke slette"
                    ]

            LeggTilFlereUtdanninger utdanninger avsluttetGrunn ->
                BrukerInput.knapper Flytende
                    ([ [ Knapp.knapp BrukerVilRegistrereUtdanning "Ja, legg til en utdanning"
                            |> Knapp.withId (inputIdTilString LeggTilUtdanningId)
                       , Knapp.knapp (G친TilArbeidserfaring avsluttetGrunn) "Nei, jeg er ferdig"
                       ]
                     , if List.length utdanninger > 0 then
                        [ Knapp.knapp BrukerVilRedigereUtdanning "Nei, jeg vil endre det jeg har lagt inn" ]

                       else
                        []
                     ]
                        |> List.concat
                    )

            LagrerSkjema _ lagreStatus ->
                if LagreStatus.lagrerEtterUtlogging lagreStatus then
                    LoggInnLenke.viewLoggInnLenke

                else
                    BrukerInput.utenInnhold

            LagringFeilet error _ ->
                case ErrorH친ndtering.operasjonEtterError error of
                    GiOpp ->
                        BrukerInput.knapper Flytende
                            [ Knapp.knapp BrukerVilAvbryteLagringen "G친 videre"
                                |> Knapp.withId (inputIdTilString LagringFeiletActionId)
                            ]

                    Pr칮vP친Nytt ->
                        BrukerInput.knapper Flytende
                            [ Knapp.knapp BrukerVilPr칮ve칀LagreP친Nytt "Pr칮v igjen"
                                |> Knapp.withId (inputIdTilString LagringFeiletActionId)
                            , Knapp.knapp BrukerVilAvbryteLagringen "G친 videre"
                            ]

                    LoggInn ->
                        LoggInnLenke.viewLoggInnLenke

            BekreftAvbrytingAvRegistreringen _ ->
                BrukerInput.knapper Flytende
                    [ Knapp.knapp BrukerBekrefterAvbrytingAvRegistrering "Ja, jeg vil avbryte"
                        |> Knapp.withId (inputIdTilString AvbrytSlettingId)
                    , Knapp.knapp BrukerVilIkkeAvbryteRegistreringen "Nei, jeg vil fortsette"
                    ]

            VenterP친AnimasjonF칮rFullf칮ring _ _ ->
                BrukerInput.utenInnhold

    else
        BrukerInput.utenInnhold


type InputId
    = HarUtdanningId
    | RedigerUtdanningId
    | VelgNiv친Input
    | RegistrerSkoleInput
    | RegistrerRetningInput
    | RegistrerBeskrivelseInput
    | FraM친nedId
    | RegistrereFra칀rInput
    | TilM친nedId
    | RegistrereTil칀rInput
    | N친v칝rendeId
    | VelgNiv친ISkjemaId
    | BekreftOppsummeringId
    | LeggTilUtdanningId
    | SletteP친begyntId
    | LagringFeiletActionId
    | AvbrytSlettingId


inputIdTilString : InputId -> String
inputIdTilString inputId =
    case inputId of
        HarUtdanningId ->
            "har-utdanning"

        RedigerUtdanningId ->
            "utdanning-rediger"

        VelgNiv친Input ->
            "utdanning-velg-niv친"

        RegistrerSkoleInput ->
            "utdanning-registrer-skole"

        RegistrerRetningInput ->
            "utdanning-registrer-grad"

        RegistrerBeskrivelseInput ->
            "utdanning-registrer-beskrivelse"

        RegistrereFra칀rInput ->
            "utdanning-registrere-fra-친r"

        RegistrereTil칀rInput ->
            "utdanning-registrere-til-친r"

        FraM친nedId ->
            "utdanning-fra-m친ned-id"

        TilM친nedId ->
            "utdanning-til-m친ned-id"

        N친v칝rendeId ->
            "utdanning-n친v칝rende-id"

        VelgNiv친ISkjemaId ->
            "utdanning-velg-niv친-skjema-id"

        BekreftOppsummeringId ->
            "utdanning-bekreft-oppsummering-id"

        LeggTilUtdanningId ->
            "utdanning-legg-til-id"

        SletteP친begyntId ->
            "utdanning-slett-p친begynt-id"

        LagringFeiletActionId ->
            "utdanning-lagring-feilet-id"

        AvbrytSlettingId ->
            "utdanning-avbrytt-slett-id"


velgNiv친Knapp : Niv친 -> Knapp Msg
velgNiv친Knapp niv친 =
    niv친
        |> niv친ToString
        |> Knapp.knapp (BrukerVilRegistrereNiv친 niv친)


maybeHvisTrue : Bool -> Maybe a -> Maybe a
maybeHvisTrue bool maybe =
    if bool then
        maybe

    else
        Nothing


viewSkjema : UtdanningSkjema -> BrukerInput Msg
viewSkjema utdanningsskjema =
    BrukerInput.skjema { lagreMsg = OppsummeringSkjemaLagreknappTrykket, lagreKnappTekst = "Lagre endringer" }
        [ Select.select "Utdanningsniv친" (Niv친 >> OppsummeringEndret) selectNiv친Liste
            |> Select.withSelected (utdanningsskjema |> Skjema.niv친 |> tilNiv친Key)
            |> Select.withErObligatorisk
            |> Select.withId (inputIdTilString VelgNiv친ISkjemaId)
            |> Select.toHtml
        , utdanningsskjema
            |> Skjema.innholdTekstFelt Studiested
            |> Input.input { label = "Skole/studiested", msg = Tekst Studiested >> OppsummeringEndret }
            |> Input.toHtml
        , utdanningsskjema
            |> Skjema.innholdTekstFelt Utdanningsretning
            |> Input.input { label = "Grad og utdanningsretning", msg = Tekst Utdanningsretning >> OppsummeringEndret }
            |> Input.toHtml
        , utdanningsskjema
            |> Skjema.innholdTekstFelt Beskrivelse
            |> Textarea.textarea { label = "Beskriv utdanningen", msg = Tekst Beskrivelse >> OppsummeringEndret }
            |> Textarea.withFeilmelding (Validering.feilmeldingMaxAntallTegn (Skjema.innholdTekstFelt Beskrivelse utdanningsskjema) maxLengthBeskrivelse)
            |> Textarea.toHtml
        , div [ class "DatoInput-fra-til-rad" ]
            [ DatoInput.datoInput
                { label = "N친r startet du p친 utdanningen?"
                , onM친nedChange = FraM친ned >> OppsummeringEndret
                , m친ned = Skjema.fraM친ned utdanningsskjema
                , on칀rChange = Tekst Fra칀r >> OppsummeringEndret
                , 친r = Skjema.innholdTekstFelt Fra칀r utdanningsskjema
                }
                |> DatoInput.withFeilmelding칀r (Skjema.feilmeldingFra칀r utdanningsskjema)
                |> DatoInput.withOnBlur칀r (OppsummeringEndret Fra칀rBlurred)
                |> DatoInput.toHtml
            , if not (Skjema.n친v칝rende utdanningsskjema) then
                DatoInput.datoInput
                    { label = "N친r avsluttet du utdanningen?"
                    , onM친nedChange = TilM친ned >> OppsummeringEndret
                    , m친ned = Skjema.tilM친ned utdanningsskjema
                    , on칀rChange = Tekst Til칀r >> OppsummeringEndret
                    , 친r = Skjema.innholdTekstFelt Til칀r utdanningsskjema
                    }
                    |> DatoInput.withFeilmelding칀r (Skjema.feilmeldingTil칀r utdanningsskjema)
                    |> DatoInput.withOnBlur칀r (OppsummeringEndret Til칀rBlurred)
                    |> DatoInput.toHtml

              else
                text ""
            ]
        , utdanningsskjema
            |> Skjema.n친v칝rende
            |> Checkbox.checkbox "Jeg holder fortsatt p친 med utdanningen" (OppsummeringEndret N친v칝rendeToggled)
            |> Checkbox.withClass "blokk-m"
            |> Checkbox.toHtml
        ]


viewBekreftOppsummering : Bool -> BrukerInput Msg
viewBekreftOppsummering skalViseSlett =
    if skalViseSlett then
        BrukerInput.knapper Kolonne
            [ Knapp.knapp OppsummeringBekreftet "Ja, det er riktig"
                |> Knapp.withId (inputIdTilString BekreftOppsummeringId)
            , Knapp.knapp BrukerVilEndreOppsummering "Nei, jeg vil endre"
            , Knapp.knapp VilSletteP친begynt "Nei, jeg vil slette"
            ]

    else
        BrukerInput.knapper Flytende
            [ Knapp.knapp OppsummeringBekreftet "Ja, det er riktig"
                |> Knapp.withId (inputIdTilString BekreftOppsummeringId)
            , Knapp.knapp BrukerVilEndreOppsummering "Nei, jeg vil endre"
            ]


utdanningKnapp : Utdanning -> Knapp Msg
utdanningKnapp utdanning =
    let
        text =
            case Utdanning.utdanningsretning utdanning of
                Just value ->
                    if value == "" then
                        utdanning |> Utdanning.niv친 |> niv친ToString

                    else
                        value

                Nothing ->
                    utdanning |> Utdanning.niv친 |> niv친ToString
    in
    Knapp.knapp (BrukerHarValgtUtdanning칀Redigere utdanning) text


selectNiv친Liste : List ( String, String )
selectNiv친Liste =
    [ ( tilNiv친Key Grunnskole, "Grunnskole" )
    , ( tilNiv친Key Videreg친endeYrkesskole, "Videreg친ende/Yrkesskole" )
    , ( tilNiv친Key Fagskole, "Fagskole" )
    , ( tilNiv친Key Folkeh칮yskole, "Folkeh칮yskole" )
    , ( tilNiv친Key H칮yereUtdanning1til4, "H칮yere utdanning, 1-4 친r" )
    , ( tilNiv친Key H칮yereUtdanning4pluss, "H칮yere utdanning, mer enn 4 친r" )
    , ( tilNiv친Key Doktorgrad, "Doktorgrad" )
    ]


tilNiv친Key : Niv친 -> String
tilNiv친Key niv친 =
    case niv친 of
        Grunnskole ->
            "Grunnskole"

        Videreg친endeYrkesskole ->
            "Videreg친endeYrkesskole"

        Fagskole ->
            "Fagskole"

        Folkeh칮yskole ->
            "Folkeh칮yskole"

        H칮yereUtdanning1til4 ->
            "H칮yereUtdanning1til4"

        H칮yereUtdanning4pluss ->
            "H칮yereUtdanning4pluss"

        Doktorgrad ->
            "Doktorgrad"


postEllerPutUtdanning : (Result Error (List Utdanning) -> msg) -> ValidertUtdanningSkjema -> Cmd msg
postEllerPutUtdanning msgConstructor skjema =
    case (Skjema.tilUvalidertSkjema >> Skjema.id) skjema of
        Just id ->
            Api.putUtdanning msgConstructor skjema id

        Nothing ->
            Api.postUtdanning msgConstructor skjema


init : DebugStatus -> FerdigAnimertMeldingsLogg -> List Utdanning -> ( Model, Cmd Msg )
init debugStatus gammelMeldingsLogg utdanningListe =
    let
        aktivSamtale =
            Intro utdanningListe
    in
    ( Model
        { seksjonsMeldingsLogg =
            gammelMeldingsLogg
                |> MeldingsLogg.tilMeldingsLogg
                |> MeldingsLogg.leggTilSp칮rsm친l (samtaleTilMeldingsLogg aktivSamtale)
        , aktivSamtale = aktivSamtale
        , utdanningListe = utdanningListe
        , debugStatus = debugStatus
        }
    , lagtTilSp칮rsm친lCmd debugStatus
    )


subscriptions : Model -> Sub Msg
subscriptions (Model model) =
    Sub.batch
        [ Browser.Events.onVisibilityChange WindowEndrerVisibility
        , model.seksjonsMeldingsLogg
            |> SamtaleAnimasjon.subscriptions
            |> Sub.map SamtaleAnimasjonMsg
        ]
