module Seksjon.Personalia exposing
    ( Model
    , Msg
    , SamtaleStatus(..)
    , init
    , meldingsLogg
    , update
    , viewBrukerInput
    )

import Api
import Browser.Dom as Dom
import DebugStatus exposing (DebugStatus)
import Feilmelding
import FrontendModuler.Containers as Containers exposing (KnapperLayout(..))
import FrontendModuler.Input as Input
import FrontendModuler.Knapp as Knapp
import Html exposing (..)
import Html.Attributes exposing (..)
import Http
import Json.Encode
import Melding exposing (Melding(..))
import MeldingsLogg exposing (FerdigAnimertMeldingsLogg, FerdigAnimertStatus(..), MeldingsLogg)
import Personalia exposing (Personalia)
import Poststed exposing (Poststed)
import Process
import SamtaleAnimasjon
import Skjema.Personalia exposing (PersonaliaSkjema, ValidertPersonaliaSkjema)
import Task



--- MODEL ---


type Model
    = Model ModelInfo


type alias ModelInfo =
    { seksjonsMeldingsLogg : MeldingsLogg
    , aktivSamtale : Samtale
    , personalia : Personalia
    , debugStatus : DebugStatus
    }


type Samtale
    = BekreftOriginal Personalia
    | EndreOriginal PersonaliaSkjema
    | LagrerEndring PersonaliaSkjema
    | LagringFeilet Http.Error PersonaliaSkjema
    | VenterP친AnimasjonF칮rFullf칮ring Personalia


type SamtaleStatus
    = IkkeFerdig ( Model, Cmd Msg )
    | Ferdig Personalia FerdigAnimertMeldingsLogg


meldingsLogg : Model -> MeldingsLogg
meldingsLogg (Model model) =
    model.seksjonsMeldingsLogg



--- UPDATE ---


type Msg
    = OriginalPersonaliaBekreftet
    | BrukerVilEndreOriginalPersonalia
    | PersonaliaSkjemaEndret Skjema.Personalia.Felt String
    | PoststedHentet String (Result Http.Error Poststed)
    | PoststedfeltEndretSelvOmDetErDisabled String
    | PersonaliaskjemaLagreknappTrykket ValidertPersonaliaSkjema
    | PersonaliaskjemaLagreknappTrykketSelvOmDenErDisabled
    | PersonaliaOppdatert (Result Http.Error Personalia)
    | ViewportSatt (Result Dom.Error ())
    | Start칀Skrive
    | Fullf칮rMelding
    | ErrorLogget


update : Msg -> Model -> SamtaleStatus
update msg (Model model) =
    case msg of
        OriginalPersonaliaBekreftet ->
            ( Model
                { model
                    | seksjonsMeldingsLogg =
                        model.seksjonsMeldingsLogg
                            |> MeldingsLogg.leggTilSvar (Melding.svar [ "Ja, informasjonen er riktig" ])
                            |> MeldingsLogg.leggTilSp칮rsm친l
                                [ Melding.sp칮rsm친l [ "S친 bra! 游땕 N친 kan arbeidsgivere kontakte deg." ]
                                , Melding.sp칮rsm친l [ "Da kan vi g친 videre til utfylling av CV-en." ]
                                ]
                    , aktivSamtale = VenterP친AnimasjonF칮rFullf칮ring model.personalia
                }
            , lagtTilSp칮rsm친lCmd model.debugStatus
            )
                |> IkkeFerdig

        BrukerVilEndreOriginalPersonalia ->
            ( model.personalia
                |> Skjema.Personalia.init
                |> EndreOriginal
                |> nesteSamtaleSteg model (Melding.svar [ "Nei, jeg vil endre" ])
            , lagtTilSp칮rsm친lCmd model.debugStatus
            )
                |> IkkeFerdig

        PersonaliaSkjemaEndret felt string ->
            case model.aktivSamtale of
                EndreOriginal skjema ->
                    ( Skjema.Personalia.oppdaterFelt felt skjema string
                        |> EndreOriginal
                        |> oppdaterSamtaleSteg model
                    , case felt of
                        Skjema.Personalia.Postnummer ->
                            if String.length string == 4 && String.toInt string /= Nothing then
                                Api.hentPoststed (PoststedHentet string) string

                            else
                                Cmd.none

                        _ ->
                            Cmd.none
                    )
                        |> IkkeFerdig

                _ ->
                    ( Model model, Cmd.none )
                        |> IkkeFerdig

        PoststedHentet postnummer result ->
            case result of
                Ok poststed ->
                    case model.aktivSamtale of
                        EndreOriginal skjema ->
                            ( skjema
                                |> Skjema.Personalia.oppdaterPoststed poststed
                                |> EndreOriginal
                                |> oppdaterSamtaleSteg model
                            , Cmd.none
                            )
                                |> IkkeFerdig

                        _ ->
                            IkkeFerdig ( Model model, Cmd.none )

                Err error ->
                    case error of
                        Http.BadStatus 404 ->
                            IkkeFerdig ( Model model, Cmd.none )

                        _ ->
                            IkkeFerdig
                                ( Model model
                                , error
                                    |> Feilmelding.feilmelding ("Hent poststed for postnummer: \"" ++ postnummer ++ "\"")
                                    |> Maybe.map (Api.logError (always ErrorLogget))
                                    |> Maybe.withDefault Cmd.none
                                )

        PoststedfeltEndretSelvOmDetErDisabled _ ->
            IkkeFerdig ( Model model, Cmd.none )

        PersonaliaskjemaLagreknappTrykket validertSkjema ->
            case model.aktivSamtale of
                EndreOriginal skjema ->
                    ( skjema
                        |> LagrerEndring
                        |> nesteSamtaleSteg model
                            (skjema
                                |> personaliaSkjemaOppsummering
                                |> Melding.svar
                            )
                    , Cmd.batch
                        [ model.personalia
                            |> Personalia.id
                            |> Api.putPersonalia PersonaliaOppdatert validertSkjema
                        , lagtTilSp칮rsm친lCmd model.debugStatus
                        ]
                    )
                        |> IkkeFerdig

                _ ->
                    ( Model model, Cmd.none )
                        |> IkkeFerdig

        PersonaliaskjemaLagreknappTrykketSelvOmDenErDisabled ->
            IkkeFerdig ( Model model, Cmd.none )

        PersonaliaOppdatert result ->
            case model.aktivSamtale of
                LagrerEndring skjema ->
                    case result of
                        Ok personalia ->
                            fullf칮rSeksjonHvisMeldingsloggErFerdig model personalia

                        Err error ->
                            ( LagringFeilet error skjema
                                |> nesteSamtaleSteg model (Melding.sp칮rsm친l [ "Noe gikk galt" ])
                            , Cmd.batch
                                [ lagtTilSp칮rsm친lCmd model.debugStatus
                                , model.personalia
                                    |> Personalia.id
                                    |> Json.Encode.string
                                    |> Tuple.pair "id"
                                    |> List.singleton
                                    |> Json.Encode.object
                                    |> Api.logErrorWithRequestBody ErrorLogget "Lagre personalia" error
                                ]
                            )
                                |> IkkeFerdig

                _ ->
                    ( Model model, Cmd.none )
                        |> IkkeFerdig

        ViewportSatt _ ->
            ( Model model, Cmd.none )
                |> IkkeFerdig

        Start칀Skrive ->
            ( Model
                { model
                    | seksjonsMeldingsLogg =
                        MeldingsLogg.start칀Skrive model.seksjonsMeldingsLogg
                }
            , Cmd.batch
                [ SamtaleAnimasjon.scrollTilBunn ViewportSatt
                , MeldingsLogg.nesteMeldingToString model.seksjonsMeldingsLogg
                    * 1000.0
                    |> DebugStatus.meldingsTimeout model.debugStatus
                    |> Process.sleep
                    |> Task.perform (always Fullf칮rMelding)
                ]
            )
                |> IkkeFerdig

        Fullf칮rMelding ->
            model.seksjonsMeldingsLogg
                |> MeldingsLogg.fullf칮rMelding
                |> updateEtterFullf칮rtMelding model

        ErrorLogget ->
            IkkeFerdig ( Model model, Cmd.none )


updateEtterFullf칮rtMelding : ModelInfo -> MeldingsLogg -> SamtaleStatus
updateEtterFullf칮rtMelding model nyMeldingsLogg =
    case MeldingsLogg.ferdigAnimert nyMeldingsLogg of
        FerdigAnimert ferdigAnimertSamtale ->
            case model.aktivSamtale of
                VenterP친AnimasjonF칮rFullf칮ring personalia ->
                    Ferdig personalia ferdigAnimertSamtale

                _ ->
                    ( Model
                        { model
                            | seksjonsMeldingsLogg =
                                nyMeldingsLogg
                        }
                    , SamtaleAnimasjon.scrollTilBunn ViewportSatt
                    )
                        |> IkkeFerdig

        MeldingerGjenst친r ->
            ( Model
                { model
                    | seksjonsMeldingsLogg =
                        nyMeldingsLogg
                }
            , lagtTilSp칮rsm친lCmd model.debugStatus
            )
                |> IkkeFerdig


fullf칮rSeksjonHvisMeldingsloggErFerdig : ModelInfo -> Personalia -> SamtaleStatus
fullf칮rSeksjonHvisMeldingsloggErFerdig modelInfo personalia =
    case MeldingsLogg.ferdigAnimert modelInfo.seksjonsMeldingsLogg of
        FerdigAnimert ferdigAnimertMeldingsLogg ->
            Ferdig personalia ferdigAnimertMeldingsLogg

        MeldingerGjenst친r ->
            ( Model { modelInfo | aktivSamtale = VenterP친AnimasjonF칮rFullf칮ring personalia }, Cmd.none )
                |> IkkeFerdig


lagtTilSp칮rsm친lCmd : DebugStatus -> Cmd Msg
lagtTilSp칮rsm친lCmd debugStatus =
    Cmd.batch
        [ SamtaleAnimasjon.scrollTilBunn ViewportSatt
        , 200
            |> DebugStatus.meldingsTimeout debugStatus
            |> Process.sleep
            |> Task.perform (always Start칀Skrive)
        ]


nesteSamtaleSteg : ModelInfo -> Melding -> Samtale -> Model
nesteSamtaleSteg model melding samtaleSeksjon =
    Model
        { model
            | aktivSamtale = samtaleSeksjon
            , seksjonsMeldingsLogg =
                model.seksjonsMeldingsLogg
                    |> MeldingsLogg.leggTilSvar melding
                    |> MeldingsLogg.leggTilSp칮rsm친l (samtaleTilMeldingsLogg samtaleSeksjon)
        }


oppdaterSamtaleSteg : ModelInfo -> Samtale -> Model
oppdaterSamtaleSteg model samtaleSeksjon =
    Model
        { model
            | aktivSamtale = samtaleSeksjon
        }


samtaleTilMeldingsLogg : Samtale -> List Melding
samtaleTilMeldingsLogg personaliaSeksjon =
    case personaliaSeksjon of
        BekreftOriginal personalia ->
            [ Melding.sp칮rsm친l [ "Da setter vi i gang 游땕" ]
            , Melding.sp칮rsm친l [ "Jeg har hentet inn kontaktinformasjonen din, den vises p친 CV-en. Det er viktig at informasjonen er riktig, slik at arbeidsgivere kan kontakte deg." ]
            , Melding.sp칮rsm친l
                (List.concat
                    [ personalia
                        |> Skjema.Personalia.init
                        |> personaliaSkjemaOppsummering
                    , [ Melding.tomLinje
                      , "Er kontaktinformasjonen riktig?"
                      ]
                    ]
                )
            ]

        EndreOriginal _ ->
            [ Melding.sp칮rsm친l
                [ "Ok! Vennligst skriv inn riktig informasjon i feltene under:" ]
            ]

        LagrerEndring _ ->
            [ Melding.sp칮rsm친l
                [ "Godt jobbet! Da tar jeg vare p친 den nye infoen!" ]
            ]

        LagringFeilet _ _ ->
            [ Melding.sp칮rsm친l
                [ "Oops.. Noe gikk galt!" ]
            ]

        VenterP친AnimasjonF칮rFullf칮ring _ ->
            []


personaliaSkjemaOppsummering : PersonaliaSkjema -> List String
personaliaSkjemaOppsummering skjema =
    [ "Navn: " ++ Skjema.Personalia.fornavn skjema ++ " " ++ Skjema.Personalia.etternavn skjema
    , "F칮dselsdato: " ++ (skjema |> Skjema.Personalia.fodselsdato |> viewDatoString)
    , "E-post: " ++ Skjema.Personalia.epost skjema
    , "Telefonnummer: " ++ Skjema.Personalia.telefon skjema
    , "Adresse: "
        ++ Skjema.Personalia.gateadresse skjema
        ++ " "
        ++ Skjema.Personalia.postnummer skjema
        ++ " "
        ++ Skjema.Personalia.poststed skjema
    ]


viewDatoString : String -> String
viewDatoString datoString =
    case String.split "-" datoString of
        친r :: m친ned :: dag :: [] ->
            dag ++ "." ++ m친ned ++ "." ++ 친r

        _ ->
            datoString



--- VIEW ---


viewBrukerInput : Model -> Html Msg
viewBrukerInput (Model { aktivSamtale, seksjonsMeldingsLogg }) =
    case MeldingsLogg.ferdigAnimert seksjonsMeldingsLogg of
        FerdigAnimert _ ->
            case aktivSamtale of
                BekreftOriginal _ ->
                    Containers.knapper Flytende
                        [ Knapp.knapp OriginalPersonaliaBekreftet "Ja, informasjonen er riktig"
                            |> Knapp.toHtml
                        , Knapp.knapp BrukerVilEndreOriginalPersonalia "Nei, jeg vil endre"
                            |> Knapp.toHtml
                        ]

                EndreOriginal personaliaSkjema ->
                    -- TODO: Endre til og bruke Container og til at feilmeldinger kun kommer etter blur/lagring
                    div [ class "skjema-wrapper" ]
                        [ div [ class "skjema" ]
                            [ personaliaSkjema
                                |> Skjema.Personalia.fornavn
                                |> Input.input { label = "Fornavn*", msg = PersonaliaSkjemaEndret Skjema.Personalia.Fornavn }
                                |> Input.withMaybeFeilmelding (Skjema.Personalia.fornavnFeilmelding personaliaSkjema)
                                |> Input.toHtml
                            , personaliaSkjema
                                |> Skjema.Personalia.etternavn
                                |> Input.input { label = "Etternavn*", msg = PersonaliaSkjemaEndret Skjema.Personalia.Etternavn }
                                |> Input.withMaybeFeilmelding (Skjema.Personalia.etternavnFeilmelding personaliaSkjema)
                                |> Input.toHtml
                            , personaliaSkjema
                                |> Skjema.Personalia.epost
                                |> Input.input { label = "E-post*", msg = PersonaliaSkjemaEndret Skjema.Personalia.Epost }
                                |> Input.withMaybeFeilmelding (Skjema.Personalia.epostFeilmelding personaliaSkjema)
                                |> Input.toHtml
                            , personaliaSkjema
                                |> Skjema.Personalia.telefon
                                |> Input.input { label = "Telefon*", msg = PersonaliaSkjemaEndret Skjema.Personalia.Telefon }
                                |> Input.withMaybeFeilmelding (Skjema.Personalia.telefonFeilmelding personaliaSkjema)
                                |> Input.toHtml
                            , personaliaSkjema
                                |> Skjema.Personalia.gateadresse
                                |> Input.input { label = "Gateadresse", msg = PersonaliaSkjemaEndret Skjema.Personalia.Gateadresse }
                                |> Input.toHtml
                            , div [ class "PersonaliaSeksjon-poststed" ]
                                [ personaliaSkjema
                                    |> Skjema.Personalia.postnummer
                                    |> Input.input { label = "Postnummer", msg = PersonaliaSkjemaEndret Skjema.Personalia.Postnummer }
                                    |> Input.withMaybeFeilmelding (Skjema.Personalia.postnummerFeilmelding personaliaSkjema)
                                    |> Input.toHtml
                                , personaliaSkjema
                                    |> Skjema.Personalia.poststed
                                    |> Input.input { label = "Poststed", msg = PoststedfeltEndretSelvOmDetErDisabled }
                                    |> Input.withEnabled Input.Disabled
                                    |> Input.toHtml
                                ]
                            , case Skjema.Personalia.validerSkjema personaliaSkjema of
                                Just validertSkjema ->
                                    div []
                                        [ Knapp.knapp (PersonaliaskjemaLagreknappTrykket validertSkjema) "Lagre endringer"
                                            |> Knapp.toHtml
                                        ]

                                Nothing ->
                                    div []
                                        [ Knapp.knapp PersonaliaskjemaLagreknappTrykketSelvOmDenErDisabled "Lagre endringer"
                                            |> Knapp.withEnabled Knapp.Disabled
                                            |> Knapp.toHtml
                                        ]
                            ]
                        ]

                LagrerEndring _ ->
                    text ""

                LagringFeilet _ _ ->
                    text ""

                VenterP친AnimasjonF칮rFullf칮ring _ ->
                    text ""

        MeldingerGjenst친r ->
            text ""



--- INIT ---


init : DebugStatus -> Personalia -> MeldingsLogg -> ( Model, Cmd Msg )
init debugStatus personalia gammelMeldingsLogg =
    let
        aktivSamtale =
            BekreftOriginal personalia
    in
    ( Model
        { seksjonsMeldingsLogg =
            MeldingsLogg.leggTilSp칮rsm친l
                (samtaleTilMeldingsLogg aktivSamtale)
                gammelMeldingsLogg
        , aktivSamtale = aktivSamtale
        , personalia = personalia
        , debugStatus = debugStatus
        }
    , lagtTilSp칮rsm친lCmd debugStatus
    )
