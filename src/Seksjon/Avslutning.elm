module Seksjon.Avslutning exposing
    ( Model
    , Msg
    , SamtaleStatus(..)
    , init
    , meldingsLogg
    , update
    , viewBrukerInput
    )

-- MODEL --

import Api
import Browser.Dom as Dom
import Cv.Cv exposing (Cv)
import DebugStatus exposing (DebugStatus)
import Feilmelding
import FrontendModuler.Containers as Containers exposing (KnapperLayout(..))
import FrontendModuler.Knapp as Knapp
import Html exposing (..)
import Html.Attributes exposing (..)
import Http
import Melding exposing (Melding)
import MeldingsLogg exposing (FerdigAnimertMeldingsLogg, FerdigAnimertStatus(..), MeldingsLogg, tilMeldingsLogg)
import Person exposing (Person)
import Personalia exposing (Personalia)
import Process
import SamtaleAnimasjon
import Task


type Model
    = Model ModelInfo


type alias ModelInfo =
    { seksjonsMeldingsLogg : MeldingsLogg
    , aktivSamtale : Samtale
    , cv : Cv
    , personalia : Personalia
    , debugStatus : DebugStatus
    }


type Samtale
    = Intro
    | UnderOppf칮lging
    | DelMedArbeidsgiver Bool
    | AvsluttendeOrd
    | LagringSynlighetFeilet
    | HentPersonFeilet
    | VenterP친AnimasjonF칮rFullf칮ring


type SamtaleStatus
    = IkkeFerdig ( Model, Cmd Msg )
    | Ferdig Model FerdigAnimertMeldingsLogg


meldingsLogg : Model -> MeldingsLogg
meldingsLogg (Model model) =
    model.seksjonsMeldingsLogg



-- UPDATE --


type Msg
    = BrukerGodkjennerSynligCV
    | BrukerGodkjennerIkkeSynligCV
    | BrukerVilHentePersonP친Nytt
    | BrukerVilAvslutte String
    | PersonHentet (Result Http.Error Person)
    | SynlighetPostet (Result Http.Error Bool)
    | ViewportSatt (Result Dom.Error ())
    | Start칀Skrive
    | Fullf칮rMelding
    | ErrorLogget


update : Msg -> Model -> SamtaleStatus
update msg (Model model) =
    case msg of
        ViewportSatt _ ->
            ( Model model, Cmd.none )
                |> IkkeFerdig

        Start칀Skrive ->
            ( Model
                { model
                    | seksjonsMeldingsLogg =
                        MeldingsLogg.start칀Skrive model.seksjonsMeldingsLogg
                }
            , Cmd.batch
                [ SamtaleAnimasjon.scrollTilBunn ViewportSatt
                , (MeldingsLogg.nesteMeldingToString model.seksjonsMeldingsLogg * 1000.0)
                    |> DebugStatus.meldingsTimeout model.debugStatus
                    |> Process.sleep
                    |> Task.perform (always Fullf칮rMelding)
                ]
            )
                |> IkkeFerdig

        Fullf칮rMelding ->
            model.seksjonsMeldingsLogg
                |> MeldingsLogg.fullf칮rMelding
                |> updateEtterFullf칮rtMelding model

        PersonHentet result ->
            case result of
                Ok value ->
                    if Person.underOppfolging value then
                        ( nesteSamtaleStegUtenMelding model UnderOppf칮lging
                        , lagtTilSp칮rsm친lCmd model.debugStatus
                        )
                            |> IkkeFerdig

                    else if Person.cvSynligForArbeidsgiver value then
                        ( nesteSamtaleStegUtenMelding model (DelMedArbeidsgiver True)
                        , lagtTilSp칮rsm친lCmd model.debugStatus
                        )
                            |> IkkeFerdig

                    else
                        ( nesteSamtaleStegUtenMelding model (DelMedArbeidsgiver False)
                        , lagtTilSp칮rsm친lCmd model.debugStatus
                        )
                            |> IkkeFerdig

                Err error ->
                    ( nesteSamtaleStegUtenMelding model HentPersonFeilet
                    , Cmd.batch
                        [ lagtTilSp칮rsm친lCmd model.debugStatus
                        , error
                            |> Feilmelding.feilmelding "Hente person"
                            |> Maybe.map (Api.logError (always ErrorLogget))
                            |> Maybe.withDefault Cmd.none
                        ]
                    )
                        |> IkkeFerdig

        BrukerGodkjennerSynligCV ->
            ( Model
                { model
                    | seksjonsMeldingsLogg =
                        model.seksjonsMeldingsLogg
                            |> MeldingsLogg.leggTilSvar (Melding.svar [ "Ja, CV-en skal v칝re synlig for arbeidsgivere" ])
                }
            , Cmd.batch
                [ lagtTilSp칮rsm친lCmd model.debugStatus
                , Api.postSynlighet SynlighetPostet True
                ]
            )
                |> IkkeFerdig

        BrukerGodkjennerIkkeSynligCV ->
            ( Model
                { model
                    | seksjonsMeldingsLogg =
                        model.seksjonsMeldingsLogg
                            |> MeldingsLogg.leggTilSvar (Melding.svar [ "Nei, CV-en skal bare v칝re synlig for meg" ])
                }
            , Cmd.batch
                [ lagtTilSp칮rsm친lCmd model.debugStatus
                , Api.postSynlighet SynlighetPostet False
                ]
            )
                |> IkkeFerdig

        SynlighetPostet result ->
            case result of
                Ok _ ->
                    ( nesteSamtaleStegUtenMelding model AvsluttendeOrd, lagtTilSp칮rsm친lCmd model.debugStatus ) |> IkkeFerdig

                Err error ->
                    ( nesteSamtaleStegUtenMelding model LagringSynlighetFeilet
                    , Cmd.batch
                        [ lagtTilSp칮rsm친lCmd model.debugStatus
                        , error
                            |> Feilmelding.feilmelding "Lagre synlighet"
                            |> Maybe.map (Api.logError (always ErrorLogget))
                            |> Maybe.withDefault Cmd.none
                        ]
                    )
                        |> IkkeFerdig

        BrukerVilHentePersonP친Nytt ->
            ( Model
                { model
                    | seksjonsMeldingsLogg =
                        model.seksjonsMeldingsLogg
                            |> MeldingsLogg.leggTilSvar (Melding.svar [ "Ja, pr칮v p친 nytt" ])
                }
            , Cmd.batch
                [ lagtTilSp칮rsm친lCmd model.debugStatus
                , hentSynlighet
                ]
            )
                |> IkkeFerdig

        BrukerVilAvslutte knappeTekst ->
            ( nesteSamtaleSteg model (Melding.svar [ knappeTekst ]) AvsluttendeOrd
            , lagtTilSp칮rsm친lCmd model.debugStatus
            )
                |> IkkeFerdig

        ErrorLogget ->
            IkkeFerdig ( Model model, Cmd.none )


nesteSamtaleSteg : ModelInfo -> Melding -> Samtale -> Model
nesteSamtaleSteg model melding samtaleSeksjon =
    Model
        { model
            | aktivSamtale = samtaleSeksjon
            , seksjonsMeldingsLogg =
                model.seksjonsMeldingsLogg
                    |> MeldingsLogg.leggTilSvar melding
                    |> MeldingsLogg.leggTilSp칮rsm친l (samtaleTilMeldingsLogg samtaleSeksjon)
        }


nesteSamtaleStegUtenMelding : ModelInfo -> Samtale -> Model
nesteSamtaleStegUtenMelding model samtaleSeksjon =
    Model
        { model
            | aktivSamtale = samtaleSeksjon
            , seksjonsMeldingsLogg =
                model.seksjonsMeldingsLogg
                    |> MeldingsLogg.leggTilSp칮rsm친l (samtaleTilMeldingsLogg samtaleSeksjon)
        }


updateEtterFullf칮rtMelding : ModelInfo -> MeldingsLogg -> SamtaleStatus
updateEtterFullf칮rtMelding model nyMeldingsLogg =
    case MeldingsLogg.ferdigAnimert nyMeldingsLogg of
        FerdigAnimert ferdigAnimertSamtale ->
            case model.aktivSamtale of
                VenterP친AnimasjonF칮rFullf칮ring ->
                    Ferdig (Model model) ferdigAnimertSamtale

                _ ->
                    ( Model
                        { model
                            | seksjonsMeldingsLogg =
                                nyMeldingsLogg
                        }
                    , SamtaleAnimasjon.scrollTilBunn ViewportSatt
                    )
                        |> IkkeFerdig

        MeldingerGjenst친r ->
            ( Model
                { model
                    | seksjonsMeldingsLogg =
                        nyMeldingsLogg
                }
            , lagtTilSp칮rsm친lCmd model.debugStatus
            )
                |> IkkeFerdig


lagtTilSp칮rsm친lCmd : DebugStatus -> Cmd Msg
lagtTilSp칮rsm친lCmd debugStatus =
    Cmd.batch
        [ SamtaleAnimasjon.scrollTilBunn ViewportSatt
        , 200
            |> DebugStatus.meldingsTimeout debugStatus
            |> Process.sleep
            |> Task.perform (always Start칀Skrive)
        ]


samtaleTilMeldingsLogg : Samtale -> List Melding
samtaleTilMeldingsLogg avslutningsSeksjon =
    case avslutningsSeksjon of
        VenterP친AnimasjonF칮rFullf칮ring ->
            []

        Intro ->
            [ Melding.sp칮rsm친l
                [ "I denne CV-tjenesten kan arbeidsgivere s칮ke opp CV-en din. Hvis de har en ledig jobb du kan passe til, kan de ta kontakt." ]
            ]

        DelMedArbeidsgiver synlig ->
            if synlig then
                [ Melding.sp칮rsm친l
                    [ "CV-en din er allerede synlig for arbeidsgivere!"
                    , "칒nsker du fremdeles at arbeidsgivere skal kunne se CV-en din?"
                    ]
                ]

            else
                [ Melding.sp칮rsm친l
                    [ "칒nsker du at arbeidsgivere skal kunne se CV-en din?" ]
                ]

        UnderOppf칮lging ->
            [ Melding.sp칮rsm친l [ "Siden du er under oppf칮lging fra NAV, s친 vil CV-en din v칝re synlig for arbeidsgivere og NAV-veiledere." ]
            , Melding.sp칮rsm친l [ "Bra innsats! 游녨游녨 Alt du har lagt inn er n친 lagret i CV-en din." ]
            , Melding.sp칮rsm친l [ "Da er vi ferdige med CV-en. Husk at du n친r som helst kan endre og forbedre den." ]
            , Melding.sp칮rsm친l [ "Lykke til med jobbjakten! 游땕" ]
            ]

        AvsluttendeOrd ->
            [ Melding.sp칮rsm친l [ "Bra innsats! 游녨游녨 Alt du har lagt inn er n친 lagret i CV-en din." ]
            , Melding.sp칮rsm친l [ "Da er vi ferdige med CV-en. Husk at du n친r som helst kan endre og forbedre den." ]
            , Melding.sp칮rsm친l [ "Lykke til med jobbjakten! 游땕" ]
            ]

        HentPersonFeilet ->
            [ Melding.sp칮rsm친l
                [ "Oisann. Jeg klarte ikke 친 hente informasjonen for 친 fullf칮re registreringen."
                , "Vil du pr칮ve p친 nytt?"
                ]
            ]

        LagringSynlighetFeilet ->
            [ Melding.sp칮rsm친l
                [ "Oops. Jeg klarte ikke 친 lagre informasjonen."
                , "Vil du pr칮ve p친 nytt?"
                ]
            ]



-- VIEW --


viewBrukerInput : Model -> Html Msg
viewBrukerInput (Model model) =
    case MeldingsLogg.ferdigAnimert model.seksjonsMeldingsLogg of
        FerdigAnimert _ ->
            case model.aktivSamtale of
                AvsluttendeOrd ->
                    Containers.knapper Flytende
                        [ a [ href "/cv/forhandsvis", class "avslutt-knapp" ]
                            [ div [ class "Knapp" ]
                                [ text "Avslutt og vis CV-en min" ]
                            ]
                        ]

                VenterP친AnimasjonF칮rFullf칮ring ->
                    text ""

                DelMedArbeidsgiver _ ->
                    Containers.knapper Flytende
                        [ Knapp.knapp BrukerGodkjennerSynligCV "Ja, CV-en skal v칝re synlig for arbeidsgivere"
                            |> Knapp.toHtml
                        , Knapp.knapp BrukerGodkjennerIkkeSynligCV "Nei, CV-en skal bare v칝re synlig for meg"
                            |> Knapp.toHtml
                        ]

                Intro ->
                    text ""

                UnderOppf칮lging ->
                    Containers.knapper Flytende
                        [ a [ href "/cv/forhandsvis", class "avslutt-knapp" ]
                            [ div [ class "Knapp" ]
                                [ text "Avslutt og vis CV-en min" ]
                            ]
                        ]

                HentPersonFeilet ->
                    Containers.knapper Flytende
                        [ Knapp.knapp BrukerVilHentePersonP친Nytt "Ja, pr칮v p친 nytt"
                            |> Knapp.toHtml
                        , Knapp.knapp (BrukerVilAvslutte "Nei, jeg gj칮r det senere") "Nei, jeg gj칮r det senere"
                            |> Knapp.toHtml
                        ]

                LagringSynlighetFeilet ->
                    Containers.knapper Flytende
                        [ Knapp.knapp BrukerGodkjennerSynligCV "Ja, CV-en skal v칝re synlig for arbeidsgivere"
                            |> Knapp.toHtml
                        , Knapp.knapp BrukerGodkjennerIkkeSynligCV "Nei, CV-en skal bare v칝re synlig for meg"
                            |> Knapp.toHtml
                        , Knapp.knapp (BrukerVilAvslutte "Avslutt, jeg gj칮r det senere") "Avslutt, jeg gj칮r det senere"
                            |> Knapp.toHtml
                        ]

        MeldingerGjenst친r ->
            text ""



-- INIT --


hentSynlighet : Cmd Msg
hentSynlighet =
    Api.getPerson PersonHentet


init : DebugStatus -> Personalia -> Cv -> FerdigAnimertMeldingsLogg -> ( Model, Cmd Msg )
init debugStatus personalia cv gammelMeldingsLogg =
    let
        aktivSamtale =
            Intro
    in
    ( Model
        { seksjonsMeldingsLogg =
            MeldingsLogg.leggTilSp칮rsm친l (samtaleTilMeldingsLogg aktivSamtale) (tilMeldingsLogg gammelMeldingsLogg)
        , aktivSamtale = aktivSamtale
        , personalia = personalia
        , cv = cv
        , debugStatus = debugStatus
        }
    , Cmd.batch
        [ lagtTilSp칮rsm친lCmd debugStatus
        , hentSynlighet
        ]
    )
