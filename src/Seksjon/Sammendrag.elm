module Seksjon.Sammendrag exposing
    ( Model
    , Msg
    , SamtaleStatus(..)
    , init
    , meldingsLogg
    , update
    , viewBrukerInput
    )

import Api
import Browser.Dom as Dom
import Cv.Sammendrag exposing (Sammendrag)
import DebugStatus exposing (DebugStatus)
import FrontendModuler.Knapp as Knapp
import FrontendModuler.Textarea as Textarea
import Html exposing (..)
import Html.Attributes exposing (..)
import Http
import Melding exposing (Melding)
import MeldingsLogg exposing (FerdigAnimertMeldingsLogg, FerdigAnimertStatus(..), MeldingsLogg, tilMeldingsLogg)
import Process
import SamtaleAnimasjon
import Task



-- MODEL --


type Model
    = Model ModelInfo


type alias ModelInfo =
    { seksjonsMeldingsLogg : MeldingsLogg
    , aktivSamtale : Samtale
    , sammendrag : String
    , debugStatus : DebugStatus
    }


type Samtale
    = BekreftOriginal String
    | EndreOriginal String
    | LagrerEndring String
    | LagringFeilet Http.Error String
    | VenterP친AnimasjonF칮rFullf칮ring


type SamtaleStatus
    = IkkeFerdig ( Model, Cmd Msg )
    | Ferdig FerdigAnimertMeldingsLogg


meldingsLogg : Model -> MeldingsLogg
meldingsLogg (Model model) =
    model.seksjonsMeldingsLogg



-- UPDATE --


type Msg
    = OriginalSammendragBekreftet
    | BrukerVilEndreSammendrag String
    | SammendragEndret String
    | BrukerVilLagreSammendrag String
    | SammendragOppdatert (Result Http.Error Sammendrag)
    | BrukerVilAvslutteSeksjonen
    | ViewportSatt (Result Dom.Error ())
    | FokusSatt (Result Dom.Error ())
    | Start칀Skrive
    | Fullf칮rMelding
    | ErrorLogget


update : Msg -> Model -> SamtaleStatus
update msg (Model model) =
    case msg of
        OriginalSammendragBekreftet ->
            ( Model
                { model
                    | seksjonsMeldingsLogg =
                        model.seksjonsMeldingsLogg
                            |> MeldingsLogg.leggTilSvar (Melding.svar [ "Nei, g친 videre" ])
                            |> MeldingsLogg.leggTilSp칮rsm친l [ Melding.sp칮rsm친l [ "Flott! Da er vi nesten ferdige!" ] ]
                    , aktivSamtale = VenterP친AnimasjonF칮rFullf칮ring
                }
            , lagtTilSp칮rsm친lCmd model.debugStatus
            )
                |> IkkeFerdig

        BrukerVilEndreSammendrag sammendrag ->
            ( model.sammendrag
                |> EndreOriginal
                |> nesteSamtaleSteg model
                    (if model.sammendrag == "" then
                        Melding.svar [ "Jeg vil legge til" ]

                     else
                        Melding.svar [ "Ja, jeg vil se over" ]
                    )
            , lagtTilSp칮rsm친lCmd model.debugStatus
            )
                |> IkkeFerdig

        SammendragEndret nyttSammendrag ->
            ( oppdaterSamtaleSteg { model | sammendrag = nyttSammendrag } (EndreOriginal nyttSammendrag)
            , lagtTilSp칮rsm친lCmd model.debugStatus
            )
                |> IkkeFerdig

        BrukerVilLagreSammendrag sammendrag ->
            case model.aktivSamtale of
                LagringFeilet error feiletSammendrag ->
                    ( nesteSamtaleSteg model (Melding.svar [ "Pr칮v p친 nytt" ]) (LagrerEndring feiletSammendrag)
                    , Cmd.batch
                        [ lagtTilSp칮rsm친lCmd model.debugStatus
                        , leggSammendragTilAPI sammendrag
                        ]
                    )
                        |> IkkeFerdig

                EndreOriginal _ ->
                    ( nesteSamtaleSteg model (Melding.svar [ sammendrag ]) (LagrerEndring sammendrag)
                    , Cmd.batch
                        [ lagtTilSp칮rsm친lCmd model.debugStatus
                        , leggSammendragTilAPI sammendrag
                        ]
                    )
                        |> IkkeFerdig

                _ ->
                    ( Model model, Cmd.none ) |> IkkeFerdig

        BrukerVilAvslutteSeksjonen ->
            ( nesteSamtaleSteg model (Melding.svar [ "Nei, g친 videre" ]) VenterP친AnimasjonF칮rFullf칮ring
            , lagtTilSp칮rsm친lCmd model.debugStatus
            )
                |> IkkeFerdig

        ViewportSatt result ->
            ( Model model, Cmd.none )
                |> IkkeFerdig

        Start칀Skrive ->
            ( Model
                { model
                    | seksjonsMeldingsLogg =
                        MeldingsLogg.start칀Skrive model.seksjonsMeldingsLogg
                }
            , Cmd.batch
                [ SamtaleAnimasjon.scrollTilBunn ViewportSatt
                , (MeldingsLogg.nesteMeldingToString model.seksjonsMeldingsLogg * 1000.0)
                    |> DebugStatus.meldingsTimeout model.debugStatus
                    |> Process.sleep
                    |> Task.perform (always Fullf칮rMelding)
                ]
            )
                |> IkkeFerdig

        Fullf칮rMelding ->
            model.seksjonsMeldingsLogg
                |> MeldingsLogg.fullf칮rMelding
                |> updateEtterFullf칮rtMelding model

        SammendragOppdatert result ->
            case model.aktivSamtale of
                LagrerEndring sammendrag ->
                    case result of
                        Ok value ->
                            ( Model
                                { model
                                    | sammendrag = Cv.Sammendrag.sammendrag value
                                    , aktivSamtale = VenterP친AnimasjonF칮rFullf칮ring
                                    , seksjonsMeldingsLogg =
                                        model.seksjonsMeldingsLogg
                                            |> MeldingsLogg.leggTilSp칮rsm친l [ Melding.sp칮rsm친l [ "Veldig bra! N친 er vi ferdig med det vanskeligste 游땕" ] ]
                                }
                            , lagtTilSp칮rsm친lCmd model.debugStatus
                            )
                                |> IkkeFerdig

                        Err error ->
                            ( nesteSamtaleSteg model (Melding.sp칮rsm친l [ "Oisann.. Klarte ikke 친 lagre!" ]) (LagringFeilet error sammendrag)
                            , Cmd.batch
                                [ lagtTilSp칮rsm친lCmd model.debugStatus
                                , sammendrag
                                    |> Api.encodeSammendrag
                                    |> Api.logErrorWithRequestBody ErrorLogget "Lagre sammendrag" error
                                ]
                            )
                                |> IkkeFerdig

                _ ->
                    ( Model model, Cmd.none ) |> IkkeFerdig

        ErrorLogget ->
            IkkeFerdig ( Model model, Cmd.none )

        FokusSatt _ ->
            IkkeFerdig ( Model model, Cmd.none )


leggSammendragTilAPI : String -> Cmd Msg
leggSammendragTilAPI sammendrag =
    Api.putSammendrag SammendragOppdatert sammendrag


updateEtterFullf칮rtMelding : ModelInfo -> MeldingsLogg -> SamtaleStatus
updateEtterFullf칮rtMelding model nyMeldingsLogg =
    case MeldingsLogg.ferdigAnimert nyMeldingsLogg of
        FerdigAnimert ferdigAnimertSamtale ->
            case model.aktivSamtale of
                VenterP친AnimasjonF칮rFullf칮ring ->
                    Ferdig ferdigAnimertSamtale

                _ ->
                    ( Model
                        { model
                            | seksjonsMeldingsLogg =
                                nyMeldingsLogg
                        }
                    , Cmd.batch
                        [ SamtaleAnimasjon.scrollTilBunn ViewportSatt
                        , case model.aktivSamtale of
                            EndreOriginal _ ->
                                settFokusCmd sammendragId

                            _ ->
                                Cmd.none
                        ]
                    )
                        |> IkkeFerdig

        MeldingerGjenst친r ->
            ( Model
                { model
                    | seksjonsMeldingsLogg =
                        nyMeldingsLogg
                }
            , lagtTilSp칮rsm친lCmd model.debugStatus
            )
                |> IkkeFerdig


lagtTilSp칮rsm친lCmd : DebugStatus -> Cmd Msg
lagtTilSp칮rsm친lCmd debugStatus =
    Cmd.batch
        [ SamtaleAnimasjon.scrollTilBunn ViewportSatt
        , 200
            |> DebugStatus.meldingsTimeout debugStatus
            |> Process.sleep
            |> Task.perform (always Start칀Skrive)
        ]


settFokusCmd : String -> Cmd Msg
settFokusCmd id =
    id
        |> Dom.focus
        |> Task.attempt FokusSatt


samtaleTilMeldingsLogg : Samtale -> List Melding
samtaleTilMeldingsLogg sammendragSeksjon =
    case sammendragSeksjon of
        BekreftOriginal sammendrag ->
            if sammendrag == "" then
                [ Melding.sp칮rsm친l [ "N친 skal du skrive et sammendrag. Her har du mulighet til 친 selge deg inn. Fortell arbeidsgivere om kompetansen din og personlige egenskaper." ] ]

            else
                [ Melding.sp칮rsm친l [ "N친 skal vi skrive et sammendrag." ]
                , Melding.sp칮rsm친l
                    [ "Du har allerede skrevet dette..."
                    ]
                , Melding.sp칮rsm친l [ sammendrag ]
                , Melding.sp칮rsm친l [ "Vil du legge til eller endre p친 noe?" ]
                ]

        EndreOriginal string ->
            [ Melding.sp칮rsm친l [ "Ok! Fyll ut sammendraget ditt i boksen under." ] ]

        LagrerEndring string ->
            []

        LagringFeilet error string ->
            [ Melding.sp칮rsm친l
                [ "Sjekk at du er p친 internett og pr칮v igjen!" ]
            ]

        VenterP친AnimasjonF칮rFullf칮ring ->
            [ Melding.sp칮rsm친l [ "Veldig bra! N친 er vi ferdig med det vanskeligste 游땕" ] ]


nesteSamtaleSteg : ModelInfo -> Melding -> Samtale -> Model
nesteSamtaleSteg model melding samtaleSeksjon =
    Model
        { model
            | aktivSamtale = samtaleSeksjon
            , seksjonsMeldingsLogg =
                model.seksjonsMeldingsLogg
                    |> MeldingsLogg.leggTilSvar melding
                    |> MeldingsLogg.leggTilSp칮rsm친l (samtaleTilMeldingsLogg samtaleSeksjon)
        }


oppdaterSamtaleSteg : ModelInfo -> Samtale -> Model
oppdaterSamtaleSteg model samtaleSeksjon =
    Model
        { model
            | aktivSamtale = samtaleSeksjon
        }



-- VIEW --


viewBrukerInput : Model -> Html Msg
viewBrukerInput (Model model) =
    case MeldingsLogg.ferdigAnimert model.seksjonsMeldingsLogg of
        FerdigAnimert ferdigAnimertMeldingsLogg ->
            case model.aktivSamtale of
                BekreftOriginal sammendrag ->
                    div [ class "skjema-wrapper" ]
                        [ div [ class "skjema" ]
                            [ div [ class "inputkolonne" ]
                                [ Knapp.knapp (BrukerVilEndreSammendrag sammendrag)
                                    (if sammendrag == "" then
                                        "Jeg vil legge til sammendrag"

                                     else
                                        "Ja, jeg vil se over"
                                    )
                                    |> Knapp.withClass Knapp.Spr친kniv친Knapp
                                    |> Knapp.toHtml
                                , Knapp.knapp BrukerVilAvslutteSeksjonen "Nei, g친 videre"
                                    |> Knapp.withClass Knapp.Spr친kniv친Knapp
                                    |> Knapp.toHtml
                                ]
                            ]
                        ]

                EndreOriginal sammendrag ->
                    div [ class "skjema-wrapper" ]
                        [ div [ class "skjema" ]
                            [ Textarea.textarea { label = "Sammendrag", msg = SammendragEndret } sammendrag
                                |> Textarea.withTextAreaClass "textarea_stor"
                                |> Textarea.withId sammendragId
                                |> Textarea.toHtml
                            , Knapp.knapp (BrukerVilLagreSammendrag sammendrag) "G친 videre"
                                |> Knapp.toHtml
                            ]
                        ]

                LagrerEndring string ->
                    text ""

                LagringFeilet error sammendrag ->
                    div [ class "inputkolonne" ]
                        [ div [ class "inputkolonne-innhold" ]
                            [ Knapp.knapp (BrukerVilLagreSammendrag sammendrag) "Pr칮v p친 nytt"
                                |> Knapp.withClass Knapp.Spr친kniv친Knapp
                                |> Knapp.toHtml
                            , Knapp.knapp BrukerVilAvslutteSeksjonen "G친 videre uten 친 lagre"
                                |> Knapp.withClass Knapp.Spr친kniv친Knapp
                                |> Knapp.toHtml
                            ]
                        ]

                VenterP친AnimasjonF칮rFullf칮ring ->
                    text ""

        MeldingerGjenst친r ->
            text ""


sammendragId : String
sammendragId =
    "sammendrag-input"



-- INIT --


init : DebugStatus -> FerdigAnimertMeldingsLogg -> Maybe Sammendrag -> ( Model, Cmd Msg )
init debugStatus gammelMeldingsLogg sammendrag =
    let
        sammendragToString =
            case sammendrag of
                Just a ->
                    Cv.Sammendrag.sammendrag a

                Nothing ->
                    ""

        aktivSamtale =
            BekreftOriginal sammendragToString
    in
    ( Model
        { seksjonsMeldingsLogg =
            MeldingsLogg.leggTilSp칮rsm친l (samtaleTilMeldingsLogg aktivSamtale) (tilMeldingsLogg gammelMeldingsLogg)
        , aktivSamtale = aktivSamtale
        , sammendrag = sammendragToString
        , debugStatus = debugStatus
        }
    , lagtTilSp칮rsm친lCmd debugStatus
    )
