module Kurs.Seksjon exposing
    ( Model
    , Msg
    , SamtaleStatus(..)
    , init
    , meldingsLogg
    , sistLagret
    , subscriptions
    , update
    , viewBrukerInput
    )

import Api
import Browser.Dom as Dom
import Browser.Events exposing (Visibility(..))
import Dato.Dato as Dato exposing (칀r)
import Dato.Maned as M친ned exposing (M친ned(..))
import DebugStatus exposing (DebugStatus)
import ErrorHandtering as ErrorH친ndtering exposing (OperasjonEtterError(..))
import FrontendModuler.BrukerInput as BrukerInput exposing (BrukerInput, KnapperLayout(..))
import FrontendModuler.BrukerInputMedGaVidereKnapp as BrukerInputMedG친VidereKnapp
import FrontendModuler.DatoInput as DatoInput
import FrontendModuler.Feilmelding as Feilmelding
import FrontendModuler.Input as Input
import FrontendModuler.Knapp as Knapp exposing (Knapp)
import FrontendModuler.LoggInnLenke as LoggInnLenke
import FrontendModuler.Select as Select
import FrontendModuler.ValgfriDatoInput as ValgfriDatoInput
import Html exposing (..)
import Html.Attributes exposing (..)
import Http exposing (Error)
import Kurs.Kurs as Kurs exposing (Kurs)
import Kurs.Skjema as Skjema exposing (Felt(..), Fullf칮rtDato(..), KursSkjema, ValidertKursSkjema, VarighetEnhet(..))
import LagreStatus exposing (LagreStatus)
import Meldinger.Melding as Melding exposing (Melding(..))
import Meldinger.MeldingsLogg as MeldingsLogg exposing (FerdigAnimertMeldingsLogg, FerdigAnimertStatus(..), MeldingsLogg)
import Meldinger.SamtaleAnimasjon as SamtaleAnimasjon
import Meldinger.SamtaleOppdatering exposing (SamtaleOppdatering(..))
import Process
import Task
import Tid exposing (nyesteSistLagretVerdi)
import Time exposing (Posix)



--- MODEL ---


type Model
    = Model ModelInfo


type alias ModelInfo =
    { seksjonsMeldingsLogg : MeldingsLogg
    , aktivSamtale : Samtale
    , kursListe : List Kurs
    , debugStatus : DebugStatus
    , sistLagretFraForrigeSeksjon : Posix
    }


meldingsLogg : Model -> MeldingsLogg
meldingsLogg (Model model) =
    model.seksjonsMeldingsLogg


sistLagret : Model -> Posix
sistLagret (Model model) =
    model.kursListe
        |> List.map Kurs.sistEndretDato
        |> nyesteSistLagretVerdi model.sistLagretFraForrigeSeksjon


type SamtaleStatus
    = IkkeFerdig ( Model, Cmd Msg )
    | Ferdig Posix (List Kurs) FerdigAnimertMeldingsLogg


type AvsluttetGrunn
    = AvbruttP친begynt
    | SlettetP친begynt
    | AnnenAvslutning


type OppsummeringsType
    = F칮rsteGang
    | EtterEndring
    | Avbr칮tSletting


type Samtale
    = RegistrerKursnavn KursnavnInfo
    | RegistrerKursholder KursholderInfo
    | Sp칮rOmBrukerVilLeggeInnFullf칮rtDato KursholderInfo
    | RegistrerFullf칮rtDato Fullf칮rtDatoInfo
    | RegistrerVarighetEnhet VarighetInfo
    | RegistrerVarighet VarighetInfo
    | VisOppsummering OppsummeringsType ValidertKursSkjema
    | EndreOpplysninger KursSkjema
    | BekreftSlettingAvP친begynt ValidertKursSkjema
    | LagrerSkjema ValidertKursSkjema LagreStatus
    | LagringFeilet Http.Error ValidertKursSkjema
    | BekreftAvbrytingAvRegistreringen Samtale
    | VenterP친AnimasjonF칮rFullf칮ring (List Kurs) AvsluttetGrunn


type alias KursnavnInfo =
    { kursnavn : String
    , tillat칀ViseFeilmeldingKursnavn : Bool
    }


type alias KursholderInfo =
    { kursnavn : String
    , kursholder : String
    }


type alias Fullf칮rtDatoInfo =
    { kursnavn : String
    , kursholder : String
    , fullf칮rt칀r : String
    , fullf칮rtM친ned : M친ned
    , tillat칀ViseFeilmelding칀r : Bool
    }


type alias VarighetInfo =
    { kursnavn : String
    , kursholder : String
    , fullf칮rtDato : Fullf칮rtDato
    , varighet : String
    , varighetEnhet : VarighetEnhet
    , tillat칀ViseFeilmeldingVarighet : Bool
    }


kursnavnTilKursholder : String -> KursholderInfo
kursnavnTilKursholder kursnavn =
    { kursnavn = kursnavn
    , kursholder = ""
    }


kursholderTilFullf칮rtDato : KursholderInfo -> Fullf칮rtDatoInfo
kursholderTilFullf칮rtDato input =
    { kursnavn = input.kursnavn
    , kursholder = input.kursholder
    , fullf칮rt칀r = ""
    , fullf칮rtM친ned = Januar
    , tillat칀ViseFeilmelding칀r = False
    }


kursholderTilVarighet : KursholderInfo -> VarighetInfo
kursholderTilVarighet input =
    { kursnavn = input.kursnavn
    , kursholder = input.kursholder
    , fullf칮rtDato = IkkeOppgitt
    , varighet = ""
    , varighetEnhet = Time
    , tillat칀ViseFeilmeldingVarighet = False
    }


fullf칮rtDatoTilVarighet : Fullf칮rtDatoInfo -> 칀r -> VarighetInfo
fullf칮rtDatoTilVarighet input 친r =
    { kursnavn = input.kursnavn
    , kursholder = input.kursholder
    , fullf칮rtDato = Oppgitt input.fullf칮rtM친ned 친r
    , varighet = ""
    , varighetEnhet = Time
    , tillat칀ViseFeilmeldingVarighet = False
    }


varighetTilSkjema : VarighetInfo -> ValidertKursSkjema
varighetTilSkjema info =
    Skjema.initValidertSkjema
        { kursnavn = info.kursnavn
        , kursholder = info.kursholder
        , fullf칮rtDato = info.fullf칮rtDato
        , varighet = String.toInt info.varighet
        , varighetEnhet = info.varighetEnhet
        , id = Nothing
        }



--- UPDATE ---


type Msg
    = VilRegistrereKursnavn
    | OppdatererKursnavn String
    | VilRegistrereKursholder
    | OppdatererKursholder String
    | SvarerJaTilFullf칮rtDato
    | SvarerNeiTilFullf칮rtDato
    | OppdatererFullf칮rtM친ned String
    | OppdatererFullf칮rt칀r String
    | VilRegistrereFullf칮rtDato
    | VarighetEnhetValgt VarighetEnhet
    | VilRegistrereVarighet
    | OppdatererVarighet String
    | VilLagreKurs
    | VilEndreOpplysninger
    | SkjemaEndret SkjemaEndring
    | VilSletteP친begynt
    | BekrefterSlettP친begynt
    | AngrerSlettP친begynt
    | VilLagreEndretSkjema
    | KursLagret (Result Http.Error (List Kurs))
    | VilAvbryteRegistreringen
    | BekrefterAvbrytingAvRegistrering
    | VilIkkeAvbryteRegistreringen
    | FerdigMedKurs
    | WindowEndrerVisibility Visibility
    | SamtaleAnimasjonMsg SamtaleAnimasjon.Msg
    | FokusSatt (Result Dom.Error ())
    | FeltMisterFokus
    | TimeoutEtterAtFeltMistetFokus
    | ErrorLogget


type SkjemaEndring
    = Tekst Felt String
    | Fullf칮rtM친ned String
    | VarighetEnhet String
    | Fullf칮rt칀rBlurred
    | KursnavnBlurred
    | VarighetBlurred


update : Msg -> Model -> SamtaleStatus
update msg (Model model) =
    case msg of
        VilRegistrereKursnavn ->
            case model.aktivSamtale of
                RegistrerKursnavn info ->
                    case Skjema.feilmeldingKursnavn info.kursnavn of
                        Just _ ->
                            ( { info | tillat칀ViseFeilmeldingKursnavn = True }
                                |> RegistrerKursnavn
                                |> oppdaterSamtale model IngenNyeMeldinger
                            , Cmd.none
                            )
                                |> IkkeFerdig

                        Nothing ->
                            ( info.kursnavn
                                |> kursnavnTilKursholder
                                |> RegistrerKursholder
                                |> oppdaterSamtale model (SvarFraMsg msg)
                            , lagtTilSp칮rsm친lCmd model.debugStatus
                            )
                                |> IkkeFerdig

                _ ->
                    IkkeFerdig ( Model model, Cmd.none )

        OppdatererKursnavn string ->
            case model.aktivSamtale of
                RegistrerKursnavn info ->
                    ( { info | kursnavn = string }
                        |> RegistrerKursnavn
                        |> oppdaterSamtale model IngenNyeMeldinger
                    , Cmd.none
                    )
                        |> IkkeFerdig

                _ ->
                    IkkeFerdig ( Model model, Cmd.none )

        VilRegistrereKursholder ->
            case model.aktivSamtale of
                RegistrerKursholder input ->
                    case Skjema.feilmeldingKursholder input.kursholder of
                        Nothing ->
                            ( input
                                |> Sp칮rOmBrukerVilLeggeInnFullf칮rtDato
                                |> oppdaterSamtale model (SvarFraMsg msg)
                            , lagtTilSp칮rsm친lCmd model.debugStatus
                            )
                                |> IkkeFerdig

                        Just _ ->
                            IkkeFerdig ( Model model, Cmd.none )

                _ ->
                    IkkeFerdig ( Model model, Cmd.none )

        OppdatererKursholder string ->
            case model.aktivSamtale of
                RegistrerKursholder kursholder ->
                    ( { kursholder | kursholder = string }
                        |> RegistrerKursholder
                        |> oppdaterSamtale model IngenNyeMeldinger
                    , Cmd.none
                    )
                        |> IkkeFerdig

                _ ->
                    IkkeFerdig ( Model model, Cmd.none )

        SvarerJaTilFullf칮rtDato ->
            case model.aktivSamtale of
                Sp칮rOmBrukerVilLeggeInnFullf칮rtDato info ->
                    ( info
                        |> kursholderTilFullf칮rtDato
                        |> RegistrerFullf칮rtDato
                        |> oppdaterSamtale model (SvarFraMsg msg)
                    , lagtTilSp칮rsm친lCmd model.debugStatus
                    )
                        |> IkkeFerdig

                _ ->
                    IkkeFerdig ( Model model, Cmd.none )

        SvarerNeiTilFullf칮rtDato ->
            case model.aktivSamtale of
                Sp칮rOmBrukerVilLeggeInnFullf칮rtDato info ->
                    ( info
                        |> kursholderTilVarighet
                        |> RegistrerVarighetEnhet
                        |> oppdaterSamtale model (SvarFraMsg msg)
                    , lagtTilSp칮rsm친lCmd model.debugStatus
                    )
                        |> IkkeFerdig

                _ ->
                    IkkeFerdig ( Model model, Cmd.none )

        OppdatererFullf칮rtM친ned m친nedString ->
            case model.aktivSamtale of
                RegistrerFullf칮rtDato fullf칮rtDatoInfo ->
                    ( { fullf칮rtDatoInfo | fullf칮rtM친ned = M친ned.stringTilM친ned m친nedString }
                        |> RegistrerFullf칮rtDato
                        |> oppdaterSamtale model IngenNyeMeldinger
                    , Cmd.none
                    )
                        |> IkkeFerdig

                _ ->
                    IkkeFerdig ( Model model, Cmd.none )

        OppdatererFullf칮rt칀r 친r ->
            case model.aktivSamtale of
                RegistrerFullf칮rtDato fullf칮rtDatoInfo ->
                    ( { fullf칮rtDatoInfo | fullf칮rt칀r = 친r }
                        |> RegistrerFullf칮rtDato
                        |> oppdaterSamtale model IngenNyeMeldinger
                    , Cmd.none
                    )
                        |> IkkeFerdig

                _ ->
                    IkkeFerdig ( Model model, Cmd.none )

        VilRegistrereFullf칮rtDato ->
            case model.aktivSamtale of
                RegistrerFullf칮rtDato fullf칮rtDatoInfo ->
                    case Dato.stringTil칀r fullf칮rtDatoInfo.fullf칮rt칀r of
                        Just fullf칮rt칀r ->
                            ( fullf칮rt칀r
                                |> fullf칮rtDatoTilVarighet fullf칮rtDatoInfo
                                |> RegistrerVarighetEnhet
                                |> oppdaterSamtale model (SvarFraMsg msg)
                            , lagtTilSp칮rsm친lCmd model.debugStatus
                            )
                                |> IkkeFerdig

                        Nothing ->
                            ( { fullf칮rtDatoInfo | tillat칀ViseFeilmelding칀r = True }
                                |> RegistrerFullf칮rtDato
                                |> oppdaterSamtale model IngenNyeMeldinger
                            , Cmd.none
                            )
                                |> IkkeFerdig

                _ ->
                    IkkeFerdig ( Model model, Cmd.none )

        VarighetEnhetValgt enhet ->
            case model.aktivSamtale of
                RegistrerVarighetEnhet varighetInfo ->
                    ( { varighetInfo | varighetEnhet = enhet }
                        |> RegistrerVarighet
                        |> oppdaterSamtale model (SvarFraMsg msg)
                    , lagtTilSp칮rsm친lCmd model.debugStatus
                    )
                        |> IkkeFerdig

                _ ->
                    IkkeFerdig ( Model model, Cmd.none )

        VilRegistrereVarighet ->
            case model.aktivSamtale of
                RegistrerVarighet info ->
                    case Skjema.feilmeldingVarighet info.varighet of
                        Just _ ->
                            ( { info | tillat칀ViseFeilmeldingVarighet = True }
                                |> RegistrerVarighet
                                |> oppdaterSamtale model IngenNyeMeldinger
                            , Cmd.none
                            )
                                |> IkkeFerdig

                        Nothing ->
                            ( info
                                |> varighetTilSkjema
                                |> VisOppsummering F칮rsteGang
                                |> oppdaterSamtale model (SvarFraMsg msg)
                            , lagtTilSp칮rsm친lCmd model.debugStatus
                            )
                                |> IkkeFerdig

                _ ->
                    IkkeFerdig ( Model model, Cmd.none )

        OppdatererVarighet varighet_ ->
            case model.aktivSamtale of
                RegistrerVarighet varighetInfo ->
                    ( { varighetInfo | varighet = varighet_ }
                        |> RegistrerVarighet
                        |> oppdaterSamtale model IngenNyeMeldinger
                    , Cmd.none
                    )
                        |> IkkeFerdig

                _ ->
                    IkkeFerdig ( Model model, Cmd.none )

        FeltMisterFokus ->
            IkkeFerdig ( Model model, mistetFokusCmd )

        TimeoutEtterAtFeltMistetFokus ->
            case model.aktivSamtale of
                RegistrerKursnavn kursnavnInfo ->
                    ( { kursnavnInfo | tillat칀ViseFeilmeldingKursnavn = True }
                        |> RegistrerKursnavn
                        |> oppdaterSamtale model IngenNyeMeldinger
                    , Cmd.none
                    )
                        |> IkkeFerdig

                RegistrerFullf칮rtDato fullf칮rtDatoInfo ->
                    ( { fullf칮rtDatoInfo | tillat칀ViseFeilmelding칀r = True }
                        |> RegistrerFullf칮rtDato
                        |> oppdaterSamtale model IngenNyeMeldinger
                    , Cmd.none
                    )
                        |> IkkeFerdig

                RegistrerVarighet varighetInfo ->
                    ( { varighetInfo | tillat칀ViseFeilmeldingVarighet = True }
                        |> RegistrerVarighet
                        |> oppdaterSamtale model IngenNyeMeldinger
                    , Cmd.none
                    )
                        |> IkkeFerdig

                _ ->
                    IkkeFerdig ( Model model, Cmd.none )

        VilEndreOpplysninger ->
            case model.aktivSamtale of
                VisOppsummering _ validertKursSkjema ->
                    updateEtterVilEndreSkjema model msg validertKursSkjema

                _ ->
                    IkkeFerdig ( Model model, Cmd.none )

        SkjemaEndret skjemaEndring ->
            case model.aktivSamtale of
                EndreOpplysninger kursSkjema ->
                    ( kursSkjema
                        |> oppdaterSkjema skjemaEndring
                        |> EndreOpplysninger
                        |> oppdaterSamtale model IngenNyeMeldinger
                    , Cmd.none
                    )
                        |> IkkeFerdig

                _ ->
                    IkkeFerdig ( Model model, Cmd.none )

        VilSletteP친begynt ->
            case model.aktivSamtale of
                VisOppsummering _ skjema ->
                    ( BekreftSlettingAvP친begynt skjema
                        |> oppdaterSamtale model (SvarFraMsg msg)
                    , lagtTilSp칮rsm친lCmd model.debugStatus
                    )
                        |> IkkeFerdig

                _ ->
                    IkkeFerdig ( Model model, Cmd.none )

        BekrefterSlettP친begynt ->
            case model.aktivSamtale of
                BekreftSlettingAvP친begynt _ ->
                    ( VenterP친AnimasjonF칮rFullf칮ring model.kursListe SlettetP친begynt
                        |> oppdaterSamtale model (SvarFraMsg msg)
                    , lagtTilSp칮rsm친lCmd model.debugStatus
                    )
                        |> IkkeFerdig

                _ ->
                    IkkeFerdig ( Model model, Cmd.none )

        AngrerSlettP친begynt ->
            case model.aktivSamtale of
                BekreftSlettingAvP친begynt skjema ->
                    ( VisOppsummering Avbr칮tSletting skjema
                        |> oppdaterSamtale model (SvarFraMsg msg)
                    , lagtTilSp칮rsm친lCmd model.debugStatus
                    )
                        |> IkkeFerdig

                _ ->
                    IkkeFerdig ( Model model, Cmd.none )

        VilLagreEndretSkjema ->
            case model.aktivSamtale of
                EndreOpplysninger skjema ->
                    case Skjema.valider skjema of
                        Just validertSkjema ->
                            ( validertSkjema
                                |> VisOppsummering EtterEndring
                                |> oppdaterSamtale model (ManueltSvar (Melding.svar (validertSkjemaTilSetninger validertSkjema)))
                            , lagtTilSp칮rsm친lCmd model.debugStatus
                            )
                                |> IkkeFerdig

                        Nothing ->
                            ( skjema
                                |> Skjema.tillat칀ViseAlleFeilmeldinger
                                |> EndreOpplysninger
                                |> oppdaterSamtale model IngenNyeMeldinger
                            , Cmd.none
                            )
                                |> IkkeFerdig

                _ ->
                    IkkeFerdig ( Model model, Cmd.none )

        VilLagreKurs ->
            case model.aktivSamtale of
                VisOppsummering _ skjema ->
                    updateEtterLagreKnappTrykket model msg skjema

                LagringFeilet error skjema ->
                    ( error
                        |> LagreStatus.fraError
                        |> LagrerSkjema skjema
                        |> oppdaterSamtale model (SvarFraMsg msg)
                    , Api.opprettKurs KursLagret skjema
                    )
                        |> IkkeFerdig

                _ ->
                    IkkeFerdig ( Model model, Cmd.none )

        KursLagret result ->
            case model.aktivSamtale of
                LagrerSkjema skjema lagreStatus ->
                    case result of
                        Ok kurs ->
                            let
                                oppdatertMeldingslogg =
                                    if LagreStatus.lagrerEtterUtlogging lagreStatus then
                                        model.seksjonsMeldingsLogg
                                            |> MeldingsLogg.leggTilSvar (Melding.svar [ LoggInnLenke.loggInnLenkeTekst ])
                                            |> MeldingsLogg.leggTilSp칮rsm친l [ Melding.sp칮rsm친l [ "Bra. N친 har du lagt til et kurs 游녨" ] ]

                                    else
                                        model.seksjonsMeldingsLogg
                                            |> MeldingsLogg.leggTilSp칮rsm친l [ Melding.sp칮rsm친l [ "Bra. N친 har du lagt til et kurs 游녨" ] ]
                            in
                            ( VenterP친AnimasjonF칮rFullf칮ring kurs AnnenAvslutning
                                |> oppdaterSamtale { model | seksjonsMeldingsLogg = oppdatertMeldingslogg, kursListe = kurs } UtenSvar
                            , lagtTilSp칮rsm친lCmd model.debugStatus
                            )
                                |> IkkeFerdig

                        Err error ->
                            if LagreStatus.lagrerEtterUtlogging lagreStatus then
                                if LagreStatus.fors칮kP친Nytt lagreStatus then
                                    ( LagreStatus.fraError error
                                        |> LagrerSkjema skjema
                                        |> oppdaterSamtale model IngenNyeMeldinger
                                    , Api.opprettKurs KursLagret skjema
                                    )
                                        |> IkkeFerdig

                                else
                                    ( skjema
                                        |> LagringFeilet error
                                        |> oppdaterSamtale model IngenNyeMeldinger
                                    , skjema
                                        |> Skjema.encode
                                        |> Api.logErrorWithRequestBody ErrorLogget "Lagre kurs" error
                                    )
                                        |> IkkeFerdig

                            else
                                ( skjema
                                    |> LagringFeilet error
                                    |> oppdaterSamtale model UtenSvar
                                , Cmd.batch
                                    [ lagtTilSp칮rsm친lCmd model.debugStatus
                                    , skjema
                                        |> Skjema.encode
                                        |> Api.logErrorWithRequestBody ErrorLogget "Lagre kurs" error
                                    ]
                                )
                                    |> IkkeFerdig

                _ ->
                    IkkeFerdig ( Model model, Cmd.none )

        VilAvbryteRegistreringen ->
            case model.aktivSamtale of
                RegistrerKursnavn _ ->
                    avbrytRegistrering model msg

                _ ->
                    ( model.aktivSamtale
                        |> BekreftAvbrytingAvRegistreringen
                        |> oppdaterSamtale model (SvarFraMsg msg)
                    , lagtTilSp칮rsm친lCmd model.debugStatus
                    )
                        |> IkkeFerdig

        BekrefterAvbrytingAvRegistrering ->
            avbrytRegistrering model msg

        VilIkkeAvbryteRegistreringen ->
            case model.aktivSamtale of
                BekreftAvbrytingAvRegistreringen samtaleStegF칮rAvbryting ->
                    ( Model
                        { model
                            | aktivSamtale = samtaleStegF칮rAvbryting
                            , seksjonsMeldingsLogg =
                                model.seksjonsMeldingsLogg
                                    |> MeldingsLogg.leggTilSvar (svarFraBrukerInput model msg)
                                    |> MeldingsLogg.leggTilSp칮rsm친l
                                        (List.concat
                                            [ [ Melding.sp칮rsm친l [ "Ok. Da fortsetter vi der vi slapp." ] ]
                                            , samtaleTilMeldingsLogg samtaleStegF칮rAvbryting
                                            ]
                                        )
                        }
                    , lagtTilSp칮rsm친lCmd model.debugStatus
                    )
                        |> IkkeFerdig

                _ ->
                    IkkeFerdig ( Model model, Cmd.none )

        FerdigMedKurs ->
            case model.aktivSamtale of
                LagringFeilet _ _ ->
                    ( VenterP친AnimasjonF칮rFullf칮ring model.kursListe AnnenAvslutning
                        |> oppdaterSamtale model (SvarFraMsg msg)
                    , lagtTilSp칮rsm친lCmd model.debugStatus
                    )
                        |> IkkeFerdig

                _ ->
                    IkkeFerdig ( Model model, Cmd.none )

        WindowEndrerVisibility visibility ->
            case visibility of
                Visible ->
                    case model.aktivSamtale of
                        LagrerSkjema skjema lagreStatus ->
                            ( lagreStatus
                                |> LagreStatus.setFors칮kP친Nytt
                                |> LagrerSkjema skjema
                                |> oppdaterSamtale model IngenNyeMeldinger
                            , Cmd.none
                            )
                                |> IkkeFerdig

                        LagringFeilet error skjema ->
                            if ErrorH친ndtering.operasjonEtterError error == LoggInn then
                                IkkeFerdig
                                    ( error
                                        |> LagreStatus.fraError
                                        |> LagrerSkjema skjema
                                        |> oppdaterSamtale model IngenNyeMeldinger
                                    , Api.opprettKurs KursLagret skjema
                                    )

                            else
                                IkkeFerdig ( Model model, Cmd.none )

                        _ ->
                            IkkeFerdig ( Model model, Cmd.none )

                Hidden ->
                    IkkeFerdig ( Model model, Cmd.none )

        SamtaleAnimasjonMsg samtaleAnimasjonMsg ->
            SamtaleAnimasjon.update model.debugStatus samtaleAnimasjonMsg model.seksjonsMeldingsLogg
                |> updateEtterFullf칮rtMelding model

        FokusSatt _ ->
            IkkeFerdig ( Model model, Cmd.none )

        ErrorLogget ->
            IkkeFerdig ( Model model, Cmd.none )


avbrytRegistrering : ModelInfo -> Msg -> SamtaleStatus
avbrytRegistrering model msg =
    ( AvbruttP친begynt
        |> VenterP친AnimasjonF칮rFullf칮ring model.kursListe
        |> oppdaterSamtale model (SvarFraMsg msg)
    , lagtTilSp칮rsm친lCmd model.debugStatus
    )
        |> IkkeFerdig


oppdaterSkjema : SkjemaEndring -> KursSkjema -> KursSkjema
oppdaterSkjema endring skjema =
    case endring of
        Tekst felt innhold ->
            Skjema.oppdaterTekstFelt felt innhold skjema

        Fullf칮rtM친ned m친nedString ->
            m친nedString
                |> M친ned.fraString
                |> Skjema.oppdaterFullf칮rtM친ned skjema

        VarighetEnhet string ->
            string
                |> Skjema.stringTilVarighetEnhet
                |> Skjema.oppdaterVarighetEnhet skjema

        Fullf칮rt칀rBlurred ->
            Skjema.tillat칀ViseFeilmeldingFullf칮rt칀r skjema

        KursnavnBlurred ->
            Skjema.tillat칀ViseFeilmeldingKursnavn skjema

        VarighetBlurred ->
            Skjema.tillat칀ViseFeilmeldingVarighet skjema


updateEtterFullf칮rtMelding : ModelInfo -> ( MeldingsLogg, Cmd SamtaleAnimasjon.Msg ) -> SamtaleStatus
updateEtterFullf칮rtMelding model ( nyMeldingsLogg, cmd ) =
    case MeldingsLogg.ferdigAnimert nyMeldingsLogg of
        FerdigAnimert ferdigAnimertSamtale ->
            case model.aktivSamtale of
                VenterP친AnimasjonF칮rFullf칮ring kursListe _ ->
                    Ferdig (sistLagret (Model model)) kursListe ferdigAnimertSamtale

                _ ->
                    ( Model { model | seksjonsMeldingsLogg = nyMeldingsLogg }
                    , Cmd.batch
                        [ Cmd.map SamtaleAnimasjonMsg cmd
                        , settFokus model.aktivSamtale
                        ]
                    )
                        |> IkkeFerdig

        MeldingerGjenst친r ->
            ( Model { model | seksjonsMeldingsLogg = nyMeldingsLogg }
            , Cmd.map SamtaleAnimasjonMsg cmd
            )
                |> IkkeFerdig


updateEtterVilEndreSkjema : ModelInfo -> Msg -> ValidertKursSkjema -> SamtaleStatus
updateEtterVilEndreSkjema model msg skjema =
    ( skjema
        |> Skjema.tilUvalidertSkjema
        |> EndreOpplysninger
        |> oppdaterSamtale model (SvarFraMsg msg)
    , lagtTilSp칮rsm친lCmd model.debugStatus
    )
        |> IkkeFerdig


updateEtterLagreKnappTrykket : ModelInfo -> Msg -> ValidertKursSkjema -> SamtaleStatus
updateEtterLagreKnappTrykket model msg skjema =
    ( LagreStatus.init
        |> LagrerSkjema skjema
        |> oppdaterSamtale model (SvarFraMsg msg)
    , Api.opprettKurs KursLagret skjema
    )
        |> IkkeFerdig


lagtTilSp칮rsm친lCmd : DebugStatus -> Cmd Msg
lagtTilSp칮rsm친lCmd debugStatus =
    SamtaleAnimasjon.startAnimasjon debugStatus
        |> Cmd.map SamtaleAnimasjonMsg


mistetFokusCmd : Cmd Msg
mistetFokusCmd =
    Process.sleep 100
        |> Task.perform (\_ -> TimeoutEtterAtFeltMistetFokus)


svarFraBrukerInput : ModelInfo -> Msg -> Melding
svarFraBrukerInput modelInfo msg =
    modelInfo
        |> modelTilBrukerInput
        |> BrukerInput.tilSvarMelding msg


oppdaterSamtale : ModelInfo -> SamtaleOppdatering Msg -> Samtale -> Model
oppdaterSamtale model meldingsoppdatering samtale =
    Model
        { model
            | aktivSamtale = samtale
            , seksjonsMeldingsLogg =
                case meldingsoppdatering of
                    IngenNyeMeldinger ->
                        model.seksjonsMeldingsLogg

                    SvarFraMsg msg ->
                        model.seksjonsMeldingsLogg
                            |> MeldingsLogg.leggTilSvar (svarFraBrukerInput model msg)
                            |> MeldingsLogg.leggTilSp칮rsm친l (samtaleTilMeldingsLogg samtale)

                    ManueltSvar melding ->
                        model.seksjonsMeldingsLogg
                            |> MeldingsLogg.leggTilSvar melding
                            |> MeldingsLogg.leggTilSp칮rsm친l (samtaleTilMeldingsLogg samtale)

                    UtenSvar ->
                        model.seksjonsMeldingsLogg
                            |> MeldingsLogg.leggTilSp칮rsm친l (samtaleTilMeldingsLogg samtale)
        }


samtaleTilMeldingsLogg : Samtale -> List Melding
samtaleTilMeldingsLogg kursSeksjon =
    case kursSeksjon of
        RegistrerKursnavn _ ->
            [ Melding.sp칮rsm친l [ "Hva heter kurset du vil legge inn?" ]
            , Melding.sp칮rsm친l
                [ "Har du f칮rstehjelpskurs, kanskje et kurs i legemiddelh친ndtering, eller noe helt annet?" ]
            ]

        RegistrerKursholder _ ->
            [ Melding.sp칮rsm친l [ "Hvilket firma eller organisasjon arrangerte kurset?" ]
            ]

        Sp칮rOmBrukerVilLeggeInnFullf칮rtDato _ ->
            [ Melding.sp칮rsm친l [ "Det kan v칝re nyttig for en arbeidsgiver 친 vite n친r du fullf칮rte kurset. Vil du legge inn det?" ]
            ]

        RegistrerFullf칮rtDato _ ->
            [ Melding.sp칮rsm친l [ "N친r var du ferdig med kurset?" ]
            ]

        RegistrerVarighetEnhet _ ->
            [ Melding.sp칮rsm친l [ "Hvor lenge varte kurset? Var det timer, dager, uker eller m친neder?" ] ]

        RegistrerVarighet info ->
            [ Melding.sp칮rsm친l [ "Hvor mange " ++ (Skjema.varighetEnhetTilString >> String.toLower) info.varighetEnhet ++ " varte kurset?" ] ]

        VisOppsummering oppsummeringsType validertSkjema ->
            case oppsummeringsType of
                Avbr칮tSletting ->
                    [ Melding.sp칮rsm친l [ "Ok, da lar jeg kurset st친." ]
                    , oppsummeringsSp칮rsm친l validertSkjema
                    ]

                EtterEndring ->
                    [ Melding.sp칮rsm친l [ "Du har endret. Er det riktig n친?" ] ]

                F칮rsteGang ->
                    [ oppsummeringsSp칮rsm친l validertSkjema
                    ]

        EndreOpplysninger _ ->
            [ Melding.sp칮rsm친l [ "Gj칮r endringene du 칮nsker i feltene under." ] ]

        BekreftSlettingAvP친begynt _ ->
            [ Melding.sp칮rsm친l [ "Er du sikker p친 at du vil slette dette kurset?" ] ]

        LagrerSkjema _ _ ->
            []

        LagringFeilet error _ ->
            [ ErrorH친ndtering.errorMelding { error = error, operasjon = "lagre kurs" } ]

        BekreftAvbrytingAvRegistreringen _ ->
            [ Melding.sp칮rsm친l [ "Hvis du avbryter, blir ikke kurset lagret p친 CV-en din. Er du sikker p친 at du vil avbryte?" ] ]

        VenterP친AnimasjonF칮rFullf칮ring _ avsluttetGrunn ->
            case avsluttetGrunn of
                AvbruttP친begynt ->
                    [ Melding.sp칮rsm친l [ "N친 har jeg avbrutt. Vil du legge inn flere kategorier?" ] ]

                SlettetP친begynt ->
                    [ Melding.sp칮rsm친l [ "N친 har jeg slettet kurset. Vil du legge inn flere kategorier?" ] ]

                AnnenAvslutning ->
                    [ Melding.sp칮rsm친l [ "Vil du legge inn flere kategorier?" ] ]


validertSkjemaTilSetninger : ValidertKursSkjema -> List String
validertSkjemaTilSetninger validertSkjema =
    let
        skjema =
            Skjema.tilUvalidertSkjema validertSkjema
    in
    [ case Skjema.fullf칮rtDatoValidert validertSkjema of
        Oppgitt m친ned_ 친r_ ->
            [ Dato.datoTilString m친ned_ 친r_ ]

        IkkeOppgitt ->
            []
    , [ "Kursnavn: " ++ Skjema.innholdTekstFelt Kursnavn skjema
      , "Kursholder: " ++ Skjema.innholdTekstFelt Kursholder skjema
      , "Varighet: " ++ Skjema.innholdTekstFelt Varighet skjema ++ " " ++ (skjema |> Skjema.varighetEnhet |> Skjema.varighetEnhetTilString |> String.toLower)
      ]
    ]
        |> List.concat


oppsummeringsSp칮rsm친l : ValidertKursSkjema -> Melding
oppsummeringsSp칮rsm친l skjema =
    [ [ "Du har lagt inn dette:"
      , Melding.tomLinje
      ]
    , validertSkjemaTilSetninger skjema
    , [ Melding.tomLinje
      , "Er informasjonen riktig?"
      ]
    ]
        |> List.concat
        |> Melding.sp칮rsm친l


settFokus : Samtale -> Cmd Msg
settFokus samtale =
    case samtale of
        RegistrerKursnavn _ ->
            settFokusCmd KursnavnId

        RegistrerKursholder _ ->
            settFokusCmd KursholderId

        Sp칮rOmBrukerVilLeggeInnFullf칮rtDato _ ->
            settFokusCmd LeggTilFullf칮rtId

        RegistrerFullf칮rtDato _ ->
            settFokusCmd Fullf칮rtM친nedId

        RegistrerVarighetEnhet _ ->
            settFokusCmd VarighetEnhetId

        RegistrerVarighet _ ->
            settFokusCmd VarighetId

        VisOppsummering _ _ ->
            settFokusCmd BekreftOppsummeringId

        EndreOpplysninger _ ->
            settFokusCmd KursnavnId

        LagringFeilet _ _ ->
            settFokusCmd LagringFeiletActionId

        BekreftSlettingAvP친begynt _ ->
            settFokusCmd SletteP친begyntId

        BekreftAvbrytingAvRegistreringen _ ->
            settFokusCmd AvbrytSlettingId

        _ ->
            Cmd.none


settFokusCmd : InputId -> Cmd Msg
settFokusCmd inputId =
    Process.sleep 200
        |> Task.andThen (\_ -> (inputIdTilString >> Dom.focus) inputId)
        |> Task.attempt FokusSatt



--- VIEW ---


type InputId
    = KursnavnId
    | KursholderId
    | Fullf칮rtM친nedId
    | LeggTilFullf칮rtId
    | VarighetId
    | VarighetEnhetId
    | VarighetEnhetISkjemaId
    | BekreftOppsummeringId
    | SletteP친begyntId
    | LagringFeiletActionId
    | AvbrytSlettingId


inputIdTilString : InputId -> String
inputIdTilString inputId =
    case inputId of
        KursnavnId ->
            "kurs-kursnavn-id"

        KursholderId ->
            "kurs-kursholder-id"

        Fullf칮rtM친nedId ->
            "kurs-fullf칮rtM친ned-id"

        LeggTilFullf칮rtId ->
            "kurs-kursholder-id"

        VarighetId ->
            "kurs-varighet-id"

        VarighetEnhetId ->
            "kurs-varighet-enhet-id"

        BekreftOppsummeringId ->
            "kurs-bekreft-oppsummering-id"

        VarighetEnhetISkjemaId ->
            "kurs-varighet-enhet-skjema-id"

        SletteP친begyntId ->
            "kurs-slett-p친begynt-id"

        LagringFeiletActionId ->
            "kurs-lagring-feilet-id"

        AvbrytSlettingId ->
            "kurs-avbrytt-slett-id"


viewBrukerInput : Model -> Html Msg
viewBrukerInput (Model model) =
    model
        |> modelTilBrukerInput
        |> BrukerInput.toHtml


modelTilBrukerInput : ModelInfo -> BrukerInput Msg
modelTilBrukerInput model =
    if MeldingsLogg.visBrukerInput model.seksjonsMeldingsLogg then
        case model.aktivSamtale of
            RegistrerKursnavn info ->
                BrukerInput.inputMedG친VidereKnapp { onAvbryt = VilAvbryteRegistreringen, onG친Videre = VilRegistrereKursnavn }
                    (info.kursnavn
                        |> Input.input { label = "Kursets navn", msg = OppdatererKursnavn }
                        |> Input.withOnEnter VilRegistrereKursnavn
                        |> Input.withId (inputIdTilString KursnavnId)
                        |> Input.withOnBlur FeltMisterFokus
                        |> Input.withFeilmelding
                            (info.kursnavn
                                |> Skjema.feilmeldingKursnavn
                                |> maybeHvisTrue info.tillat칀ViseFeilmeldingKursnavn
                            )
                        |> Input.withErObligatorisk
                    )

            RegistrerKursholder info ->
                BrukerInput.inputMedG친VidereKnapp { onAvbryt = VilAvbryteRegistreringen, onG친Videre = VilRegistrereKursholder }
                    (info.kursholder
                        |> Input.input { label = "Kursets arrang칮r", msg = OppdatererKursholder }
                        |> Input.withOnEnter VilRegistrereKursholder
                        |> Input.withFeilmelding (Skjema.feilmeldingKursholder info.kursholder)
                        |> Input.withId (inputIdTilString KursholderId)
                    )

            Sp칮rOmBrukerVilLeggeInnFullf칮rtDato _ ->
                BrukerInput.knapper Flytende
                    [ Knapp.knapp SvarerJaTilFullf칮rtDato "Ja, det vil jeg"
                        |> Knapp.withId (inputIdTilString LeggTilFullf칮rtId)
                    , Knapp.knapp SvarerNeiTilFullf칮rtDato "Nei, det vil jeg ikke"
                    ]

            RegistrerFullf칮rtDato fullf칮rtDatoInfo ->
                DatoInput.datoInput
                    { onM친nedChange = OppdatererFullf칮rtM친ned
                    , m친ned = fullf칮rtDatoInfo.fullf칮rtM친ned
                    , on칀rChange = OppdatererFullf칮rt칀r
                    , 친r = fullf칮rtDatoInfo.fullf칮rt칀r
                    }
                    |> DatoInput.withFokusId (inputIdTilString Fullf칮rtM친nedId)
                    |> DatoInput.withFeilmelding칀r
                        (fullf칮rtDatoInfo.fullf칮rt칀r
                            |> Dato.feilmelding칀r
                            |> maybeHvisTrue fullf칮rtDatoInfo.tillat칀ViseFeilmelding칀r
                        )
                    |> DatoInput.withOnBlur칀r FeltMisterFokus
                    |> BrukerInputMedG친VidereKnapp.datoM친ned칀r VilRegistrereFullf칮rtDato
                    |> BrukerInputMedG친VidereKnapp.withAvbrytKnapp VilAvbryteRegistreringen
                    |> BrukerInput.brukerInputMedG친VidereKnapp

            RegistrerVarighetEnhet _ ->
                varighetEnhetKnapper

            RegistrerVarighet info ->
                BrukerInput.inputMedG친VidereKnapp { onAvbryt = VilAvbryteRegistreringen, onG친Videre = VilRegistrereVarighet }
                    (info.varighet
                        |> Input.input { label = "Antall", msg = OppdatererVarighet }
                        |> Input.withWrapperClass "친r-wrapper"
                        |> Input.withOnEnter VilRegistrereVarighet
                        |> Input.withOnBlur FeltMisterFokus
                        |> Input.withId (inputIdTilString VarighetId)
                        |> Input.withFeilmelding
                            (info.varighet
                                |> Skjema.feilmeldingVarighet
                                |> maybeHvisTrue info.tillat칀ViseFeilmeldingVarighet
                            )
                    )

            VisOppsummering _ _ ->
                viewBekreftOppsummering

            EndreOpplysninger skjema ->
                BrukerInput.skjema { lagreMsg = VilLagreEndretSkjema, lagreKnappTekst = "Lagre endringer" }
                    [ skjema
                        |> Skjema.innholdTekstFelt Kursnavn
                        |> Input.input { label = "Kursnavn", msg = Tekst Kursnavn >> SkjemaEndret }
                        |> Input.withFeilmelding (Skjema.feilmeldingKursnavnHvisSynlig skjema)
                        |> Input.withOnBlur (SkjemaEndret KursnavnBlurred)
                        |> Input.withErObligatorisk
                        |> Input.withId (inputIdTilString KursnavnId)
                        |> Input.toHtml
                    , skjema
                        |> Skjema.innholdTekstFelt Kursholder
                        |> Input.input { label = "Kursholder", msg = Tekst Kursholder >> SkjemaEndret }
                        |> Input.withFeilmelding (Skjema.innholdTekstFelt Kursholder skjema |> Skjema.feilmeldingKursholder)
                        |> Input.toHtml
                    , div [ class "DatoInput-fra-til-rad" ]
                        [ ValgfriDatoInput.datoInput
                            { label = "N친r avsluttet du kurset?"
                            , onM친nedChange = Fullf칮rtM친ned >> SkjemaEndret
                            , m친ned = Skjema.fullf칮rtM친ned skjema
                            , on칀rChange = Tekst Fullf칮rt칀r >> SkjemaEndret
                            , 친r = Skjema.innholdTekstFelt Fullf칮rt칀r skjema
                            }
                            |> ValgfriDatoInput.withFeilmelding칀r (Skjema.feilmeldingValgfrittFullf칮rt칀r skjema)
                            |> ValgfriDatoInput.withFeilmeldingPeriode (Skjema.feilmeldingPeriode skjema)
                            |> ValgfriDatoInput.withOnBlur칀r (SkjemaEndret Fullf칮rt칀rBlurred)
                            |> ValgfriDatoInput.toHtml
                        , viewVarighet skjema
                        ]
                    ]

            BekreftSlettingAvP친begynt _ ->
                BrukerInput.knapper Flytende
                    [ Knapp.knapp BekrefterSlettP친begynt "Ja, jeg vil slette"
                        |> Knapp.withId (inputIdTilString SletteP친begyntId)
                    , Knapp.knapp AngrerSlettP친begynt "Nei, jeg vil ikke slette"
                    ]

            LagrerSkjema _ lagreStatus ->
                if LagreStatus.lagrerEtterUtlogging lagreStatus then
                    LoggInnLenke.viewLoggInnLenke

                else
                    BrukerInput.utenInnhold

            LagringFeilet error _ ->
                case ErrorH친ndtering.operasjonEtterError error of
                    ErrorH친ndtering.GiOpp ->
                        BrukerInput.knapper Flytende
                            [ Knapp.knapp FerdigMedKurs "G친 videre"
                                |> Knapp.withId (inputIdTilString LagringFeiletActionId)
                            ]

                    ErrorH친ndtering.Pr칮vP친Nytt ->
                        BrukerInput.knapper Flytende
                            [ Knapp.knapp VilLagreKurs "Pr칮v igjen"
                                |> Knapp.withId (inputIdTilString LagringFeiletActionId)
                            , Knapp.knapp FerdigMedKurs "G친 videre"
                            ]

                    ErrorH친ndtering.LoggInn ->
                        LoggInnLenke.viewLoggInnLenke

            BekreftAvbrytingAvRegistreringen _ ->
                BrukerInput.knapper Flytende
                    [ Knapp.knapp BekrefterAvbrytingAvRegistrering "Ja, jeg vil avbryte"
                        |> Knapp.withId (inputIdTilString AvbrytSlettingId)
                    , Knapp.knapp VilIkkeAvbryteRegistreringen "Nei, jeg vil fortsette"
                    ]

            VenterP친AnimasjonF칮rFullf칮ring _ _ ->
                BrukerInput.utenInnhold

    else
        BrukerInput.utenInnhold


varighetEnhetKnapper : BrukerInput Msg
varighetEnhetKnapper =
    BrukerInput.knapper VarighetGrid
        ((varighetEnhetKnapp Time
            |> Knapp.withId (inputIdTilString VarighetEnhetId)
         )
            :: List.map varighetEnhetKnapp [ Dag, Uke, M친ned ]
        )


varighetEnhetKnapp : VarighetEnhet -> Knapp Msg
varighetEnhetKnapp enhet =
    enhet
        |> Skjema.varighetEnhetTilString
        |> Knapp.knapp (VarighetEnhetValgt enhet)


viewBekreftOppsummering : BrukerInput Msg
viewBekreftOppsummering =
    BrukerInput.knapper Kolonne
        [ Knapp.knapp VilLagreKurs "Ja, det er riktig"
            |> Knapp.withId (inputIdTilString BekreftOppsummeringId)
        , Knapp.knapp VilEndreOpplysninger "Nei, jeg vil endre"
        , Knapp.knapp VilSletteP친begynt "Nei, jeg vil slette"
        ]


viewVarighet : KursSkjema -> Html Msg
viewVarighet skjema =
    fieldset [ class "DatoInput-fieldset Varighet-fieldset" ]
        [ legend [ class "skjemaelement__label" ]
            [ text "Hvor lenge varte kurset?" ]
        , div [ class "Varighet-wrapper" ]
            [ Skjema.innholdTekstFelt Varighet skjema
                |> Input.input { label = "Antall", msg = Tekst Varighet >> SkjemaEndret }
                |> Input.withClass
                    ("Varighet-antall"
                        ++ (case Skjema.feilmeldingVarighetHvisSynlig skjema of
                                Just _ ->
                                    " skjemaelement__input--harFeil"

                                Nothing ->
                                    ""
                           )
                    )
                |> Input.withOnBlur (SkjemaEndret VarighetBlurred)
                |> Input.toHtml
            , Select.select
                "Timer/dager/uker/m친neder"
                (VarighetEnhet >> SkjemaEndret)
                [ ( "Timer", "Timer" )
                , ( "Dager", "Dager" )
                , ( "Uker", "Uker" )
                , ( "M친neder", "M친neder" )
                ]
                |> Select.withSelected
                    (Skjema.varighetEnhetTilString (Skjema.varighetEnhet skjema))
                |> Select.withClass "Varighet-enhet"
                |> Select.toHtml
            ]
        , Feilmelding.htmlFeilmelding (Skjema.feilmeldingVarighetHvisSynlig skjema)
        ]


maybeHvisTrue : Bool -> Maybe a -> Maybe a
maybeHvisTrue bool maybe =
    if bool then
        maybe

    else
        Nothing



--- INIT ---


init : DebugStatus -> Posix -> FerdigAnimertMeldingsLogg -> List Kurs -> ( Model, Cmd Msg )
init debugStatus sistLagretFraForrigeSeksjon gammelMeldingsLogg kursListe =
    let
        aktivSamtale =
            RegistrerKursnavn { kursnavn = "", tillat칀ViseFeilmeldingKursnavn = False }
    in
    ( Model
        { seksjonsMeldingsLogg =
            gammelMeldingsLogg
                |> MeldingsLogg.tilMeldingsLogg
                |> MeldingsLogg.leggTilSp칮rsm친l (samtaleTilMeldingsLogg aktivSamtale)
        , aktivSamtale = aktivSamtale
        , kursListe = kursListe
        , debugStatus = debugStatus
        , sistLagretFraForrigeSeksjon = sistLagretFraForrigeSeksjon
        }
    , lagtTilSp칮rsm친lCmd debugStatus
    )


subscriptions : Model -> Sub Msg
subscriptions (Model model) =
    Sub.batch
        [ Browser.Events.onVisibilityChange WindowEndrerVisibility
        , model.seksjonsMeldingsLogg
            |> SamtaleAnimasjon.subscriptions
            |> Sub.map SamtaleAnimasjonMsg
        ]
