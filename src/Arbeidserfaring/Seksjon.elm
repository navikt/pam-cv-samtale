module Arbeidserfaring.Seksjon exposing
    ( Model
    , Msg
    , SamtaleStatus(..)
    , init
    , meldingsLogg
    , sistLagret
    , subscriptions
    , update
    , viewBrukerInput
    )

import Api
import Arbeidserfaring.Arbeidserfaring as Arbeidserfaring exposing (Arbeidserfaring)
import Arbeidserfaring.Skjema as Skjema exposing (ArbeidserfaringSkjema, Felt(..), ValidertArbeidserfaringSkjema)
import Arbeidserfaring.Yrke as Yrke exposing (Yrke)
import Browser.Dom as Dom
import Browser.Events exposing (Visibility(..))
import Dato.Dato as Dato exposing (TilDato(..), 칀r)
import Dato.Maned as M친ned exposing (M친ned(..))
import DebugStatus exposing (DebugStatus)
import ErrorHandtering as ErrorH친ndtering exposing (OperasjonEtterError(..))
import Feilmelding
import FrontendModuler.BrukerInput as BrukerInput exposing (BrukerInput, KnapperLayout(..))
import FrontendModuler.BrukerInputMedGaVidereKnapp as BrukerInputMedG친VidereKnapp
import FrontendModuler.Checkbox as Checkbox
import FrontendModuler.DatoInput as DatoInput
import FrontendModuler.Input as Input
import FrontendModuler.Knapp as Knapp exposing (Knapp)
import FrontendModuler.LoggInnLenke as LoggInnLenke
import FrontendModuler.Textarea as Textarea
import FrontendModuler.Typeahead
import Html exposing (Attribute, Html, div, text)
import Html.Attributes exposing (class)
import Http exposing (Error)
import LagreStatus exposing (LagreStatus)
import Maybe.Extra as Maybe
import Meldinger.Melding as Melding exposing (Melding, Tekstomr친de(..))
import Meldinger.MeldingsLogg as MeldingsLogg exposing (FerdigAnimertMeldingsLogg, FerdigAnimertStatus(..), MeldingsLogg)
import Meldinger.SamtaleAnimasjon as SamtaleAnimasjon
import Meldinger.SamtaleOppdatering exposing (SamtaleOppdatering(..))
import Process
import Result.Extra as Result
import Task
import Tid exposing (nyesteSistLagretVerdi)
import Time exposing (Posix)
import Typeahead.Typeahead as Typeahead exposing (GetSuggestionStatus(..), InputStatus(..))
import Validering



--- MODEL ---


type Model
    = Model ModelInfo


type alias ModelInfo =
    { seksjonsMeldingsLogg : MeldingsLogg
    , arbeidserfaringListe : List Arbeidserfaring
    , aktivSamtale : Samtale
    , debugStatus : DebugStatus
    , sistLagretFraForrigeSeksjon : Posix
    , harFullf칮rtEnSamtale : Bool
    }


meldingsLogg : Model -> MeldingsLogg
meldingsLogg (Model model) =
    model.seksjonsMeldingsLogg


sistLagret : Model -> Posix
sistLagret (Model model) =
    model.arbeidserfaringListe
        |> List.map Arbeidserfaring.sistEndretDato
        |> nyesteSistLagretVerdi model.sistLagretFraForrigeSeksjon


type SamtaleStatus
    = IkkeFerdig ( Model, Cmd Msg )
    | Ferdig Posix FerdigAnimertMeldingsLogg


type AvsluttetGrunn
    = AvbruttP친begynt
    | SlettetP친begynt
    | AnnenAvslutning
    | EndretEksisterende


type OppsummeringsType
    = F칮rsteGang
    | EtterEndring
    | Avbr칮tSletting
    | NyArbeidserfaring


type SkjemaType
    = OppsummeringsSkjema
    | NyArbeidserfaringsSkjema


type Samtale
    = Intro
    | VelgEnArbeidserfaring칀Redigere
    | RegistrerYrke { visFeilmelding : Bool } (Typeahead.Model Yrke)
    | Sp칮rOmBrukerVilEndreJobbtittel JobbtittelInfo
    | EndreJobbtittel JobbtittelInfo
    | RegistrereBedriftsnavn BedriftnavnInfo
    | RegistrereSted StedInfo
    | RegistrereArbeidsoppgaver Bool ArbeidsoppgaverInfo
    | RegistrereFraDato FraDatoInfo
    | RegistrereN친v칝rende N친v칝rendeInfo
    | RegistrereTilDato TilDatoInfo
    | VisOppsummering OppsummeringsType ValidertArbeidserfaringSkjema
    | RedigerSkjema SkjemaType (Typeahead.Model Yrke) ArbeidserfaringSkjema
    | BekreftSlettingAvP친begynt ValidertArbeidserfaringSkjema
    | LagrerArbeidserfaring ValidertArbeidserfaringSkjema LagreStatus
    | LagringFeilet Http.Error ValidertArbeidserfaringSkjema
    | Sp칮rOmBrukerVilLeggeInnMer (List Arbeidserfaring) AvsluttetGrunn
    | BekreftAvbrytingAvRegistreringen Samtale
    | VenterP친AnimasjonF칮rFullf칮ring String



--- UPDATE ---


type Msg
    = BrukerVilLeggeTilNyArbeidserfaring
    | BrukerVilRedigereArbeidserfaring
    | BrukerHarValgtArbeidserfaring칀Redigere Arbeidserfaring
    | BrukerHopperOverArbeidserfaring
    | TypeaheadMsg (Typeahead.Msg Yrke)
    | HentetYrkeTypeahead Typeahead.Query (Result Http.Error (List Yrke))
    | FeltMisterFokus
    | TimeoutEtterAtFeltMistetFokus
    | BrukerVilRegistrereYrke
    | BrukerVilEndreJobbtittel
    | BrukerVilIkkeEndreJobbtittel
    | BrukerOppdatererJobbtittelFelt String
    | BrukerVilRegistrereJobbtittel
    | BrukerOppdatererBedriftsnavn String
    | BrukerVilRegistrereBedriftsnavn
    | BrukerOppdatererSted String
    | BrukerVilRegistrereSted
    | VilSeEksempel
    | BrukerOppdatererArbeidsoppgaver String
    | BrukerVilRegistrereArbeidsoppgaver
    | BrukerOppdatererFraM친ned String
    | BrukerOppdatererFra칀r String
    | BrukerVilRegistrereFraDato
    | BrukerSvarerJaTilN친v칝rende
    | BrukerSvarerNeiTilN친v칝rende
    | BrukerOppdatererTilM친ned String
    | BrukerOppdatererTil칀r String
    | BrukerVilRegistrereTilDato
    | BrukerVilRedigereOppsummering
    | SkjemaEndret SkjemaEndring
    | VilSletteP친begynt
    | BekrefterSlettP친begynt
    | AngrerSlettP친begynt
    | BrukerVilLagreArbeidserfaringIOppsummering
    | BrukerVilLagreArbeidserfaringSkjema
    | ArbeidserfaringLagret (Result Http.Error (List Arbeidserfaring))
    | BrukerVilPr칮ve칀LagreP친Nytt
    | BrukerVilAvbryteLagringen
    | BrukerVilAvbryteRegistreringen
    | BrukerBekrefterAvbrytingAvRegistrering
    | BrukerVilIkkeAvbryteRegistreringen
    | VilLeggeTilFlereArbeidserfaringer
    | FerdigMedArbeidserfaring
    | WindowEndrerVisibility Visibility
    | SamtaleAnimasjonMsg SamtaleAnimasjon.Msg
    | FokusSatt (Result Dom.Error ())
    | ErrorLogget


type SkjemaEndring
    = Tekst Felt String
    | N친v칝rendeToggled
    | FraM친ned String
    | TilM친ned String
    | Fra칀rBlurred
    | Til칀rBlurred


type alias JobbtittelInfo =
    { tidligereInfo : Yrke
    , jobbtittel : String
    }


type alias BedriftnavnInfo =
    { tidligereInfo : JobbtittelInfo
    , bedriftNavn : String
    }


type alias StedInfo =
    { tidligereInfo : BedriftnavnInfo
    , lokasjon : String
    }


type alias ArbeidsoppgaverInfo =
    { tidligereInfo : StedInfo
    , arbeidsoppgaver : String
    }


type alias FraDatoInfo =
    { tidligereInfo : ArbeidsoppgaverInfo
    , fraM친ned : M친ned
    , fra칀r : String
    , visFeilmeldingFra칀r : Bool
    }


type alias N친v칝rendeInfo =
    { yrke : Yrke
    , jobbtittel : String
    , bedriftNavn : String
    , lokasjon : String
    , arbeidsoppgaver : String
    , fra칀r : 칀r
    , fraM친ned : M친ned
    }


type alias TilDatoInfo =
    { n친v칝rendeInfo : N친v칝rendeInfo
    , tilM친ned : M친ned
    , til칀r : String
    , visFeilmeldingTil칀r : Bool
    }


maxLengthArbeidsoppgaver =
    2000


yrkeInfoTilJobbtittelInfo : Yrke -> JobbtittelInfo
yrkeInfoTilJobbtittelInfo yrkeTypeahead =
    { tidligereInfo = yrkeTypeahead, jobbtittel = "" }


jobbtittelInfoTilBedriftnavnsInfo : JobbtittelInfo -> BedriftnavnInfo
jobbtittelInfoTilBedriftnavnsInfo jobbtittelInfo =
    { tidligereInfo = jobbtittelInfo
    , bedriftNavn = ""
    }


bedriftnavnsInfoTilLokasjonInfo : BedriftnavnInfo -> StedInfo
bedriftnavnsInfoTilLokasjonInfo beriftnavnsInfo =
    { tidligereInfo = beriftnavnsInfo
    , lokasjon = ""
    }


stedInfoTilArbeidsoppgaverInfo : StedInfo -> ArbeidsoppgaverInfo
stedInfoTilArbeidsoppgaverInfo lokasjonInfo =
    { tidligereInfo = lokasjonInfo
    , arbeidsoppgaver = ""
    }


arbeidsoppgaverInfoTilFraDatoInfo : ArbeidsoppgaverInfo -> FraDatoInfo
arbeidsoppgaverInfoTilFraDatoInfo arbeidsoppgaverInfo =
    { tidligereInfo = arbeidsoppgaverInfo
    , fraM친ned = Januar
    , fra칀r = ""
    , visFeilmeldingFra칀r = False
    }


fraDatoTilN친v칝rende : FraDatoInfo -> 칀r -> N친v칝rendeInfo
fraDatoTilN친v칝rende info 친r =
    { yrke = info.tidligereInfo.tidligereInfo.tidligereInfo.tidligereInfo.tidligereInfo
    , jobbtittel = info.tidligereInfo.tidligereInfo.tidligereInfo.tidligereInfo.jobbtittel
    , bedriftNavn = info.tidligereInfo.tidligereInfo.tidligereInfo.bedriftNavn
    , lokasjon = info.tidligereInfo.tidligereInfo.lokasjon
    , arbeidsoppgaver = info.tidligereInfo.arbeidsoppgaver
    , fraM친ned = info.fraM친ned
    , fra칀r = 친r
    }


n친v칝rendeInfoTilTilDatoInfo : N친v칝rendeInfo -> TilDatoInfo
n친v칝rendeInfoTilTilDatoInfo n친v칝rendeInfo =
    { n친v칝rendeInfo = n친v칝rendeInfo
    , tilM친ned = Januar
    , til칀r = ""
    , visFeilmeldingTil칀r = False
    }


n친v칝rendeInfoTilSkjema : N친v칝rendeInfo -> ValidertArbeidserfaringSkjema
n친v칝rendeInfoTilSkjema n친v칝rendeInfo =
    Skjema.initValidertSkjema
        { yrke = n친v칝rendeInfo.yrke
        , jobbTittel = n친v칝rendeInfo.jobbtittel
        , bedriftNavn = n친v칝rendeInfo.bedriftNavn
        , lokasjon = n친v칝rendeInfo.lokasjon
        , arbeidsoppgaver = n친v칝rendeInfo.arbeidsoppgaver
        , fraM친ned = n친v칝rendeInfo.fraM친ned
        , fra칀r = n친v칝rendeInfo.fra칀r
        , tilDato = N친v칝rende
        , id = Nothing
        }


tilDatoInfoTilSkjema : TilDatoInfo -> 칀r -> ValidertArbeidserfaringSkjema
tilDatoInfoTilSkjema info 친r =
    Skjema.initValidertSkjema
        { yrke = info.n친v칝rendeInfo.yrke
        , jobbTittel = info.n친v칝rendeInfo.jobbtittel
        , bedriftNavn = info.n친v칝rendeInfo.bedriftNavn
        , lokasjon = info.n친v칝rendeInfo.lokasjon
        , arbeidsoppgaver = info.n친v칝rendeInfo.arbeidsoppgaver
        , fraM친ned = info.n친v칝rendeInfo.fraM친ned
        , fra칀r = info.n친v칝rendeInfo.fra칀r
        , tilDato = Avsluttet info.tilM친ned 친r
        , id = Nothing
        }


update : Msg -> Model -> SamtaleStatus
update msg (Model model) =
    case msg of
        BrukerHopperOverArbeidserfaring ->
            ( VenterP친AnimasjonF칮rFullf칮ring "Ok, da g친r vi videre."
                |> oppdaterSamtale model (SvarFraMsg msg)
            , lagtTilSp칮rsm친lCmd model.debugStatus
            )
                |> IkkeFerdig

        BrukerVilRedigereArbeidserfaring ->
            ( VelgEnArbeidserfaring칀Redigere
                |> oppdaterSamtale model (SvarFraMsg msg)
            , lagtTilSp칮rsm친lCmd model.debugStatus
            )
                |> IkkeFerdig

        BrukerHarValgtArbeidserfaring칀Redigere arbeidserfaring ->
            let
                ( typeaheadModel, query ) =
                    initSkjemaTypeaheadFraArbeidserfaring arbeidserfaring
            in
            ( arbeidserfaring
                |> Skjema.fraArbeidserfaring
                |> RedigerSkjema OppsummeringsSkjema typeaheadModel
                |> oppdaterSamtale model (SvarFraMsg msg)
            , Cmd.batch
                [ lagtTilSp칮rsm친lCmd model.debugStatus
                , Api.getYrkeTypeahead HentetYrkeTypeahead query
                ]
            )
                |> IkkeFerdig

        BrukerVilLeggeTilNyArbeidserfaring ->
            ( initSamtaleTypeahead
                |> Tuple.first
                |> RegistrerYrke { visFeilmelding = False }
                |> oppdaterSamtale model (SvarFraMsg msg)
            , lagtTilSp칮rsm친lCmd model.debugStatus
            )
                |> IkkeFerdig

        TypeaheadMsg typeaheadMsg ->
            case model.aktivSamtale of
                RegistrerYrke visFeilmelding typeaheadModel ->
                    updateSamtaleTypeahead model visFeilmelding typeaheadMsg typeaheadModel

                RedigerSkjema skjemaType gammelTypeaheadModel skjema ->
                    let
                        ( nyTypeaheadModel, status ) =
                            Typeahead.update Yrke.label typeaheadMsg gammelTypeaheadModel
                    in
                    IkkeFerdig
                        ( nyTypeaheadModel
                            |> Typeahead.selected
                            |> Skjema.oppdaterYrke skjema
                            |> Skjema.gj칮rFeilmeldingYrkeSynlig (Typeahead.inputStatus status == InputBlurred)
                            |> RedigerSkjema skjemaType nyTypeaheadModel
                            |> oppdaterSamtale model IngenNyeMeldinger
                        , case Typeahead.getSuggestionsStatus status of
                            GetSuggestionsForInput query ->
                                Api.getYrkeTypeahead HentetYrkeTypeahead query

                            DoNothing ->
                                Cmd.none
                        )

                _ ->
                    IkkeFerdig ( Model model, Cmd.none )

        HentetYrkeTypeahead query result ->
            case model.aktivSamtale of
                RegistrerYrke visFeilmelding typeaheadModel ->
                    ( result
                        |> Typeahead.updateSuggestions Yrke.label typeaheadModel query
                        |> RegistrerYrke visFeilmelding
                        |> oppdaterSamtale model IngenNyeMeldinger
                    , result
                        |> Result.error
                        |> Maybe.map (logFeilmelding "Hente Yrketypeahead")
                        |> Maybe.withDefault Cmd.none
                    )
                        |> IkkeFerdig

                RedigerSkjema skjemaType typeaheadModel skjema ->
                    ( RedigerSkjema skjemaType (Typeahead.updateSuggestions Yrke.label typeaheadModel query result) skjema
                        |> oppdaterSamtale model IngenNyeMeldinger
                    , result
                        |> Result.error
                        |> Maybe.map (logFeilmelding "Hente Yrketypeahead")
                        |> Maybe.withDefault Cmd.none
                    )
                        |> IkkeFerdig

                _ ->
                    ( Model model, Cmd.none )
                        |> IkkeFerdig

        FeltMisterFokus ->
            IkkeFerdig ( Model model, mistetFokusCmd )

        TimeoutEtterAtFeltMistetFokus ->
            case model.aktivSamtale of
                RegistrerYrke _ typeaheadModel ->
                    visFeilmeldingRegistrerYrke model typeaheadModel

                RegistrereFraDato info ->
                    ( { info | visFeilmeldingFra칀r = True }
                        |> RegistrereFraDato
                        |> oppdaterSamtale model IngenNyeMeldinger
                    , Cmd.none
                    )
                        |> IkkeFerdig

                RegistrereTilDato til칀rInfo ->
                    ( { til칀rInfo | visFeilmeldingTil칀r = True }
                        |> RegistrereTilDato
                        |> oppdaterSamtale model IngenNyeMeldinger
                    , Cmd.none
                    )
                        |> IkkeFerdig

                _ ->
                    IkkeFerdig ( Model model, Cmd.none )

        BrukerVilRegistrereYrke ->
            case model.aktivSamtale of
                RegistrerYrke _ typeaheadModel ->
                    case Typeahead.selected typeaheadModel of
                        Just yrke ->
                            brukerVelgerYrke model msg yrke

                        Nothing ->
                            visFeilmeldingRegistrerYrke model typeaheadModel

                _ ->
                    IkkeFerdig ( Model model, Cmd.none )

        BrukerVilEndreJobbtittel ->
            case model.aktivSamtale of
                Sp칮rOmBrukerVilEndreJobbtittel jobbtittelInfo ->
                    ( jobbtittelInfo
                        |> EndreJobbtittel
                        |> oppdaterSamtale model (SvarFraMsg msg)
                    , lagtTilSp칮rsm친lCmd model.debugStatus
                    )
                        |> IkkeFerdig

                _ ->
                    IkkeFerdig ( Model model, Cmd.none )

        BrukerOppdatererJobbtittelFelt string ->
            case model.aktivSamtale of
                EndreJobbtittel jobbtittelInfo ->
                    ( { jobbtittelInfo | jobbtittel = string }
                        |> EndreJobbtittel
                        |> oppdaterSamtale model IngenNyeMeldinger
                    , Cmd.none
                    )
                        |> IkkeFerdig

                _ ->
                    ( Model model, Cmd.none )
                        |> IkkeFerdig

        BrukerVilIkkeEndreJobbtittel ->
            case model.aktivSamtale of
                Sp칮rOmBrukerVilEndreJobbtittel jobbtittelInfo ->
                    ( jobbtittelInfo
                        |> jobbtittelInfoTilBedriftnavnsInfo
                        |> RegistrereBedriftsnavn
                        |> oppdaterSamtale model (SvarFraMsg msg)
                    , lagtTilSp칮rsm친lCmd model.debugStatus
                    )
                        |> IkkeFerdig

                _ ->
                    ( Model model, lagtTilSp칮rsm친lCmd model.debugStatus )
                        |> IkkeFerdig

        BrukerVilRegistrereJobbtittel ->
            case model.aktivSamtale of
                EndreJobbtittel jobbtittelInfo ->
                    ( jobbtittelInfo
                        |> jobbtittelInfoTilBedriftnavnsInfo
                        |> RegistrereBedriftsnavn
                        |> oppdaterSamtale model (SvarFraMsg msg)
                    , lagtTilSp칮rsm친lCmd model.debugStatus
                    )
                        |> IkkeFerdig

                _ ->
                    ( Model model, lagtTilSp칮rsm친lCmd model.debugStatus )
                        |> IkkeFerdig

        BrukerOppdatererBedriftsnavn string ->
            case model.aktivSamtale of
                RegistrereBedriftsnavn beriftnavnsInfo ->
                    ( { beriftnavnsInfo | bedriftNavn = string }
                        |> RegistrereBedriftsnavn
                        |> oppdaterSamtale model IngenNyeMeldinger
                    , Cmd.none
                    )
                        |> IkkeFerdig

                _ ->
                    ( Model model, Cmd.none )
                        |> IkkeFerdig

        BrukerVilRegistrereBedriftsnavn ->
            case model.aktivSamtale of
                RegistrereBedriftsnavn bedriftnavnInfo ->
                    ( bedriftnavnInfo
                        |> bedriftnavnsInfoTilLokasjonInfo
                        |> RegistrereSted
                        |> oppdaterSamtale model (SvarFraMsg msg)
                    , lagtTilSp칮rsm친lCmd model.debugStatus
                    )
                        |> IkkeFerdig

                _ ->
                    ( Model model, Cmd.none )
                        |> IkkeFerdig

        BrukerOppdatererSted string ->
            case model.aktivSamtale of
                RegistrereSted stedInfo ->
                    ( { stedInfo | lokasjon = string }
                        |> RegistrereSted
                        |> oppdaterSamtale model IngenNyeMeldinger
                    , Cmd.none
                    )
                        |> IkkeFerdig

                _ ->
                    ( Model model, Cmd.none )
                        |> IkkeFerdig

        BrukerVilRegistrereSted ->
            case model.aktivSamtale of
                RegistrereSted stedInfo ->
                    ( stedInfo
                        |> stedInfoTilArbeidsoppgaverInfo
                        |> RegistrereArbeidsoppgaver True
                        |> oppdaterSamtale model (SvarFraMsg msg)
                    , lagtTilSp칮rsm친lCmd model.debugStatus
                    )
                        |> IkkeFerdig

                _ ->
                    ( Model model, Cmd.none )
                        |> IkkeFerdig

        VilSeEksempel ->
            case model.aktivSamtale of
                RegistrereArbeidsoppgaver _ info ->
                    let
                        oppdatertMeldingslogg =
                            model.seksjonsMeldingsLogg
                                |> MeldingsLogg.leggTilSvar (svarFraBrukerInput model msg)
                                |> MeldingsLogg.leggTilSp칮rsm친l eksemplerP친Arbeidserfaring
                    in
                    IkkeFerdig
                        ( info
                            |> RegistrereArbeidsoppgaver False
                            |> oppdaterSamtale { model | seksjonsMeldingsLogg = oppdatertMeldingslogg } IngenNyeMeldinger
                        , lagtTilSp칮rsm친lCmd model.debugStatus
                        )

                _ ->
                    IkkeFerdig ( Model model, Cmd.none )

        BrukerOppdatererArbeidsoppgaver string ->
            case model.aktivSamtale of
                RegistrereArbeidsoppgaver medEksempelKnapp arbeidsoppgaverInfo ->
                    ( { arbeidsoppgaverInfo | arbeidsoppgaver = string }
                        |> RegistrereArbeidsoppgaver medEksempelKnapp
                        |> oppdaterSamtale model IngenNyeMeldinger
                    , Cmd.none
                    )
                        |> IkkeFerdig

                _ ->
                    ( Model model, Cmd.none )
                        |> IkkeFerdig

        BrukerVilRegistrereArbeidsoppgaver ->
            case model.aktivSamtale of
                RegistrereArbeidsoppgaver _ arbeidsOppgaveInfo ->
                    case Validering.feilmeldingMaxAntallTegn arbeidsOppgaveInfo.arbeidsoppgaver maxLengthArbeidsoppgaver of
                        Nothing ->
                            ( arbeidsOppgaveInfo
                                |> arbeidsoppgaverInfoTilFraDatoInfo
                                |> RegistrereFraDato
                                |> oppdaterSamtale model (SvarFraMsg msg)
                            , lagtTilSp칮rsm친lCmd model.debugStatus
                            )
                                |> IkkeFerdig

                        Just _ ->
                            IkkeFerdig ( Model model, Cmd.none )

                _ ->
                    ( Model model, Cmd.none )
                        |> IkkeFerdig

        BrukerOppdatererFraM친ned string ->
            case model.aktivSamtale of
                RegistrereFraDato info ->
                    ( { info | fraM친ned = M친ned.stringTilM친ned string }
                        |> RegistrereFraDato
                        |> oppdaterSamtale model IngenNyeMeldinger
                    , Cmd.none
                    )
                        |> IkkeFerdig

                _ ->
                    ( Model model, Cmd.none )
                        |> IkkeFerdig

        BrukerOppdatererFra칀r string ->
            case model.aktivSamtale of
                RegistrereFraDato info ->
                    ( { info | fra칀r = string }
                        |> RegistrereFraDato
                        |> oppdaterSamtale model IngenNyeMeldinger
                    , Cmd.none
                    )
                        |> IkkeFerdig

                _ ->
                    ( Model model, Cmd.none )
                        |> IkkeFerdig

        BrukerVilRegistrereFraDato ->
            case model.aktivSamtale of
                RegistrereFraDato fraDatoInfo ->
                    case Dato.stringTil칀r fraDatoInfo.fra칀r of
                        Just fra칀r ->
                            ( fra칀r
                                |> fraDatoTilN친v칝rende fraDatoInfo
                                |> RegistrereN친v칝rende
                                |> oppdaterSamtale model (SvarFraMsg msg)
                            , lagtTilSp칮rsm친lCmd model.debugStatus
                            )
                                |> IkkeFerdig

                        Nothing ->
                            ( { fraDatoInfo | visFeilmeldingFra칀r = True }
                                |> RegistrereFraDato
                                |> oppdaterSamtale model IngenNyeMeldinger
                            , Cmd.none
                            )
                                |> IkkeFerdig

                _ ->
                    IkkeFerdig ( Model model, Cmd.none )

        BrukerSvarerJaTilN친v칝rende ->
            case model.aktivSamtale of
                RegistrereN친v칝rende n친v칝rendeInfo ->
                    ( n친v칝rendeInfo
                        |> n친v칝rendeInfoTilSkjema
                        |> VisOppsummering F칮rsteGang
                        |> oppdaterSamtale model (SvarFraMsg msg)
                    , lagtTilSp칮rsm친lCmd model.debugStatus
                    )
                        |> IkkeFerdig

                _ ->
                    ( Model model, Cmd.none )
                        |> IkkeFerdig

        BrukerSvarerNeiTilN친v칝rende ->
            case model.aktivSamtale of
                RegistrereN친v칝rende info ->
                    ( info
                        |> n친v칝rendeInfoTilTilDatoInfo
                        |> RegistrereTilDato
                        |> oppdaterSamtale model (SvarFraMsg msg)
                    , lagtTilSp칮rsm친lCmd model.debugStatus
                    )
                        |> IkkeFerdig

                _ ->
                    ( Model model, Cmd.none )
                        |> IkkeFerdig

        BrukerOppdatererTilM친ned string ->
            case model.aktivSamtale of
                RegistrereTilDato tilDatoInfo ->
                    ( { tilDatoInfo | tilM친ned = M친ned.stringTilM친ned string }
                        |> RegistrereTilDato
                        |> oppdaterSamtale model IngenNyeMeldinger
                    , Cmd.none
                    )
                        |> IkkeFerdig

                _ ->
                    ( Model model, Cmd.none )
                        |> IkkeFerdig

        BrukerOppdatererTil칀r string ->
            case model.aktivSamtale of
                RegistrereTilDato tilDatoInfo ->
                    ( { tilDatoInfo | til칀r = string }
                        |> RegistrereTilDato
                        |> oppdaterSamtale model IngenNyeMeldinger
                    , Cmd.none
                    )
                        |> IkkeFerdig

                _ ->
                    ( Model model, Cmd.none )
                        |> IkkeFerdig

        BrukerVilRegistrereTilDato ->
            case model.aktivSamtale of
                RegistrereTilDato tilDatoInfo ->
                    case Dato.stringTil칀r tilDatoInfo.til칀r of
                        Just til칀r ->
                            ( til칀r
                                |> tilDatoInfoTilSkjema tilDatoInfo
                                |> VisOppsummering F칮rsteGang
                                |> oppdaterSamtale model (SvarFraMsg msg)
                            , lagtTilSp칮rsm친lCmd model.debugStatus
                            )
                                |> IkkeFerdig

                        Nothing ->
                            ( { tilDatoInfo | visFeilmeldingTil칀r = True }
                                |> RegistrereTilDato
                                |> oppdaterSamtale model IngenNyeMeldinger
                            , Cmd.none
                            )
                                |> IkkeFerdig

                _ ->
                    IkkeFerdig ( Model model, Cmd.none )

        BrukerVilRedigereOppsummering ->
            case model.aktivSamtale of
                VisOppsummering _ skjema ->
                    updateEtterVilEndreSkjema model msg skjema

                _ ->
                    IkkeFerdig ( Model model, Cmd.none )

        SkjemaEndret skjemaEndring ->
            case model.aktivSamtale of
                RedigerSkjema typeaheadModel skjemaType arbeidserfaringSkjema ->
                    ( arbeidserfaringSkjema
                        |> oppdaterSkjema skjemaEndring
                        |> RedigerSkjema typeaheadModel skjemaType
                        |> oppdaterSamtale model IngenNyeMeldinger
                    , Cmd.none
                    )
                        |> IkkeFerdig

                _ ->
                    IkkeFerdig ( Model model, Cmd.none )

        VilSletteP친begynt ->
            case model.aktivSamtale of
                VisOppsummering _ skjema ->
                    ( BekreftSlettingAvP친begynt skjema
                        |> oppdaterSamtale model (SvarFraMsg msg)
                    , lagtTilSp칮rsm친lCmd model.debugStatus
                    )
                        |> IkkeFerdig

                _ ->
                    IkkeFerdig ( Model model, Cmd.none )

        BekrefterSlettP친begynt ->
            case model.aktivSamtale of
                BekreftSlettingAvP친begynt _ ->
                    ( SlettetP친begynt
                        |> Sp칮rOmBrukerVilLeggeInnMer model.arbeidserfaringListe
                        |> oppdaterSamtale model (SvarFraMsg msg)
                    , lagtTilSp칮rsm친lCmd model.debugStatus
                    )
                        |> IkkeFerdig

                _ ->
                    IkkeFerdig ( Model model, Cmd.none )

        AngrerSlettP친begynt ->
            case model.aktivSamtale of
                BekreftSlettingAvP친begynt skjema ->
                    ( VisOppsummering Avbr칮tSletting skjema
                        |> oppdaterSamtale model (SvarFraMsg msg)
                    , lagtTilSp칮rsm친lCmd model.debugStatus
                    )
                        |> IkkeFerdig

                _ ->
                    IkkeFerdig ( Model model, Cmd.none )

        BrukerVilLagreArbeidserfaringIOppsummering ->
            case model.aktivSamtale of
                VisOppsummering _ skjema ->
                    updateEtterLagreKnappTrykket model msg skjema

                _ ->
                    IkkeFerdig ( Model model, Cmd.none )

        BrukerVilLagreArbeidserfaringSkjema ->
            case model.aktivSamtale of
                RedigerSkjema skjemaType typeaheadModel skjema ->
                    case Skjema.valider skjema of
                        Just validertSkjema ->
                            let
                                oppsummeringstype =
                                    case skjemaType of
                                        NyArbeidserfaringsSkjema ->
                                            NyArbeidserfaring

                                        _ ->
                                            EtterEndring
                            in
                            ( validertSkjema
                                |> VisOppsummering oppsummeringstype
                                |> oppdaterSamtale model (ManueltSvar (Melding.svar (validertSkjemaTilSetninger validertSkjema)))
                            , lagtTilSp칮rsm친lCmd model.debugStatus
                            )
                                |> IkkeFerdig

                        Nothing ->
                            ( skjema
                                |> Skjema.gj칮rAlleFeilmeldingerSynlig
                                |> RedigerSkjema skjemaType typeaheadModel
                                |> oppdaterSamtale model IngenNyeMeldinger
                            , Cmd.none
                            )
                                |> IkkeFerdig

                _ ->
                    IkkeFerdig ( Model model, Cmd.none )

        ArbeidserfaringLagret result ->
            case model.aktivSamtale of
                LagrerArbeidserfaring skjema lagreStatus ->
                    case result of
                        Ok arbeidserfaringer ->
                            let
                                avsluttetGrunn =
                                    if List.length model.arbeidserfaringListe == List.length arbeidserfaringer then
                                        EndretEksisterende

                                    else
                                        AnnenAvslutning

                                nyModel =
                                    if List.length model.arbeidserfaringListe == List.length arbeidserfaringer then
                                        { model | arbeidserfaringListe = arbeidserfaringer }

                                    else
                                        { model | arbeidserfaringListe = arbeidserfaringer, harFullf칮rtEnSamtale = True }
                            in
                            ( if LagreStatus.lagrerEtterUtlogging lagreStatus then
                                avsluttetGrunn
                                    |> Sp칮rOmBrukerVilLeggeInnMer arbeidserfaringer
                                    |> oppdaterSamtale nyModel (ManueltSvar (Melding.svar [ LoggInnLenke.loggInnLenkeTekst ]))

                              else
                                avsluttetGrunn
                                    |> Sp칮rOmBrukerVilLeggeInnMer arbeidserfaringer
                                    |> oppdaterSamtale nyModel UtenSvar
                            , lagtTilSp칮rsm친lCmd model.debugStatus
                            )
                                |> IkkeFerdig

                        Err error ->
                            if LagreStatus.lagrerEtterUtlogging lagreStatus then
                                if LagreStatus.fors칮kP친Nytt lagreStatus then
                                    ( LagreStatus.fraError error
                                        |> LagrerArbeidserfaring skjema
                                        |> oppdaterSamtale model IngenNyeMeldinger
                                    , lagreArbeidserfaring ArbeidserfaringLagret skjema
                                    )
                                        |> IkkeFerdig

                                else
                                    ( skjema
                                        |> LagringFeilet error
                                        |> oppdaterSamtale model IngenNyeMeldinger
                                    , skjema
                                        |> Skjema.encode
                                        |> Api.logErrorWithRequestBody ErrorLogget "Lagre arbeidserfaring" error
                                    )
                                        |> IkkeFerdig

                            else
                                ( skjema
                                    |> LagringFeilet error
                                    |> oppdaterSamtale model UtenSvar
                                , Cmd.batch
                                    [ lagtTilSp칮rsm친lCmd model.debugStatus
                                    , skjema
                                        |> Skjema.encode
                                        |> Api.logErrorWithRequestBody ErrorLogget "Lagre arbeidserfaring" error
                                    ]
                                )
                                    |> IkkeFerdig

                _ ->
                    IkkeFerdig ( Model model, Cmd.none )

        BrukerVilPr칮ve칀LagreP친Nytt ->
            case model.aktivSamtale of
                LagringFeilet error skjema ->
                    ( error
                        |> LagreStatus.fraError
                        |> LagrerArbeidserfaring skjema
                        |> oppdaterSamtale model (SvarFraMsg msg)
                    , lagreArbeidserfaring ArbeidserfaringLagret skjema
                    )
                        |> IkkeFerdig

                _ ->
                    IkkeFerdig ( Model model, Cmd.none )

        BrukerVilAvbryteLagringen ->
            IkkeFerdig ( Model model, Cmd.none )

        BrukerVilAvbryteRegistreringen ->
            case model.aktivSamtale of
                RegistrerYrke _ _ ->
                    avbrytRegistrering model msg

                _ ->
                    ( model.aktivSamtale
                        |> BekreftAvbrytingAvRegistreringen
                        |> oppdaterSamtale model (SvarFraMsg msg)
                    , lagtTilSp칮rsm친lCmd model.debugStatus
                    )
                        |> IkkeFerdig

        BrukerBekrefterAvbrytingAvRegistrering ->
            avbrytRegistrering model msg

        BrukerVilIkkeAvbryteRegistreringen ->
            case model.aktivSamtale of
                BekreftAvbrytingAvRegistreringen samtaleStegF칮rAvbryting ->
                    ( Model
                        { model
                            | aktivSamtale = samtaleStegF칮rAvbryting
                            , seksjonsMeldingsLogg =
                                model.seksjonsMeldingsLogg
                                    |> MeldingsLogg.leggTilSvar (svarFraBrukerInput model msg)
                                    |> MeldingsLogg.leggTilSp칮rsm친l
                                        (List.concat
                                            [ [ Melding.sp칮rsm친l [ "Ok. Da fortsetter vi der vi slapp." ] ]
                                            , samtaleTilMeldingsLogg samtaleStegF칮rAvbryting
                                            ]
                                        )
                        }
                    , lagtTilSp칮rsm친lCmd model.debugStatus
                    )
                        |> IkkeFerdig

                _ ->
                    IkkeFerdig ( Model model, Cmd.none )

        WindowEndrerVisibility visibility ->
            case visibility of
                Visible ->
                    case model.aktivSamtale of
                        LagrerArbeidserfaring skjema lagreStatus ->
                            ( lagreStatus
                                |> LagreStatus.setFors칮kP친Nytt
                                |> LagrerArbeidserfaring skjema
                                |> oppdaterSamtale model IngenNyeMeldinger
                            , Cmd.none
                            )
                                |> IkkeFerdig

                        LagringFeilet error skjema ->
                            if ErrorH친ndtering.operasjonEtterError error == LoggInn then
                                IkkeFerdig
                                    ( error
                                        |> LagreStatus.fraError
                                        |> LagrerArbeidserfaring skjema
                                        |> oppdaterSamtale model IngenNyeMeldinger
                                    , lagreArbeidserfaring ArbeidserfaringLagret skjema
                                    )

                            else
                                IkkeFerdig ( Model model, Cmd.none )

                        _ ->
                            IkkeFerdig ( Model model, Cmd.none )

                Hidden ->
                    IkkeFerdig ( Model model, Cmd.none )

        SamtaleAnimasjonMsg samtaleAnimasjonMsg ->
            SamtaleAnimasjon.update model.debugStatus samtaleAnimasjonMsg model.seksjonsMeldingsLogg
                |> updateEtterFullf칮rtMelding model

        ErrorLogget ->
            IkkeFerdig ( Model model, Cmd.none )

        VilLeggeTilFlereArbeidserfaringer ->
            let
                ( typeaheadModel, query ) =
                    initSamtaleTypeahead
            in
            ( Skjema.init
                |> RedigerSkjema NyArbeidserfaringsSkjema typeaheadModel
                |> oppdaterSamtale model (SvarFraMsg msg)
            , Cmd.batch
                [ lagtTilSp칮rsm친lCmd model.debugStatus
                , Api.getYrkeTypeahead HentetYrkeTypeahead query
                ]
            )
                |> IkkeFerdig

        FerdigMedArbeidserfaring ->
            let
                sisteMelding =
                    if List.isEmpty model.arbeidserfaringListe then
                        "Ok, da g친r vi videre."

                    else
                        "Bra innsats! 游땕 N친 kan arbeidsgivere finne deg hvis de ser etter en med din erfaring."
            in
            ( sisteMelding
                |> VenterP친AnimasjonF칮rFullf칮ring
                |> oppdaterSamtale model (SvarFraMsg msg)
            , lagtTilSp칮rsm친lCmd model.debugStatus
            )
                |> IkkeFerdig

        FokusSatt _ ->
            case model.aktivSamtale of
                RedigerSkjema skjemaType typeaheadModel skjema ->
                    IkkeFerdig
                        ( RedigerSkjema skjemaType (Typeahead.hideSuggestions typeaheadModel) skjema
                            |> oppdaterSamtale model IngenNyeMeldinger
                        , Cmd.none
                        )

                _ ->
                    IkkeFerdig ( Model model, Cmd.none )


avbrytRegistrering : ModelInfo -> Msg -> SamtaleStatus
avbrytRegistrering model msg =
    ( AvbruttP친begynt
        |> Sp칮rOmBrukerVilLeggeInnMer model.arbeidserfaringListe
        |> oppdaterSamtale model (SvarFraMsg msg)
    , lagtTilSp칮rsm친lCmd model.debugStatus
    )
        |> IkkeFerdig


updateSamtaleTypeahead : ModelInfo -> { visFeilmelding : Bool } -> Typeahead.Msg Yrke -> Typeahead.Model Yrke -> SamtaleStatus
updateSamtaleTypeahead model visFeilmelding msg typeaheadModel =
    let
        ( nyTypeaheadModel, status ) =
            Typeahead.update Yrke.label msg typeaheadModel
    in
    case Typeahead.inputStatus status of
        Typeahead.Submit ->
            case Typeahead.selected typeaheadModel of
                Just yrke ->
                    brukerVelgerYrke model (TypeaheadMsg msg) yrke

                Nothing ->
                    visFeilmeldingRegistrerYrke model nyTypeaheadModel

        Typeahead.InputBlurred ->
            IkkeFerdig
                ( nyTypeaheadModel
                    |> RegistrerYrke visFeilmelding
                    |> oppdaterSamtale model IngenNyeMeldinger
                , mistetFokusCmd
                )

        Typeahead.NoChange ->
            IkkeFerdig
                ( nyTypeaheadModel
                    |> RegistrerYrke visFeilmelding
                    |> oppdaterSamtale model IngenNyeMeldinger
                , case Typeahead.getSuggestionsStatus status of
                    GetSuggestionsForInput query ->
                        Api.getYrkeTypeahead HentetYrkeTypeahead query

                    DoNothing ->
                        Cmd.none
                )

        NewActiveElement ->
            IkkeFerdig
                ( nyTypeaheadModel
                    |> RegistrerYrke visFeilmelding
                    |> oppdaterSamtale model IngenNyeMeldinger
                , nyTypeaheadModel
                    |> Typeahead.scrollActiveSuggestionIntoView Yrke.label Nothing
                    |> Cmd.map TypeaheadMsg
                )


initSamtaleTypeahead : ( Typeahead.Model Yrke, Typeahead.Query )
initSamtaleTypeahead =
    Typeahead.init
        { value = ""
        , label = "Hvilken stilling/yrke har du?"
        , id = inputIdTilString YrkeTypeaheadId
        , toString = Yrke.label
        }


initSkjemaTypeaheadFraArbeidserfaring : Arbeidserfaring -> ( Typeahead.Model Yrke, Typeahead.Query )
initSkjemaTypeaheadFraArbeidserfaring arbeidserfaring =
    arbeidserfaring
        |> Arbeidserfaring.yrke
        |> Maybe.map initSkjemaTypeaheadFraYrke
        |> Maybe.withDefault
            (Typeahead.init
                { value = ""
                , label = "Stilling/yrke"
                , id = inputIdTilString YrkeTypeaheadId
                , toString = Yrke.label
                }
            )


initSkjemaTypeaheadFraYrke : Yrke -> ( Typeahead.Model Yrke, Typeahead.Query )
initSkjemaTypeaheadFraYrke yrke =
    Typeahead.initWithSelected
        { selected = yrke
        , label = "Stilling/yrke"
        , id = inputIdTilString YrkeTypeaheadId
        , toString = Yrke.label
        }


oppdaterSkjema : SkjemaEndring -> ArbeidserfaringSkjema -> ArbeidserfaringSkjema
oppdaterSkjema endring skjema =
    case endring of
        Tekst felt string ->
            Skjema.oppdaterStringFelt felt string skjema

        N친v칝rendeToggled ->
            Skjema.toggleN친v칝rende skjema

        FraM친ned m친nedString ->
            m친nedString
                |> M친ned.stringTilM친ned
                |> Skjema.oppdaterFraM친ned skjema

        TilM친ned m친nedString ->
            m친nedString
                |> M친ned.stringTilM친ned
                |> Skjema.oppdaterTilM친ned skjema

        Fra칀rBlurred ->
            Skjema.gj칮rFeilmeldingFra칀rSynlig skjema

        Til칀rBlurred ->
            Skjema.gj칮rFeilmeldingTil칀rSynlig skjema


updateEtterFullf칮rtMelding : ModelInfo -> ( MeldingsLogg, Cmd SamtaleAnimasjon.Msg ) -> SamtaleStatus
updateEtterFullf칮rtMelding info ( nyMeldingsLogg, cmd ) =
    case MeldingsLogg.ferdigAnimert nyMeldingsLogg of
        MeldingsLogg.FerdigAnimert ferdigAnimertSamtale ->
            case info.aktivSamtale of
                VenterP친AnimasjonF칮rFullf칮ring _ ->
                    Ferdig (sistLagret (Model info)) ferdigAnimertSamtale

                _ ->
                    ( Model { info | seksjonsMeldingsLogg = nyMeldingsLogg }
                    , Cmd.batch
                        [ Cmd.map SamtaleAnimasjonMsg cmd
                        , settFokus info.aktivSamtale
                        ]
                    )
                        |> IkkeFerdig

        MeldingsLogg.MeldingerGjenst친r ->
            ( Model { info | seksjonsMeldingsLogg = nyMeldingsLogg }
            , Cmd.map SamtaleAnimasjonMsg cmd
            )
                |> IkkeFerdig


updateEtterVilEndreSkjema : ModelInfo -> Msg -> ValidertArbeidserfaringSkjema -> SamtaleStatus
updateEtterVilEndreSkjema model msg skjema =
    let
        ( typeaheadModel, query ) =
            initSkjemaTypeaheadFraYrke (Skjema.yrke skjema)
    in
    ( skjema
        |> Skjema.tilUvalidertSkjema
        |> RedigerSkjema OppsummeringsSkjema typeaheadModel
        |> oppdaterSamtale model (SvarFraMsg msg)
    , Cmd.batch
        [ lagtTilSp칮rsm친lCmd model.debugStatus
        , Api.getYrkeTypeahead HentetYrkeTypeahead query
        ]
    )
        |> IkkeFerdig


updateEtterLagreKnappTrykket : ModelInfo -> Msg -> ValidertArbeidserfaringSkjema -> SamtaleStatus
updateEtterLagreKnappTrykket model msg skjema =
    ( LagreStatus.init
        |> LagrerArbeidserfaring skjema
        |> oppdaterSamtale model (SvarFraMsg msg)
    , lagreArbeidserfaring ArbeidserfaringLagret skjema
    )
        |> IkkeFerdig


settFokus : Samtale -> Cmd Msg
settFokus samtale =
    case samtale of
        Intro ->
            settFokusCmd HarArbeidserfaringId

        RegistrerYrke _ _ ->
            settFokusCmd YrkeTypeaheadId

        Sp칮rOmBrukerVilEndreJobbtittel _ ->
            settFokusCmd EndreJobbtittelId

        EndreJobbtittel _ ->
            settFokusCmd JobbtittelInput

        RegistrereBedriftsnavn _ ->
            settFokusCmd BedriftsnavnInput

        RegistrereSted _ ->
            settFokusCmd StedInput

        RegistrereArbeidsoppgaver _ _ ->
            settFokusCmd ArbeidsoppgaverInput

        RegistrereFraDato _ ->
            settFokusCmd FraM친nedId

        RegistrereN친v칝rende _ ->
            settFokusCmd N친v칝rendeId

        RegistrereTilDato _ ->
            settFokusCmd TilM친nedId

        RedigerSkjema _ _ _ ->
            settFokusCmd YrkeTypeaheadId

        VisOppsummering _ _ ->
            settFokusCmd BekreftOppsummeringId

        Sp칮rOmBrukerVilLeggeInnMer _ _ ->
            settFokusCmd LeggTilArbeidserfaringId

        VelgEnArbeidserfaring칀Redigere ->
            settFokusCmd RedigerArbeidserfaringId

        LagringFeilet _ _ ->
            settFokusCmd LagringFeiletActionId

        BekreftSlettingAvP친begynt _ ->
            settFokusCmd SletteP친begyntId

        BekreftAvbrytingAvRegistreringen _ ->
            settFokusCmd AvbrytSlettingId

        _ ->
            Cmd.none


settFokusCmd : InputId -> Cmd Msg
settFokusCmd inputId =
    Process.sleep 200
        |> Task.andThen (\_ -> (inputIdTilString >> Dom.focus) inputId)
        |> Task.attempt FokusSatt


lagtTilSp칮rsm친lCmd : DebugStatus -> Cmd Msg
lagtTilSp칮rsm친lCmd debugStatus =
    SamtaleAnimasjon.startAnimasjon debugStatus
        |> Cmd.map SamtaleAnimasjonMsg


mistetFokusCmd : Cmd Msg
mistetFokusCmd =
    Process.sleep 100
        |> Task.perform (\_ -> TimeoutEtterAtFeltMistetFokus)


brukerVelgerYrke : ModelInfo -> Msg -> Yrke -> SamtaleStatus
brukerVelgerYrke info msg yrkesTypeahead =
    ( yrkesTypeahead
        |> yrkeInfoTilJobbtittelInfo
        |> Sp칮rOmBrukerVilEndreJobbtittel
        |> oppdaterSamtale info (SvarFraMsg msg)
    , lagtTilSp칮rsm친lCmd info.debugStatus
    )
        |> IkkeFerdig


feilmeldingTypeahead : Typeahead.Model Yrke -> Maybe String
feilmeldingTypeahead typeaheadModel =
    case Typeahead.selected typeaheadModel of
        Just _ ->
            Nothing

        Nothing ->
            Just "Velg et yrke fra listen med forslag som kommer opp"


visFeilmeldingRegistrerYrke : ModelInfo -> Typeahead.Model Yrke -> SamtaleStatus
visFeilmeldingRegistrerYrke model typeaheadModel =
    ( typeaheadModel
        |> RegistrerYrke { visFeilmelding = True }
        |> oppdaterSamtale model IngenNyeMeldinger
    , Cmd.none
    )
        |> IkkeFerdig


svarFraBrukerInput : ModelInfo -> Msg -> Melding
svarFraBrukerInput modelInfo msg =
    modelInfo
        |> modelTilBrukerInput
        |> BrukerInput.tilSvarMelding msg


oppdaterSamtale : ModelInfo -> SamtaleOppdatering Msg -> Samtale -> Model
oppdaterSamtale model meldingsoppdatering samtale =
    Model
        { model
            | aktivSamtale = samtale
            , seksjonsMeldingsLogg =
                case meldingsoppdatering of
                    IngenNyeMeldinger ->
                        model.seksjonsMeldingsLogg

                    SvarFraMsg msg ->
                        model.seksjonsMeldingsLogg
                            |> MeldingsLogg.leggTilSvar (svarFraBrukerInput model msg)
                            |> MeldingsLogg.leggTilSp칮rsm친l (samtaleTilMeldingsLogg samtale)

                    ManueltSvar melding ->
                        model.seksjonsMeldingsLogg
                            |> MeldingsLogg.leggTilSvar melding
                            |> MeldingsLogg.leggTilSp칮rsm친l (samtaleTilMeldingsLogg samtale)

                    UtenSvar ->
                        model.seksjonsMeldingsLogg
                            |> MeldingsLogg.leggTilSp칮rsm친l (samtaleTilMeldingsLogg samtale)
        }


samtaleTilMeldingsLogg : Samtale -> List Melding
samtaleTilMeldingsLogg personaliaSeksjon =
    case personaliaSeksjon of
        Intro ->
            [ Melding.sp칮rsm친l [ "N친 skal vi registrere arbeidserfaringen din" ] ]

        VelgEnArbeidserfaring칀Redigere ->
            [ Melding.sp칮rsm친l [ "Hvilken arbeidserfaring 칮nsker du 친 endre?" ] ]

        RegistrerYrke _ _ ->
            [ Melding.sp칮rsm친l [ "N친 skal du legge inn arbeidserfaring. La oss begynne med det siste arbeidsforholdet." ]
            , Melding.sp칮rsm친l [ "F칮rst m친 du velge et yrke. Begynn 친 skriv, velg fra listen med forslag som kommer opp." ]
            , Melding.sp칮rsm친l [ "Du m친 velge et av forslagene, da kan arbeidsgivere finne deg n친r de s칮ker etter folk. Du kan endre hva som vises p친 CV-en senere." ]
            ]

        Sp칮rOmBrukerVilEndreJobbtittel info ->
            [ Melding.sp칮rsm친l [ "Du valgte 춺" ++ Yrke.label info.tidligereInfo ++ "췉. Hvis dette ikke stemmer helt, kan du gi yrket et nytt navn. Det navnet vil vises p친 CV-en din. Vil du gi det et nytt navn?" ]
            ]

        EndreJobbtittel _ ->
            [ Melding.sp칮rsm친l [ "Ok, da kan du skrive inn et nytt navn." ] ]

        RegistrereBedriftsnavn _ ->
            [ Melding.sp칮rsm친l [ "Hvilken bedrift jobber eller jobbet du i?" ] ]

        RegistrereSted _ ->
            [ Melding.sp칮rsm친l [ "Hvor holder bedriften til?" ] ]

        RegistrereArbeidsoppgaver _ _ ->
            [ Melding.sp칮rsm친l [ "Fortell hvilke arbeidsoppgaver du har hatt og hva som var rollen din." ] ]

        RegistrereFraDato _ ->
            [ Melding.sp칮rsm친l [ "N친r begynte du i jobben?" ] ]

        RegistrereN친v칝rende n친v칝rendeInfo ->
            let
                yrkestittel =
                    case n친v칝rendeInfo.jobbtittel of
                        "" ->
                            Yrke.label n친v칝rendeInfo.yrke

                        jobbtittel ->
                            jobbtittel
            in
            [ Melding.sp칮rsm친l [ "Jobber du fremdeles som 춺" ++ yrkestittel ++ "췉 i " ++ n친v칝rendeInfo.bedriftNavn ++ "?" ] ]

        RegistrereTilDato _ ->
            [ Melding.sp칮rsm친l [ "N친r sluttet du i jobben?" ] ]

        VisOppsummering oppsummeringsType validertSkjema ->
            case oppsummeringsType of
                Avbr칮tSletting ->
                    [ Melding.sp칮rsm친l [ "Ok, da lar jeg arbeidserfaringen st친." ]
                    , Melding.sp칮rsm친l
                        (validertSkjemaTilSetninger validertSkjema
                            ++ [ Melding.tomLinje
                               , "Er informasjonen riktig?"
                               ]
                        )
                    ]

                EtterEndring ->
                    [ Melding.sp칮rsm친l [ "Du har endret. Er det riktig n친?" ] ]

                F칮rsteGang ->
                    [ Melding.sp칮rsm친l
                        (validertSkjemaTilSetninger validertSkjema
                            ++ [ Melding.tomLinje
                               , "Er informasjonen riktig?"
                               ]
                        )
                    ]

                NyArbeidserfaring ->
                    [ Melding.sp칮rsm친l [ "Du har lagt til en arbeidserfaring. Er informasjonen riktig?" ] ]

        RedigerSkjema skjemaType _ _ ->
            case skjemaType of
                OppsummeringsSkjema ->
                    [ Melding.sp칮rsm친l [ "G친 gjennom og endre det du 칮nsker." ] ]

                _ ->
                    [ Melding.sp칮rsm친l [ "Legg inn arbeidserfaringen din under." ] ]

        BekreftSlettingAvP친begynt _ ->
            [ Melding.sp칮rsm친l [ "Er du sikker p친 at du vil slette denne arbeidserfaringen?" ] ]

        LagrerArbeidserfaring _ _ ->
            []

        LagringFeilet error _ ->
            [ ErrorH친ndtering.errorMelding { error = error, operasjon = "lagre arbeidserfaringen" } ]

        Sp칮rOmBrukerVilLeggeInnMer arbeidserfaringer avsluttetGrunn ->
            [ case avsluttetGrunn of
                AvbruttP친begynt ->
                    Melding.sp칮rsm친l [ "N친 har jeg avbrutt." ]

                SlettetP친begynt ->
                    Melding.sp칮rsm친l [ "N친 har jeg slettet arbeidserfaringen." ]

                EndretEksisterende ->
                    Melding.sp칮rsm친l [ "Flott! N친 er arbeidserfaringen endret." ]

                AnnenAvslutning ->
                    Melding.sp칮rsm친l [ "Flott! N친 er arbeidserfaringen lagret." ]
            , if List.isEmpty arbeidserfaringer then
                Melding.sp칮rsm친l [ "Har du arbeidserfaring du vil legge inn i CV-en?" ]

              else
                Melding.sp칮rsm친l [ "Vil du legge inn flere arbeidserfaringer?" ]
            ]

        BekreftAvbrytingAvRegistreringen _ ->
            [ Melding.sp칮rsm친l [ "Hvis du avbryter, blir ikke denne arbeidserfaringen lagret p친 CV-en din. Er du sikker p친 at du vil avbryte?" ] ]

        VenterP친AnimasjonF칮rFullf칮ring string ->
            [ Melding.sp칮rsm친l [ string ] ]


validertSkjemaTilSetninger : ValidertArbeidserfaringSkjema -> List String
validertSkjemaTilSetninger validertSkjema =
    let
        skjema =
            Skjema.tilUvalidertSkjema validertSkjema
    in
    [ "Du har lagt inn dette:"
    , Melding.tomLinje
    , datoRad validertSkjema
    , Melding.tomLinje
    , "Stilling/Yrke: " ++ hentStilling validertSkjema
    , "Bedriftnavn: " ++ Skjema.innholdTekstFelt Bedriftsnavn skjema
    , "Sted: " ++ Skjema.innholdTekstFelt Sted skjema
    , Melding.tomLinje
    , "Arbeidsoppgaver: "
    , Skjema.innholdTekstFelt Arbeidsoppgaver skjema
    ]


hentStilling : ValidertArbeidserfaringSkjema -> String
hentStilling validertSkjema =
    let
        skjema =
            Skjema.tilUvalidertSkjema validertSkjema
    in
    if Skjema.innholdTekstFelt Jobbtittel skjema == "" then
        validertSkjema
            |> Skjema.yrke
            |> Yrke.label

    else
        Skjema.innholdTekstFelt Jobbtittel skjema


datoRad : ValidertArbeidserfaringSkjema -> String
datoRad skjema =
    Dato.periodeTilString
        ((Skjema.tilUvalidertSkjema >> Skjema.fraM친ned) skjema)
        (Skjema.fra칀rValidert skjema)
        (Skjema.tilDatoValidert skjema)


eksemplerP친Arbeidserfaring : List Melding
eksemplerP친Arbeidserfaring =
    [ Melding.eksempelMedTittel "Eksempel 1:" [ "L칝rling som elektriker hos Helgeland Elektro, som er spesialist p친 rehabilitering av elektriske anlegg. Vi er 5 ansatte og tar i hovedsak oppdrag for privatpersoner." ]
    , Melding.eksempelMedTittel "Eksempel 2:" [ "Ekstrahjelp som butikkmedarbeider i sommer- og juleferien. Kassearbeid, p친fylling av varer, salg og kundeservice." ]
    , Melding.eksempelMedTittel "Eksempel 3:" [ "Jobbet 3 친r som barnehageassistent i en barnehage med 24 barn. Hadde medansvar for pedagogisk arbeid p친 avdelingen. Bidro ved m친nedsplanlegging og foreldresamtaler." ]
    , Melding.eksempelMedTittel "Eksempel 4:"
        [ "Advokatsekret칝r med hovedansvar for resepsjonen i et advokatfirma med 45 ansatte."
        , Melding.tomLinje
        , "- Saksbehandling og kontoradministrative oppgaver"
        , "- Betjening av sentralbord"
        , "- Post- og dokumenth친ndtering"
        , "- Bilagsregistrering"
        , "- Klientkontakt"
        , "- M칮te- og kursbooking"
        ]
    ]



--- VIEW ---


viewBrukerInput : Model -> Html Msg
viewBrukerInput (Model model) =
    model
        |> modelTilBrukerInput
        |> BrukerInput.toHtml


modelTilBrukerInput : ModelInfo -> BrukerInput Msg
modelTilBrukerInput model =
    if MeldingsLogg.visBrukerInput model.seksjonsMeldingsLogg then
        case model.aktivSamtale of
            Intro ->
                if List.isEmpty model.arbeidserfaringListe then
                    BrukerInput.knapper Flytende
                        [ Knapp.knapp BrukerVilLeggeTilNyArbeidserfaring "Ja, jeg har arbeidserfaring"
                            |> Knapp.withId (inputIdTilString HarArbeidserfaringId)
                        , Knapp.knapp FerdigMedArbeidserfaring "Nei, jeg har ikke arbeidserfaring"
                        ]

                else
                    BrukerInput.knapper Flytende
                        [ Knapp.knapp BrukerVilLeggeTilNyArbeidserfaring "Ja, jeg vil legge til mer"
                            |> Knapp.withId (inputIdTilString HarArbeidserfaringId)
                        , Knapp.knapp BrukerHopperOverArbeidserfaring "Nei, jeg er ferdig"
                        , Knapp.knapp BrukerVilRedigereArbeidserfaring "Nei, jeg vil endre det jeg har lagt inn"
                        ]

            VelgEnArbeidserfaring칀Redigere ->
                BrukerInput.knapper Kolonne
                    (case model.arbeidserfaringListe of
                        first :: rest ->
                            (arbeidserfaringKnapp first
                                |> Knapp.withId (inputIdTilString RedigerArbeidserfaringId)
                            )
                                :: List.map arbeidserfaringKnapp rest

                        _ ->
                            []
                    )

            RegistrerYrke visFeilmelding typeaheadModel ->
                viewRegistrerYrke visFeilmelding typeaheadModel

            Sp칮rOmBrukerVilEndreJobbtittel _ ->
                BrukerInput.knapper Flytende
                    [ Knapp.knapp BrukerVilEndreJobbtittel "Ja, jeg vil gi det et nytt navn"
                        |> Knapp.withId (inputIdTilString EndreJobbtittelId)
                    , Knapp.knapp BrukerVilIkkeEndreJobbtittel "Nei, g친 videre"
                    ]

            EndreJobbtittel jobbtittelInfo ->
                BrukerInput.inputMedG친VidereKnapp { onAvbryt = BrukerVilAvbryteRegistreringen, onG친Videre = BrukerVilRegistrereJobbtittel }
                    (jobbtittelInfo.jobbtittel
                        |> Input.input { label = "Stilling/yrke som vil vises i CV-en", msg = BrukerOppdatererJobbtittelFelt }
                        |> Input.withOnEnter BrukerVilRegistrereJobbtittel
                        |> Input.withId (inputIdTilString JobbtittelInput)
                    )

            RegistrereBedriftsnavn bedriftnanvsInfo ->
                BrukerInput.inputMedG친VidereKnapp { onAvbryt = BrukerVilAvbryteRegistreringen, onG친Videre = BrukerVilRegistrereBedriftsnavn }
                    (bedriftnanvsInfo.bedriftNavn
                        |> Input.input { label = "Bedriftens navn", msg = BrukerOppdatererBedriftsnavn }
                        |> Input.withOnEnter BrukerVilRegistrereBedriftsnavn
                        |> Input.withId (inputIdTilString BedriftsnavnInput)
                    )

            RegistrereSted stedInfo ->
                BrukerInput.inputMedG친VidereKnapp { onAvbryt = BrukerVilAvbryteRegistreringen, onG친Videre = BrukerVilRegistrereSted }
                    (stedInfo.lokasjon
                        |> Input.input { label = "By, sted eller land", msg = BrukerOppdatererSted }
                        |> Input.withOnEnter BrukerVilRegistrereSted
                        |> Input.withId (inputIdTilString StedInput)
                    )

            RegistrereArbeidsoppgaver medEksempelKnapp arbeidsoppgaverInfo ->
                arbeidsoppgaverInfo.arbeidsoppgaver
                    |> Textarea.textarea { label = "Arbeidsoppgaver", msg = BrukerOppdatererArbeidsoppgaver }
                    |> Textarea.withId (inputIdTilString ArbeidsoppgaverInput)
                    |> Textarea.withFeilmelding (Validering.feilmeldingMaxAntallTegn arbeidsoppgaverInfo.arbeidsoppgaver maxLengthArbeidsoppgaver)
                    |> BrukerInputMedG친VidereKnapp.textarea BrukerVilRegistrereArbeidsoppgaver
                    |> BrukerInputMedG친VidereKnapp.withVisEksempelKnapp medEksempelKnapp VilSeEksempel
                    |> BrukerInputMedG친VidereKnapp.withAvbrytKnapp BrukerVilAvbryteRegistreringen
                    |> BrukerInput.brukerInputMedG친VidereKnapp

            RegistrereFraDato info ->
                DatoInput.datoInput
                    { onM친nedChange = BrukerOppdatererFraM친ned
                    , m친ned = info.fraM친ned
                    , on칀rChange = BrukerOppdatererFra칀r
                    , 친r = info.fra칀r
                    }
                    |> DatoInput.withFeilmelding칀r
                        (info.fra칀r
                            |> Dato.feilmelding칀r
                            |> maybeHvisTrue info.visFeilmeldingFra칀r
                        )
                    |> DatoInput.withFokusId (inputIdTilString FraM친nedId)
                    |> DatoInput.withOnBlur칀r FeltMisterFokus
                    |> BrukerInputMedG친VidereKnapp.datoM친ned칀r BrukerVilRegistrereFraDato
                    |> BrukerInputMedG친VidereKnapp.withAvbrytKnapp BrukerVilAvbryteRegistreringen
                    |> BrukerInput.brukerInputMedG친VidereKnapp

            RegistrereN친v칝rende _ ->
                BrukerInput.knapper Flytende
                    [ Knapp.knapp BrukerSvarerJaTilN친v칝rende "Ja"
                        |> Knapp.withId (inputIdTilString N친v칝rendeId)
                    , Knapp.knapp BrukerSvarerNeiTilN친v칝rende "Nei"
                    ]

            RegistrereTilDato info ->
                DatoInput.datoInput
                    { onM친nedChange = BrukerOppdatererTilM친ned
                    , m친ned = info.tilM친ned
                    , on칀rChange = BrukerOppdatererTil칀r
                    , 친r = info.til칀r
                    }
                    |> DatoInput.withFeilmelding칀r
                        (info.til칀r
                            |> Dato.feilmelding칀r
                            |> maybeHvisTrue info.visFeilmeldingTil칀r
                        )
                    |> DatoInput.withFokusId (inputIdTilString TilM친nedId)
                    |> DatoInput.withOnBlur칀r FeltMisterFokus
                    |> BrukerInputMedG친VidereKnapp.datoM친ned칀r BrukerVilRegistrereTilDato
                    |> BrukerInputMedG친VidereKnapp.withAvbrytKnapp BrukerVilAvbryteRegistreringen
                    |> BrukerInput.brukerInputMedG친VidereKnapp

            VisOppsummering _ skjema ->
                case Skjema.id skjema of
                    Just _ ->
                        viewBekreftOppsummering False

                    Nothing ->
                        viewBekreftOppsummering True

            RedigerSkjema skjemaType typeaheadModel skjema ->
                case skjemaType of
                    OppsummeringsSkjema ->
                        skjemaInnhold skjema typeaheadModel
                            |> BrukerInput.skjema { lagreMsg = BrukerVilLagreArbeidserfaringSkjema, lagreKnappTekst = "Lagre endringer" }

                    NyArbeidserfaringsSkjema ->
                        skjemaInnhold skjema typeaheadModel
                            |> BrukerInput.skjemaMedAvbryt
                                { lagreMsg = BrukerVilLagreArbeidserfaringSkjema
                                , lagreKnappTekst = "Lagre arbeidserfaring"
                                , onAvbryt = Just BrukerVilAvbryteRegistreringen
                                }

            BekreftSlettingAvP친begynt _ ->
                BrukerInput.knapper Flytende
                    [ Knapp.knapp BekrefterSlettP친begynt "Ja, jeg vil slette"
                        |> Knapp.withId (inputIdTilString SletteP친begyntId)
                    , Knapp.knapp AngrerSlettP친begynt "Nei, jeg vil ikke slette"
                    ]

            LagrerArbeidserfaring _ lagreStatus ->
                if LagreStatus.lagrerEtterUtlogging lagreStatus then
                    LoggInnLenke.viewLoggInnLenke

                else
                    BrukerInput.utenInnhold

            LagringFeilet error _ ->
                case ErrorH친ndtering.operasjonEtterError error of
                    GiOpp ->
                        BrukerInput.knapper Flytende
                            [ Knapp.knapp BrukerVilAvbryteLagringen "G친 videre"
                                |> Knapp.withId (inputIdTilString LagringFeiletActionId)
                            ]

                    Pr칮vP친Nytt ->
                        BrukerInput.knapper Flytende
                            [ Knapp.knapp BrukerVilPr칮ve칀LagreP친Nytt "Pr칮v igjen"
                                |> Knapp.withId (inputIdTilString LagringFeiletActionId)
                            , Knapp.knapp BrukerVilAvbryteLagringen "G친 videre"
                            ]

                    LoggInn ->
                        LoggInnLenke.viewLoggInnLenke

            Sp칮rOmBrukerVilLeggeInnMer arbeidserfaringer _ ->
                let
                    leggTilMsg =
                        if model.harFullf칮rtEnSamtale then
                            VilLeggeTilFlereArbeidserfaringer

                        else
                            BrukerVilLeggeTilNyArbeidserfaring
                in
                BrukerInput.knapper Flytende
                    ([ [ Knapp.knapp leggTilMsg "Ja, legg til en arbeidserfaring"
                            |> Knapp.withId (inputIdTilString LeggTilArbeidserfaringId)
                       , Knapp.knapp FerdigMedArbeidserfaring "Nei, jeg har lagt inn alle"
                       ]
                     , if List.length arbeidserfaringer > 0 then
                        [ Knapp.knapp BrukerVilRedigereArbeidserfaring "Nei, jeg vil endre det jeg har lagt inn" ]

                       else
                        []
                     ]
                        |> List.concat
                    )

            VenterP친AnimasjonF칮rFullf칮ring _ ->
                BrukerInput.utenInnhold

            BekreftAvbrytingAvRegistreringen _ ->
                BrukerInput.knapper Flytende
                    [ Knapp.knapp BrukerBekrefterAvbrytingAvRegistrering "Ja, jeg vil avbryte"
                        |> Knapp.withId (inputIdTilString AvbrytSlettingId)
                    , Knapp.knapp BrukerVilIkkeAvbryteRegistreringen "Nei, jeg vil fortsette"
                    ]

    else
        BrukerInput.utenInnhold


skjemaInnhold : ArbeidserfaringSkjema -> Typeahead.Model Yrke -> List (Html Msg)
skjemaInnhold skjema typeaheadModel =
    [ skjema
        |> Skjema.feilmeldingYrke
        |> Typeahead.view Yrke.label typeaheadModel
        |> Html.map TypeaheadMsg
    , if Skjema.innholdTekstFelt Jobbtittel skjema == "" then
        text ""

      else
        skjema
            |> Skjema.innholdTekstFelt Jobbtittel
            |> Input.input { label = "Jobbtittel", msg = Tekst Jobbtittel >> SkjemaEndret }
            |> Input.toHtml
    , skjema
        |> Skjema.innholdTekstFelt Bedriftsnavn
        |> Input.input { label = "Bedriftens navn", msg = Tekst Bedriftsnavn >> SkjemaEndret }
        |> Input.toHtml
    , skjema
        |> Skjema.innholdTekstFelt Sted
        |> Input.input { label = "By, sted eller land", msg = Tekst Sted >> SkjemaEndret }
        |> Input.toHtml
    , skjema
        |> Skjema.innholdTekstFelt Arbeidsoppgaver
        |> Textarea.textarea { label = "Arbeidsoppgaver", msg = Tekst Arbeidsoppgaver >> SkjemaEndret }
        |> Textarea.withFeilmelding (Validering.feilmeldingMaxAntallTegn (Skjema.innholdTekstFelt Arbeidsoppgaver skjema) maxLengthArbeidsoppgaver)
        |> Textarea.toHtml
    , div [ class "DatoInput-fra-til-rad" ]
        [ DatoInput.datoInput
            { onM친nedChange = FraM친ned >> SkjemaEndret
            , m친ned = Skjema.fraM친ned skjema
            , on칀rChange = Tekst Fra칀r >> SkjemaEndret
            , 친r = Skjema.innholdTekstFelt Fra칀r skjema
            }
            |> DatoInput.withLabel "N친r begynte du i jobbben?"
            |> DatoInput.withFeilmelding칀r (Skjema.feilmeldingFra칀r skjema)
            |> DatoInput.withOnBlur칀r (SkjemaEndret Fra칀rBlurred)
            |> DatoInput.toHtml
        , if not (Skjema.n친v칝rende skjema) then
            DatoInput.datoInput
                { onM친nedChange = TilM친ned >> SkjemaEndret
                , m친ned = Skjema.tilM친ned skjema
                , on칀rChange = Tekst Til칀r >> SkjemaEndret
                , 친r = Skjema.innholdTekstFelt Til칀r skjema
                }
                |> DatoInput.withLabel "N친r sluttet du i jobbben?"
                |> DatoInput.withFeilmelding칀r (Skjema.feilmeldingTil칀r skjema)
                |> DatoInput.withOnBlur칀r (SkjemaEndret Til칀rBlurred)
                |> DatoInput.toHtml

          else
            text ""
        ]
    , skjema
        |> Skjema.n친v칝rende
        |> Checkbox.checkbox "Jeg jobber fremdeles her" (SkjemaEndret N친v칝rendeToggled)
        |> Checkbox.withClass "blokk-m"
        |> Checkbox.toHtml
    ]


maybeHvisTrue : Bool -> Maybe a -> Maybe a
maybeHvisTrue bool maybe =
    if bool then
        maybe

    else
        Nothing


type InputId
    = HarArbeidserfaringId
    | RedigerArbeidserfaringId
    | YrkeTypeaheadId
    | EndreJobbtittelId
    | JobbtittelInput
    | BedriftsnavnInput
    | StedInput
    | ArbeidsoppgaverInput
    | FraM친nedId
    | TilM친nedId
    | N친v칝rendeId
    | BekreftOppsummeringId
    | LeggTilArbeidserfaringId
    | SletteP친begyntId
    | LagringFeiletActionId
    | AvbrytSlettingId


inputIdTilString : InputId -> String
inputIdTilString inputId =
    case inputId of
        HarArbeidserfaringId ->
            "har-arbeidserfaring-id"

        RedigerArbeidserfaringId ->
            "arbeidserfaring-rediger-id"

        YrkeTypeaheadId ->
            "arbeidserfaring-registrer-yrke-typeahead"

        JobbtittelInput ->
            "arbeidserfaring-registrer-jobbtittel"

        EndreJobbtittelId ->
            "arbeidserfaring-endre-jobbtittel"

        BedriftsnavnInput ->
            "arbeidserfaring-registrer-bedriftsnavn"

        StedInput ->
            "arbeidserfaring-registrer-sted"

        ArbeidsoppgaverInput ->
            "arbeidserfaring-registrer-arbeidsoppgaver"

        FraM친nedId ->
            "arbeidserfaring-fra-m친ned-id"

        TilM친nedId ->
            "arbeidserfaring-til-m친ned-id"

        N친v칝rendeId ->
            "arbeidserfaring-n친v칝rende-id"

        BekreftOppsummeringId ->
            "arbeidserfaring-bekreft-oppsummering-id"

        LeggTilArbeidserfaringId ->
            "arbeidserfaring-legg-til-id"

        SletteP친begyntId ->
            "arbeidserfaring-slett-p친begynt-id"

        LagringFeiletActionId ->
            "arbeidserfaring-lagring-feilet-id"

        AvbrytSlettingId ->
            "arbeidserfaring-avbrytt-slett-id"


viewRegistrerYrke : { visFeilmelding : Bool } -> Typeahead.Model Yrke -> BrukerInput Msg
viewRegistrerYrke { visFeilmelding } typeaheadModel =
    BrukerInput.typeaheadMedG친VidereKnapp { onAvbryt = BrukerVilAvbryteRegistreringen, onG친Videre = BrukerVilRegistrereYrke }
        (typeaheadModel
            |> feilmeldingTypeahead
            |> maybeHvisTrue visFeilmelding
            |> Typeahead.toViewElement Yrke.label typeaheadModel
            |> FrontendModuler.Typeahead.map TypeaheadMsg
        )


viewBekreftOppsummering : Bool -> BrukerInput Msg
viewBekreftOppsummering skalViseSlett =
    if skalViseSlett then
        BrukerInput.knapper Kolonne
            [ Knapp.knapp BrukerVilLagreArbeidserfaringIOppsummering "Ja, det er riktig"
                |> Knapp.withId (inputIdTilString BekreftOppsummeringId)
            , Knapp.knapp BrukerVilRedigereOppsummering "Nei, jeg vil endre"
            , Knapp.knapp VilSletteP친begynt "Nei, jeg vil slette"
            ]

    else
        BrukerInput.knapper Flytende
            [ Knapp.knapp BrukerVilLagreArbeidserfaringIOppsummering "Ja, det er riktig"
                |> Knapp.withId (inputIdTilString BekreftOppsummeringId)
            , Knapp.knapp BrukerVilRedigereOppsummering "Nei, jeg vil endre"
            ]


arbeidserfaringKnapp : Arbeidserfaring -> Knapp Msg
arbeidserfaringKnapp arbeidserfaring =
    let
        tekst =
            Maybe.withDefault "" (Arbeidserfaring.yrkeString arbeidserfaring)
                ++ ", "
                ++ Maybe.withDefault "" (Arbeidserfaring.arbeidsgiver arbeidserfaring)
    in
    Knapp.knapp (BrukerHarValgtArbeidserfaring칀Redigere arbeidserfaring) tekst


lagreArbeidserfaring : (Result Error (List Arbeidserfaring) -> msg) -> Skjema.ValidertArbeidserfaringSkjema -> Cmd msg
lagreArbeidserfaring msgConstructor skjema =
    case Skjema.id skjema of
        Just id ->
            Api.endreArbeidserfaring msgConstructor skjema id

        Nothing ->
            Api.opprettArbeidserfaring msgConstructor skjema


arbeidserfaringerTilTekstomr친der : List Arbeidserfaring -> List Tekstomr친de
arbeidserfaringerTilTekstomr친der arbeidserfaringer =
    arbeidserfaringer
        |> List.map arbeidserfaringTilTekstomr친de
        |> List.intersperse (Avsnitt Melding.tomLinje)


arbeidserfaringTilTekstomr친de : Arbeidserfaring -> Tekstomr친de
arbeidserfaringTilTekstomr친de arbeidserfaring =
    Seksjon (beskrivArbeidserfaring arbeidserfaring)
        [ Dato.periodeTilString (Arbeidserfaring.fraM친ned arbeidserfaring) (Arbeidserfaring.fra칀r arbeidserfaring) (Arbeidserfaring.tilDato arbeidserfaring)
        , beskrivArbeidserfaring arbeidserfaring
        ]


beskrivArbeidserfaring : Arbeidserfaring -> String
beskrivArbeidserfaring arbeidserfaring =
    let
        maybeYrkeTekst : Maybe String
        maybeYrkeTekst =
            Maybe.or
                (Arbeidserfaring.yrkeFritekst arbeidserfaring)
                (Arbeidserfaring.yrkeString arbeidserfaring)
    in
    [ maybeYrkeTekst
    , Arbeidserfaring.arbeidsgiver arbeidserfaring
    ]
        |> Maybe.values
        |> List.intersperse "hos"
        |> String.join " "


logFeilmelding : String -> Http.Error -> Cmd Msg
logFeilmelding operasjon error =
    Feilmelding.feilmelding operasjon error
        |> Maybe.map (Api.logError (always ErrorLogget))
        |> Maybe.withDefault Cmd.none


init : DebugStatus -> Posix -> FerdigAnimertMeldingsLogg -> List Arbeidserfaring -> ( Model, Cmd Msg )
init debugStatus sistLagretFraForrigeSeksjon gammelMeldingsLogg arbeidserfaringsListe =
    ( Model
        { seksjonsMeldingsLogg =
            gammelMeldingsLogg
                |> MeldingsLogg.tilMeldingsLogg
                |> (if List.isEmpty arbeidserfaringsListe then
                        MeldingsLogg.leggTilSp칮rsm친l
                            [ Melding.sp칮rsm친l
                                [ "Har du arbeidserfaring du vil legge inn?"
                                ]
                            ]

                    else
                        MeldingsLogg.leggTilSp칮rsm친l
                            [ Melding.sp칮rsm친l [ "N친 skal vi legge til arbeidserfaringen din." ]
                            , Melding.sp칮rsm친l [ "Jeg ser at du har lagt til noe allerede." ]
                            , Melding.sp칮rsm친lMedTekstomr친der (arbeidserfaringerTilTekstomr친der arbeidserfaringsListe)
                            , Melding.sp칮rsm친l [ "Vil du legge til mer?" ]
                            ]
                   )
        , arbeidserfaringListe = arbeidserfaringsListe
        , aktivSamtale = Intro
        , debugStatus = debugStatus
        , sistLagretFraForrigeSeksjon = sistLagretFraForrigeSeksjon
        , harFullf칮rtEnSamtale = False
        }
    , lagtTilSp칮rsm친lCmd debugStatus
    )


subscriptions : Model -> Sub Msg
subscriptions (Model model) =
    Sub.batch
        [ Browser.Events.onVisibilityChange WindowEndrerVisibility
        , model.seksjonsMeldingsLogg
            |> SamtaleAnimasjon.subscriptions
            |> Sub.map SamtaleAnimasjonMsg
        ]
