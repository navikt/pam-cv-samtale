module Sertifikat.Seksjon exposing
    ( Model
    , Msg
    , SamtaleStatus(..)
    , init
    , meldingsLogg
    , subscriptions
    , update
    , viewBrukerInput
    )

import Api
import Browser.Dom as Dom
import Browser.Events exposing (Visibility(..))
import Cv.Sertifikat exposing (Sertifikat)
import Dato exposing (M친ned(..), 칀r, datoTilString)
import DebugStatus exposing (DebugStatus)
import ErrorHandtering as ErrorH친ndtering exposing (OperasjonEtterError(..))
import Feilmelding
import FrontendModuler.BrukerInput as BrukerInput exposing (BrukerInput, KnapperLayout(..))
import FrontendModuler.Checkbox as Checkbox
import FrontendModuler.DatoInput as DatoInput
import FrontendModuler.Input as Input
import FrontendModuler.Knapp as Knapp
import FrontendModuler.LoggInnLenke as LoggInnLenke
import FrontendModuler.Typeahead
import Html exposing (..)
import Html.Attributes exposing (..)
import Http exposing (Error)
import LagreStatus exposing (LagreStatus)
import Meldinger.Melding as Melding exposing (Melding(..))
import Meldinger.MeldingsLogg as MeldingsLogg exposing (FerdigAnimertMeldingsLogg, FerdigAnimertStatus(..), MeldingsLogg, tilMeldingsLogg)
import Meldinger.SamtaleAnimasjon as SamtaleAnimasjon
import Meldinger.SamtaleOppdatering exposing (SamtaleOppdatering(..))
import Process
import Sertifikat.SertifikatTypeahead as SertifikatTypeahead exposing (SertifikatTypeahead)
import Sertifikat.Skjema as Skjema exposing (SertifikatFelt(..), SertifikatSkjema, Utl칮psdato(..), ValidertSertifikatSkjema)
import Task
import Typeahead.Typeahead as Typeahead exposing (GetSuggestionStatus(..), InputStatus(..))



--- MODEL ---


type Model
    = Model ModelInfo


type alias ModelInfo =
    { seksjonsMeldingsLogg : MeldingsLogg
    , aktivSamtale : Samtale
    , sertifikatListe : List Sertifikat
    , debugStatus : DebugStatus
    }


meldingsLogg : Model -> MeldingsLogg
meldingsLogg (Model model) =
    model.seksjonsMeldingsLogg


type SamtaleStatus
    = IkkeFerdig ( Model, Cmd Msg )
    | Ferdig (List Sertifikat) FerdigAnimertMeldingsLogg


type AvsluttetGrunn
    = SlettetP친begynt
    | AnnenAvslutning


type OppsummeringsType
    = F칮rsteGang
    | EtterEndring
    | Avbr칮tSletting


type Samtale
    = RegistrerSertifikatFelt Bool (Typeahead.Model SertifikatTypeahead)
    | RegistrerUtsteder UtstederInfo
    | RegistrerFullf칮rtM친ned Fullf칮rtDatoInfo
    | RegistrerFullf칮rt칀r Fullf칮rtDatoInfo
    | Sp칮rOmUtl칮psdatoFinnes ValidertFullf칮rtDatoInfo
    | RegistrerUtl칮perM친ned Utl칮psdatoInfo
    | RegistrerUtl칮per칀r Utl칮psdatoInfo
    | VisOppsummering OppsummeringsType ValidertSertifikatSkjema
    | EndreOpplysninger (Typeahead.Model SertifikatTypeahead) SertifikatSkjema
    | BekreftSlettingAvP친begynt ValidertSertifikatSkjema
    | LagrerSkjema ValidertSertifikatSkjema LagreStatus
    | LagringFeilet Http.Error ValidertSertifikatSkjema
    | VenterP친AnimasjonF칮rFullf칮ring (List Sertifikat) AvsluttetGrunn


type alias UtstederInfo =
    { sertifikatFelt : SertifikatFelt
    , utsteder : String
    }


type alias Fullf칮rtDatoInfo =
    { sertifikat : SertifikatFelt
    , utsteder : String
    , fullf칮rtM친ned : M친ned
    , fullf칮rt칀r : String
    , visFeilmeldingFullf칮rt칀r : Bool
    }


type alias ValidertFullf칮rtDatoInfo =
    { sertifikat : SertifikatFelt
    , utsteder : String
    , fullf칮rtM친ned : M친ned
    , fullf칮rt칀r : 칀r
    }


type alias Utl칮psdatoInfo =
    { forrigeFeltInfo : ValidertFullf칮rtDatoInfo
    , utl칮perM친ned : M친ned
    , utl칮per칀r : String
    , visFeilmeldingUtl칮per칀r : Bool
    }


sertifikatFeltTilUtsteder : SertifikatFelt -> UtstederInfo
sertifikatFeltTilUtsteder sertifikatFelt =
    { sertifikatFelt = sertifikatFelt
    , utsteder = ""
    }


utstederTilFullf칮rtDato : UtstederInfo -> Fullf칮rtDatoInfo
utstederTilFullf칮rtDato input =
    { sertifikat = input.sertifikatFelt
    , utsteder = input.utsteder
    , fullf칮rtM친ned = Januar
    , fullf칮rt칀r = ""
    , visFeilmeldingFullf칮rt칀r = False
    }


validertFullf칮rtDatoTilUtl칮psdato : ValidertFullf칮rtDatoInfo -> Utl칮psdatoInfo
validertFullf칮rtDatoTilUtl칮psdato input =
    { forrigeFeltInfo = input
    , utl칮perM친ned = Januar
    , utl칮per칀r = ""
    , visFeilmeldingUtl칮per칀r = False
    }


validertFullf칮rtDatoTilSkjema : ValidertFullf칮rtDatoInfo -> ValidertSertifikatSkjema
validertFullf칮rtDatoTilSkjema input =
    Skjema.initValidertSkjema
        { sertifikatFelt = input.sertifikat
        , utsteder = input.utsteder
        , fullf칮rtM친ned = input.fullf칮rtM친ned
        , fullf칮rt칀r = input.fullf칮rt칀r
        , utl칮psdato = IkkeOppgitt
        , id = Nothing
        }


utl칮psdatoTilSkjema : Utl칮psdatoInfo -> 칀r -> ValidertSertifikatSkjema
utl칮psdatoTilSkjema info 친r =
    Skjema.initValidertSkjema
        { sertifikatFelt = info.forrigeFeltInfo.sertifikat
        , utsteder = info.forrigeFeltInfo.utsteder
        , fullf칮rtM친ned = info.forrigeFeltInfo.fullf칮rtM친ned
        , fullf칮rt칀r = info.forrigeFeltInfo.fullf칮rt칀r
        , utl칮psdato = Oppgitt info.utl칮perM친ned 친r
        , id = Nothing
        }



--- UPDATE ---


type Msg
    = TypeaheadMsg (Typeahead.Msg SertifikatTypeahead)
    | HentetTypeahead (Result Http.Error (List SertifikatTypeahead))
    | VilRegistrereSertifikat
    | VilRegistrereUtsteder
    | OppdatererUtsteder String
    | Fullf칮rtM친nedValgt Dato.M친ned
    | VilRegistrereFullf칮rt칀r
    | OppdatererFullf칮rt칀r String
    | VilRegistrereUtl칮perM친ned
    | VilIkkeRegistrereUtl칮pesdato
    | Utl칮perM친nedValgt Dato.M친ned
    | VilRegistrereUtl칮per칀r
    | OppdatererUtl칮per칀r String
    | VilLagreSertifikat
    | VilEndreOpplysninger
    | SkjemaEndret SkjemaEndring
    | VilSletteP친begynt
    | BekrefterSlettP친begynt
    | AngrerSlettP친begynt
    | VilLagreEndretSkjema
    | SertifikatLagret (Result Http.Error (List Sertifikat))
    | FerdigMedSertifikat
    | WindowEndrerVisibility Visibility
    | SamtaleAnimasjonMsg SamtaleAnimasjon.Msg
    | FokusSatt (Result Dom.Error ())
    | 칀rMisterFokus
    | ErrorLogget


type SkjemaEndring
    = Utsteder String
    | Fullf칮rtM친ned String
    | Fullf칮rt칀r String
    | Fullf칮rt칀rMistetFokus
    | Utl칮perIkkeToggled
    | Utl칮perM친ned String
    | Utl칮per칀r String
    | Utl칮per칀rMistetFokus


update : Msg -> Model -> SamtaleStatus
update msg (Model model) =
    case msg of
        TypeaheadMsg typeaheadMsg ->
            case model.aktivSamtale of
                RegistrerSertifikatFelt visFeilmelding typeaheadModel ->
                    updateSamtaleTypeahead model visFeilmelding typeaheadMsg typeaheadModel

                EndreOpplysninger typeaheadModel skjema ->
                    let
                        ( nyTypeaheadModel, status ) =
                            Typeahead.update SertifikatTypeahead.label typeaheadMsg typeaheadModel
                    in
                    IkkeFerdig
                        ( nyTypeaheadModel
                            |> tilSertifikatFelt
                            |> Skjema.oppdaterSertifikat skjema
                            |> Skjema.visFeilmeldingSertifikatFelt (Typeahead.inputStatus status == InputBlurred)
                            |> EndreOpplysninger nyTypeaheadModel
                            |> oppdaterSamtale model IngenNyeMeldinger
                        , case Typeahead.getSuggestionsStatus status of
                            GetSuggestionsForInput query ->
                                Api.getSertifikatTypeahead HentetTypeahead query

                            DoNothing ->
                                Cmd.none
                        )

                _ ->
                    IkkeFerdig ( Model model, Cmd.none )

        HentetTypeahead result ->
            case model.aktivSamtale of
                RegistrerSertifikatFelt visFeilmelding typeaheadModel ->
                    case result of
                        Ok suggestions ->
                            let
                                nyTypeaheadModel =
                                    Typeahead.updateSuggestions SertifikatTypeahead.label typeaheadModel suggestions
                            in
                            ( nyTypeaheadModel
                                |> RegistrerSertifikatFelt visFeilmelding
                                |> oppdaterSamtale model IngenNyeMeldinger
                            , Cmd.none
                            )
                                |> IkkeFerdig

                        Err error ->
                            ( Model model, logFeilmelding error "Hent SertifikatTypeahead" )
                                |> IkkeFerdig

                EndreOpplysninger typeaheadModel skjema ->
                    case result of
                        Ok suggestions ->
                            ( EndreOpplysninger (Typeahead.updateSuggestions SertifikatTypeahead.label typeaheadModel suggestions) skjema
                                |> oppdaterSamtale model IngenNyeMeldinger
                            , Cmd.none
                            )
                                |> IkkeFerdig

                        Err error ->
                            ( Model model, logFeilmelding error "Hent SertifikatTypeahead" )
                                |> IkkeFerdig

                _ ->
                    IkkeFerdig ( Model model, Cmd.none )

        VilRegistrereSertifikat ->
            case model.aktivSamtale of
                RegistrerSertifikatFelt _ typeaheadModel ->
                    case Typeahead.selected typeaheadModel of
                        Just sertifikat ->
                            sertifikat
                                |> SertifikatFraTypeahead
                                |> brukerVelgerSertifikatFelt model

                        Nothing ->
                            brukerVilRegistrereFritekstSertifikat model typeaheadModel

                _ ->
                    IkkeFerdig ( Model model, Cmd.none )

        VilRegistrereUtsteder ->
            case model.aktivSamtale of
                RegistrerUtsteder input ->
                    ( input
                        |> utstederTilFullf칮rtDato
                        |> RegistrerFullf칮rtM친ned
                        |> oppdaterSamtale model (SvarFraMsg msg)
                    , lagtTilSp칮rsm친lCmd model.debugStatus
                    )
                        |> IkkeFerdig

                _ ->
                    ( Model model, Cmd.none )
                        |> IkkeFerdig

        OppdatererUtsteder string ->
            case model.aktivSamtale of
                RegistrerUtsteder utsteder ->
                    ( { utsteder | utsteder = string }
                        |> RegistrerUtsteder
                        |> oppdaterSamtale model IngenNyeMeldinger
                    , Cmd.none
                    )
                        |> IkkeFerdig

                _ ->
                    ( Model model, Cmd.none )
                        |> IkkeFerdig

        Fullf칮rtM친nedValgt m친ned ->
            case model.aktivSamtale of
                RegistrerFullf칮rtM친ned fullf칮rtDatoInfo ->
                    ( m친ned
                        |> setFullf칮rtM친ned fullf칮rtDatoInfo
                        |> RegistrerFullf칮rt칀r
                        |> oppdaterSamtale model (SvarFraMsg msg)
                    , lagtTilSp칮rsm친lCmd model.debugStatus
                    )
                        |> IkkeFerdig

                _ ->
                    ( Model model, Cmd.none )
                        |> IkkeFerdig

        VilRegistrereFullf칮rt칀r ->
            case model.aktivSamtale of
                RegistrerFullf칮rt칀r fullf칮rtDatoInfo ->
                    case Dato.stringTil칀r fullf칮rtDatoInfo.fullf칮rt칀r of
                        Just fullf칮rt칀r ->
                            ( { sertifikat = fullf칮rtDatoInfo.sertifikat
                              , utsteder = fullf칮rtDatoInfo.utsteder
                              , fullf칮rtM친ned = fullf칮rtDatoInfo.fullf칮rtM친ned
                              , fullf칮rt칀r = fullf칮rt칀r
                              }
                                |> Sp칮rOmUtl칮psdatoFinnes
                                |> oppdaterSamtale model (SvarFraMsg msg)
                            , lagtTilSp칮rsm친lCmd model.debugStatus
                            )
                                |> IkkeFerdig

                        Nothing ->
                            ( { fullf칮rtDatoInfo | visFeilmeldingFullf칮rt칀r = True }
                                |> RegistrerFullf칮rt칀r
                                |> oppdaterSamtale model IngenNyeMeldinger
                            , Cmd.none
                            )
                                |> IkkeFerdig

                _ ->
                    IkkeFerdig ( Model model, Cmd.none )

        OppdatererFullf칮rt칀r string ->
            case model.aktivSamtale of
                RegistrerFullf칮rt칀r fullf칮rtDatoInfo ->
                    ( { fullf칮rtDatoInfo | fullf칮rt칀r = string }
                        |> RegistrerFullf칮rt칀r
                        |> oppdaterSamtale model IngenNyeMeldinger
                    , Cmd.none
                    )
                        |> IkkeFerdig

                _ ->
                    ( Model model, Cmd.none )
                        |> IkkeFerdig

        VilIkkeRegistrereUtl칮pesdato ->
            case model.aktivSamtale of
                Sp칮rOmUtl칮psdatoFinnes fullf칮rtDatoInfo ->
                    ( fullf칮rtDatoInfo
                        |> validertFullf칮rtDatoTilSkjema
                        |> VisOppsummering F칮rsteGang
                        |> oppdaterSamtale model (SvarFraMsg msg)
                    , lagtTilSp칮rsm친lCmd model.debugStatus
                    )
                        |> IkkeFerdig

                _ ->
                    ( Model model, Cmd.none )
                        |> IkkeFerdig

        VilRegistrereUtl칮perM친ned ->
            case model.aktivSamtale of
                Sp칮rOmUtl칮psdatoFinnes fullf칮rtDatoInfo ->
                    ( fullf칮rtDatoInfo
                        |> validertFullf칮rtDatoTilUtl칮psdato
                        |> RegistrerUtl칮perM친ned
                        |> oppdaterSamtale model (SvarFraMsg msg)
                    , lagtTilSp칮rsm친lCmd model.debugStatus
                    )
                        |> IkkeFerdig

                _ ->
                    ( Model model, Cmd.none )
                        |> IkkeFerdig

        Utl칮perM친nedValgt m친ned ->
            case model.aktivSamtale of
                RegistrerUtl칮perM친ned utl칮psdatoInfo ->
                    ( { utl칮psdatoInfo | utl칮perM친ned = m친ned }
                        |> RegistrerUtl칮per칀r
                        |> oppdaterSamtale model (SvarFraMsg msg)
                    , lagtTilSp칮rsm친lCmd model.debugStatus
                    )
                        |> IkkeFerdig

                _ ->
                    ( Model model, Cmd.none )
                        |> IkkeFerdig

        VilRegistrereUtl칮per칀r ->
            case model.aktivSamtale of
                RegistrerUtl칮per칀r utl칮psdatoInfo ->
                    case Dato.stringTil칀r utl칮psdatoInfo.utl칮per칀r of
                        Just utl칮per칀r ->
                            ( utl칮psdatoTilSkjema utl칮psdatoInfo utl칮per칀r
                                |> VisOppsummering F칮rsteGang
                                |> oppdaterSamtale model (SvarFraMsg msg)
                            , lagtTilSp칮rsm친lCmd model.debugStatus
                            )
                                |> IkkeFerdig

                        Nothing ->
                            ( { utl칮psdatoInfo | visFeilmeldingUtl칮per칀r = True }
                                |> RegistrerUtl칮per칀r
                                |> oppdaterSamtale model IngenNyeMeldinger
                            , Cmd.none
                            )
                                |> IkkeFerdig

                _ ->
                    IkkeFerdig ( Model model, Cmd.none )

        OppdatererUtl칮per칀r string ->
            case model.aktivSamtale of
                RegistrerUtl칮per칀r utl칮psdatoInfo ->
                    ( { utl칮psdatoInfo | utl칮per칀r = string }
                        |> RegistrerUtl칮per칀r
                        |> oppdaterSamtale model IngenNyeMeldinger
                    , Cmd.none
                    )
                        |> IkkeFerdig

                _ ->
                    ( Model model, Cmd.none )
                        |> IkkeFerdig

        칀rMisterFokus ->
            case model.aktivSamtale of
                RegistrerFullf칮rt칀r fullf칮rtDatoInfo ->
                    ( { fullf칮rtDatoInfo | visFeilmeldingFullf칮rt칀r = True }
                        |> RegistrerFullf칮rt칀r
                        |> oppdaterSamtale model IngenNyeMeldinger
                    , Cmd.none
                    )
                        |> IkkeFerdig

                RegistrerUtl칮per칀r utl칮psdatoInfo ->
                    ( { utl칮psdatoInfo | visFeilmeldingUtl칮per칀r = True }
                        |> RegistrerUtl칮per칀r
                        |> oppdaterSamtale model IngenNyeMeldinger
                    , Cmd.none
                    )
                        |> IkkeFerdig

                _ ->
                    ( Model model, Cmd.none )
                        |> IkkeFerdig

        VilEndreOpplysninger ->
            case model.aktivSamtale of
                VisOppsummering _ validertSertifikatSkjema ->
                    updateEtterVilEndreSkjema model msg validertSertifikatSkjema

                _ ->
                    IkkeFerdig ( Model model, Cmd.none )

        SkjemaEndret skjemaEndring ->
            case model.aktivSamtale of
                EndreOpplysninger typeaheadModel sertifikatSkjema ->
                    ( sertifikatSkjema
                        |> oppdaterSkjema skjemaEndring
                        |> EndreOpplysninger typeaheadModel
                        |> oppdaterSamtale model IngenNyeMeldinger
                    , Cmd.none
                    )
                        |> IkkeFerdig

                _ ->
                    ( Model model, Cmd.none )
                        |> IkkeFerdig

        VilSletteP친begynt ->
            case model.aktivSamtale of
                VisOppsummering _ skjema ->
                    ( BekreftSlettingAvP친begynt skjema
                        |> oppdaterSamtale model (SvarFraMsg msg)
                    , lagtTilSp칮rsm친lCmd model.debugStatus
                    )
                        |> IkkeFerdig

                _ ->
                    IkkeFerdig ( Model model, Cmd.none )

        BekrefterSlettP친begynt ->
            case model.aktivSamtale of
                BekreftSlettingAvP친begynt _ ->
                    ( VenterP친AnimasjonF칮rFullf칮ring model.sertifikatListe SlettetP친begynt
                        |> oppdaterSamtale model (SvarFraMsg msg)
                    , lagtTilSp칮rsm친lCmd model.debugStatus
                    )
                        |> IkkeFerdig

                _ ->
                    IkkeFerdig ( Model model, Cmd.none )

        AngrerSlettP친begynt ->
            case model.aktivSamtale of
                BekreftSlettingAvP친begynt skjema ->
                    ( VisOppsummering Avbr칮tSletting skjema
                        |> oppdaterSamtale model (SvarFraMsg msg)
                    , lagtTilSp칮rsm친lCmd model.debugStatus
                    )
                        |> IkkeFerdig

                _ ->
                    IkkeFerdig ( Model model, Cmd.none )

        VilLagreEndretSkjema ->
            case model.aktivSamtale of
                EndreOpplysninger typeaheadModel skjema ->
                    case Skjema.valider skjema of
                        Just validertSkjema ->
                            ( validertSkjema
                                |> VisOppsummering EtterEndring
                                |> oppdaterSamtale model (ManueltSvar (Melding.svar (validertSkjemaTilSetninger validertSkjema)))
                            , lagtTilSp칮rsm친lCmd model.debugStatus
                            )
                                |> IkkeFerdig

                        Nothing ->
                            ( skjema
                                |> Skjema.visAlleFeilmeldinger
                                |> EndreOpplysninger typeaheadModel
                                |> oppdaterSamtale model IngenNyeMeldinger
                            , Cmd.none
                            )
                                |> IkkeFerdig

                _ ->
                    IkkeFerdig ( Model model, Cmd.none )

        VilLagreSertifikat ->
            case model.aktivSamtale of
                VisOppsummering _ skjema ->
                    updateEtterLagreKnappTrykket model msg skjema

                LagringFeilet error skjema ->
                    ( error
                        |> LagreStatus.fraError
                        |> LagrerSkjema skjema
                        |> oppdaterSamtale model (SvarFraMsg msg)
                    , postEllerPutSertifikat SertifikatLagret skjema
                    )
                        |> IkkeFerdig

                _ ->
                    IkkeFerdig ( Model model, Cmd.none )

        SertifikatLagret result ->
            case model.aktivSamtale of
                LagrerSkjema skjema lagreStatus ->
                    case result of
                        Ok sertifikater ->
                            let
                                oppdatertMeldingslogg =
                                    if LagreStatus.lagrerEtterUtlogging lagreStatus then
                                        model.seksjonsMeldingsLogg
                                            |> MeldingsLogg.leggTilSvar (Melding.svar [ LoggInnLenke.loggInnLenkeTekst ])
                                            |> MeldingsLogg.leggTilSp칮rsm친l [ Melding.sp칮rsm친l [ "N친 er sertifiseringen lagret 游녨" ] ]

                                    else
                                        model.seksjonsMeldingsLogg
                                            |> MeldingsLogg.leggTilSp칮rsm친l [ Melding.sp칮rsm친l [ "N친 er sertifiseringen lagret 游녨" ] ]
                            in
                            ( VenterP친AnimasjonF칮rFullf칮ring sertifikater AnnenAvslutning
                                |> oppdaterSamtale { model | seksjonsMeldingsLogg = oppdatertMeldingslogg } UtenSvar
                            , lagtTilSp칮rsm친lCmd model.debugStatus
                            )
                                |> IkkeFerdig

                        Err error ->
                            if LagreStatus.lagrerEtterUtlogging lagreStatus then
                                if LagreStatus.fors칮kP친Nytt lagreStatus then
                                    ( LagreStatus.fraError error
                                        |> LagrerSkjema skjema
                                        |> oppdaterSamtale model IngenNyeMeldinger
                                    , postEllerPutSertifikat SertifikatLagret skjema
                                    )
                                        |> IkkeFerdig

                                else
                                    ( skjema
                                        |> LagringFeilet error
                                        |> oppdaterSamtale model IngenNyeMeldinger
                                    , skjema
                                        |> Skjema.encode
                                        |> Api.logErrorWithRequestBody ErrorLogget "Lagre sertifikat" error
                                    )
                                        |> IkkeFerdig

                            else
                                ( skjema
                                    |> LagringFeilet error
                                    |> oppdaterSamtale model UtenSvar
                                , Cmd.batch
                                    [ lagtTilSp칮rsm친lCmd model.debugStatus
                                    , skjema
                                        |> Skjema.encode
                                        |> Api.logErrorWithRequestBody ErrorLogget "Lagre sertifikat" error
                                    ]
                                )
                                    |> IkkeFerdig

                _ ->
                    ( Model model, Cmd.none )
                        |> IkkeFerdig

        FerdigMedSertifikat ->
            case model.aktivSamtale of
                LagringFeilet _ _ ->
                    ( VenterP친AnimasjonF칮rFullf칮ring model.sertifikatListe AnnenAvslutning
                        |> oppdaterSamtale model (SvarFraMsg msg)
                    , lagtTilSp칮rsm친lCmd model.debugStatus
                    )
                        |> IkkeFerdig

                _ ->
                    ( Model model, Cmd.none )
                        |> IkkeFerdig

        WindowEndrerVisibility visibility ->
            case visibility of
                Visible ->
                    case model.aktivSamtale of
                        LagrerSkjema skjema lagreStatus ->
                            ( lagreStatus
                                |> LagreStatus.setFors칮kP친Nytt
                                |> LagrerSkjema skjema
                                |> oppdaterSamtale model IngenNyeMeldinger
                            , Cmd.none
                            )
                                |> IkkeFerdig

                        LagringFeilet error skjema ->
                            if ErrorH친ndtering.operasjonEtterError error == LoggInn then
                                IkkeFerdig
                                    ( error
                                        |> LagreStatus.fraError
                                        |> LagrerSkjema skjema
                                        |> oppdaterSamtale model IngenNyeMeldinger
                                    , postEllerPutSertifikat SertifikatLagret skjema
                                    )

                            else
                                IkkeFerdig ( Model model, Cmd.none )

                        _ ->
                            IkkeFerdig ( Model model, Cmd.none )

                Hidden ->
                    IkkeFerdig ( Model model, Cmd.none )

        SamtaleAnimasjonMsg samtaleAnimasjonMsg ->
            SamtaleAnimasjon.update model.debugStatus samtaleAnimasjonMsg model.seksjonsMeldingsLogg
                |> updateEtterFullf칮rtMelding model

        FokusSatt _ ->
            IkkeFerdig ( Model model, Cmd.none )

        ErrorLogget ->
            IkkeFerdig ( Model model, Cmd.none )


initSamtaleTypeahead : Typeahead.Model SertifikatTypeahead
initSamtaleTypeahead =
    Typeahead.init
        { value = ""
        , label = "Sertifisering eller sertifikat"
        , id = inputIdTilString SertifikatTypeaheadId
        , toString = SertifikatTypeahead.label
        }


initSkjemaTypeahead : ValidertSertifikatSkjema -> Typeahead.Model SertifikatTypeahead
initSkjemaTypeahead skjema =
    case Skjema.sertifikatFeltValidert skjema of
        SertifikatFraTypeahead sertifikatTypeahead ->
            Typeahead.initWithSelected
                { selected = sertifikatTypeahead
                , label = "Sertifisering eller sertifikat"
                , id = inputIdTilString SertifikatTypeaheadId
                , toString = SertifikatTypeahead.label
                }

        Egendefinert inputValue ->
            Typeahead.init
                { value = inputValue
                , label = "Sertifisering eller sertifikat"
                , id = inputIdTilString SertifikatTypeaheadId
                , toString = SertifikatTypeahead.label
                }


sertifikatFeltFraTypeaheadModel : Typeahead.Model SertifikatTypeahead -> Maybe SertifikatFelt
sertifikatFeltFraTypeaheadModel typeaheadModel =
    case Typeahead.selected typeaheadModel of
        Just sertifikat ->
            Just (SertifikatFraTypeahead sertifikat)

        Nothing ->
            let
                trimmedInputString =
                    typeaheadModel
                        |> Typeahead.inputValue
                        |> String.trim
            in
            if String.length trimmedInputString > 0 then
                typeaheadModel
                    |> Typeahead.inputValue
                    |> Egendefinert
                    |> Just

            else
                Nothing


brukerVilRegistrereFritekstSertifikat : ModelInfo -> Typeahead.Model SertifikatTypeahead -> SamtaleStatus
brukerVilRegistrereFritekstSertifikat model typeaheadModel =
    case sertifikatFeltFraTypeaheadModel typeaheadModel of
        Just sertifikatFelt ->
            brukerVelgerSertifikatFelt model sertifikatFelt

        Nothing ->
            visFeilmeldingRegistrerSertifikat model typeaheadModel


visFeilmeldingRegistrerSertifikat : ModelInfo -> Typeahead.Model SertifikatTypeahead -> SamtaleStatus
visFeilmeldingRegistrerSertifikat model typeaheadModel =
    ( typeaheadModel
        |> RegistrerSertifikatFelt True
        |> oppdaterSamtale model IngenNyeMeldinger
    , Cmd.none
    )
        |> IkkeFerdig


tilSertifikatFelt : Typeahead.Model SertifikatTypeahead -> SertifikatFelt
tilSertifikatFelt typeaheadModel =
    case Typeahead.selected typeaheadModel of
        Just sertifikatTypeahead ->
            SertifikatFraTypeahead sertifikatTypeahead

        Nothing ->
            Egendefinert (Typeahead.inputValue typeaheadModel)


updateSamtaleTypeahead : ModelInfo -> Bool -> Typeahead.Msg SertifikatTypeahead -> Typeahead.Model SertifikatTypeahead -> SamtaleStatus
updateSamtaleTypeahead model visFeilmelding msg typeaheadModel =
    let
        ( nyTypeaheadModel, status ) =
            Typeahead.update SertifikatTypeahead.label msg typeaheadModel
    in
    case Typeahead.inputStatus status of
        Typeahead.Submit ->
            case sertifikatFeltFraTypeaheadModel nyTypeaheadModel of
                Just sertifikatFelt ->
                    brukerVelgerSertifikatFelt model sertifikatFelt

                Nothing ->
                    visFeilmeldingRegistrerSertifikat model nyTypeaheadModel

        Typeahead.InputBlurred ->
            visFeilmeldingRegistrerSertifikat model nyTypeaheadModel

        Typeahead.NoChange ->
            IkkeFerdig
                ( nyTypeaheadModel
                    |> RegistrerSertifikatFelt visFeilmelding
                    |> oppdaterSamtale model IngenNyeMeldinger
                , case Typeahead.getSuggestionsStatus status of
                    GetSuggestionsForInput string ->
                        Api.getSertifikatTypeahead HentetTypeahead string

                    DoNothing ->
                        Cmd.none
                )


feilmeldingTypeahead : Typeahead.Model SertifikatTypeahead -> Maybe String
feilmeldingTypeahead typeaheadModel =
    case sertifikatFeltFraTypeaheadModel typeaheadModel of
        Just _ ->
            Nothing

        Nothing ->
            Just "Velg eller skriv inn sertifisering eller sertifikat"


setFullf칮rtM친ned : Fullf칮rtDatoInfo -> Dato.M친ned -> Fullf칮rtDatoInfo
setFullf칮rtM친ned fullf칮rtDatoInfo m친ned =
    { fullf칮rtDatoInfo | fullf칮rtM친ned = m친ned }


oppdaterSkjema : SkjemaEndring -> SertifikatSkjema -> SertifikatSkjema
oppdaterSkjema endring skjema =
    case endring of
        Utsteder string ->
            Skjema.oppdaterUtsteder skjema string

        Fullf칮rtM친ned m친nedString ->
            m친nedString
                |> Dato.stringTilM친ned
                |> Skjema.oppdaterFullf칮rtM친ned skjema

        Fullf칮rt칀r string ->
            Skjema.oppdaterFullf칮rt칀r skjema string

        Utl칮perM친ned m친nedString ->
            m친nedString
                |> Dato.stringTilM친ned
                |> Skjema.oppdaterUtl칮perM친ned skjema

        Utl칮per칀r string ->
            Skjema.oppdaterUtl칮per칀r skjema string

        Utl칮perIkkeToggled ->
            Skjema.toggleUtl칮perIkke skjema

        Fullf칮rt칀rMistetFokus ->
            Skjema.visFeilmeldingFullf칮rt칀r skjema

        Utl칮per칀rMistetFokus ->
            Skjema.visFeilmeldingUtl칮per칀r skjema


updateEtterFullf칮rtMelding : ModelInfo -> ( MeldingsLogg, Cmd SamtaleAnimasjon.Msg ) -> SamtaleStatus
updateEtterFullf칮rtMelding model ( nyMeldingsLogg, cmd ) =
    case MeldingsLogg.ferdigAnimert nyMeldingsLogg of
        FerdigAnimert ferdigAnimertSamtale ->
            case model.aktivSamtale of
                VenterP친AnimasjonF칮rFullf칮ring sertifikatListe _ ->
                    Ferdig sertifikatListe ferdigAnimertSamtale

                _ ->
                    ( Model { model | seksjonsMeldingsLogg = nyMeldingsLogg }
                    , Cmd.batch
                        [ Cmd.map SamtaleAnimasjonMsg cmd
                        , settFokus model.aktivSamtale
                        ]
                    )
                        |> IkkeFerdig

        MeldingerGjenst친r ->
            ( Model { model | seksjonsMeldingsLogg = nyMeldingsLogg }
            , Cmd.map SamtaleAnimasjonMsg cmd
            )
                |> IkkeFerdig


brukerVelgerSertifikatFelt : ModelInfo -> SertifikatFelt -> SamtaleStatus
brukerVelgerSertifikatFelt info sertifikatFelt =
    ( sertifikatFelt
        |> sertifikatFeltTilUtsteder
        |> RegistrerUtsteder
        |> oppdaterSamtale info (ManueltSvar (Melding.svar [ sertifikatFeltTilString sertifikatFelt ]))
    , lagtTilSp칮rsm친lCmd info.debugStatus
    )
        |> IkkeFerdig


sertifikatFeltTilString : SertifikatFelt -> String
sertifikatFeltTilString sertifikatFelt =
    case sertifikatFelt of
        SertifikatFraTypeahead sertifikatTypeahead ->
            SertifikatTypeahead.label sertifikatTypeahead

        Egendefinert inputValue ->
            inputValue


updateEtterVilEndreSkjema : ModelInfo -> Msg -> ValidertSertifikatSkjema -> SamtaleStatus
updateEtterVilEndreSkjema model msg skjema =
    ( skjema
        |> Skjema.tilUvalidertSkjema
        |> EndreOpplysninger (initSkjemaTypeahead skjema)
        |> oppdaterSamtale model (SvarFraMsg msg)
    , Cmd.batch
        [ lagtTilSp칮rsm친lCmd model.debugStatus
        , skjema
            |> Skjema.sertifikatString
            |> Api.getSertifikatTypeahead HentetTypeahead
        ]
    )
        |> IkkeFerdig


updateEtterLagreKnappTrykket : ModelInfo -> Msg -> ValidertSertifikatSkjema -> SamtaleStatus
updateEtterLagreKnappTrykket model msg skjema =
    ( LagreStatus.init
        |> LagrerSkjema skjema
        |> oppdaterSamtale model (SvarFraMsg msg)
    , postEllerPutSertifikat SertifikatLagret skjema
    )
        |> IkkeFerdig


lagtTilSp칮rsm친lCmd : DebugStatus -> Cmd Msg
lagtTilSp칮rsm친lCmd debugStatus =
    SamtaleAnimasjon.startAnimasjon debugStatus
        |> Cmd.map SamtaleAnimasjonMsg


logFeilmelding : Http.Error -> String -> Cmd Msg
logFeilmelding error operasjon =
    Feilmelding.feilmelding operasjon error
        |> Maybe.map (Api.logError (always ErrorLogget))
        |> Maybe.withDefault Cmd.none


svarFraBrukerInput : ModelInfo -> Msg -> Melding
svarFraBrukerInput modelInfo msg =
    modelInfo
        |> modelTilBrukerInput
        |> BrukerInput.tilSvarMelding msg


oppdaterSamtale : ModelInfo -> SamtaleOppdatering Msg -> Samtale -> Model
oppdaterSamtale model meldingsoppdatering samtale =
    Model
        { model
            | aktivSamtale = samtale
            , seksjonsMeldingsLogg =
                case meldingsoppdatering of
                    IngenNyeMeldinger ->
                        model.seksjonsMeldingsLogg

                    SvarFraMsg msg ->
                        model.seksjonsMeldingsLogg
                            |> MeldingsLogg.leggTilSvar (svarFraBrukerInput model msg)
                            |> MeldingsLogg.leggTilSp칮rsm친l (samtaleTilMeldingsLogg samtale)

                    ManueltSvar melding ->
                        model.seksjonsMeldingsLogg
                            |> MeldingsLogg.leggTilSvar melding
                            |> MeldingsLogg.leggTilSp칮rsm친l (samtaleTilMeldingsLogg samtale)

                    UtenSvar ->
                        model.seksjonsMeldingsLogg
                            |> MeldingsLogg.leggTilSp칮rsm친l (samtaleTilMeldingsLogg samtale)
        }


samtaleTilMeldingsLogg : Samtale -> List Melding
samtaleTilMeldingsLogg sertifikatSeksjon =
    case sertifikatSeksjon of
        RegistrerSertifikatFelt _ _ ->
            [ Melding.sp칮rsm친l [ "Hva slags sertifikat eller sertifisering har du?" ]
            , Melding.sp칮rsm친l [ "Kanskje du har truckf칮rerbevis T1, eller noe helt annet? 游땕" ]
            ]

        RegistrerUtsteder _ ->
            [ Melding.sp칮rsm친l [ "Hvilken organisasjon sertifiserte deg?" ]
            , Melding.sp칮rsm친l [ "Er du usikker p친 hvem som har ansvar for sertifiseringen? Det st친r ofte p친 beviset ditt." ]
            ]

        RegistrerFullf칮rtM친ned _ ->
            [ Melding.sp칮rsm친l [ "Hvilken m친ned fullf칮rte du sertifiseringen?" ]
            ]

        RegistrerFullf칮rt칀r _ ->
            [ Melding.sp칮rsm친l [ "Hvilket 친r fullf칮rte du sertifiseringen?" ]
            ]

        Sp칮rOmUtl칮psdatoFinnes _ ->
            [ Melding.sp칮rsm친l [ "Har sertifiseringen en utl칮psdato?" ]
            ]

        RegistrerUtl칮perM친ned _ ->
            [ Melding.sp칮rsm친l [ "Hvilken m친ned utl칮per sertifiseringen din?" ]
            ]

        RegistrerUtl칮per칀r _ ->
            [ Melding.sp칮rsm친l [ "Hvilket 친r utl칮per du sertifiseringen din?" ]
            ]

        VisOppsummering oppsummeringsType skjema ->
            case oppsummeringsType of
                Avbr칮tSletting ->
                    [ Melding.sp칮rsm친l [ "Da sletter jeg ikke sertifiseringen/sertifikatet." ]
                    , oppsummeringsSp칮rsm친l skjema
                    ]

                EtterEndring ->
                    [ Melding.sp칮rsm친l [ "Du har endret. Er det riktig n친?" ] ]

                F칮rsteGang ->
                    [ oppsummeringsSp칮rsm친l skjema ]

        EndreOpplysninger _ _ ->
            [ Melding.sp칮rsm친l [ "Endre informasjonen i feltene under." ] ]

        BekreftSlettingAvP친begynt _ ->
            [ Melding.sp칮rsm친l [ "Er du sikker p친 at du vil slette denne sertifiseringen?" ] ]

        LagrerSkjema _ _ ->
            []

        LagringFeilet error _ ->
            [ ErrorH친ndtering.errorMelding { error = error, operasjon = "lagre sertifikat/sertifisering" } ]

        VenterP친AnimasjonF칮rFullf칮ring _ avsluttetGrunn ->
            case avsluttetGrunn of
                SlettetP친begynt ->
                    [ Melding.sp칮rsm친l [ "N친 har jeg slettet sertifiseringen/sertifikatet. Vil du legge inn flere kategorier?" ] ]

                _ ->
                    [ Melding.sp칮rsm친l [ "Vil du legge inn flere kategorier?" ] ]


validertSkjemaTilSetninger : ValidertSertifikatSkjema -> List String
validertSkjemaTilSetninger validertSkjema =
    let
        skjema =
            Skjema.tilUvalidertSkjema validertSkjema
    in
    [ "Sertifisering/sertifikat: " ++ (validertSkjema |> Skjema.sertifikatString)
    , "Utsteder: " ++ Skjema.utsteder skjema
    , "Fullf칮rt: " ++ Dato.datoTilString (Skjema.fullf칮rtM친ned skjema) (Skjema.fullf칮rt칀rValidert validertSkjema)
    , "Utl칮per: " ++ utl칮psdatoTilString (Skjema.utl칮psdatoValidert validertSkjema)
    ]


oppsummeringsSp칮rsm친l : ValidertSertifikatSkjema -> Melding
oppsummeringsSp칮rsm친l skjema =
    Melding.sp칮rsm친l
        ([ "Du har lagt inn dette:"
         , Melding.tomLinje
         ]
            ++ (validertSkjemaTilSetninger skjema
                    ++ [ Melding.tomLinje
                       , "Er informasjonen riktig?"
                       ]
               )
        )


utl칮psdatoTilString : Utl칮psdato -> String
utl칮psdatoTilString utl칮psdato =
    case utl칮psdato of
        Oppgitt m친ned_ 친r_ ->
            datoTilString m친ned_ 친r_

        IkkeOppgitt ->
            ""


settFokus : Samtale -> Cmd Msg
settFokus samtale =
    case samtale of
        RegistrerSertifikatFelt _ _ ->
            settFokusCmd SertifikatTypeaheadId

        RegistrerUtsteder _ ->
            settFokusCmd UtstederId

        RegistrerFullf칮rt칀r _ ->
            settFokusCmd Fullf칮rt칀rId

        RegistrerUtl칮per칀r _ ->
            settFokusCmd Utl칮per칀rId

        _ ->
            Cmd.none


settFokusCmd : InputId -> Cmd Msg
settFokusCmd inputId =
    Process.sleep 200
        |> Task.andThen (\_ -> (inputIdTilString >> Dom.focus) inputId)
        |> Task.attempt FokusSatt



--- VIEW ---


type InputId
    = SertifikatTypeaheadId
    | UtstederId
    | Fullf칮rt칀rId
    | Utl칮per칀rId


inputIdTilString : InputId -> String
inputIdTilString inputId =
    case inputId of
        UtstederId ->
            "sertifikat-utsteder-id"

        Fullf칮rt칀rId ->
            "sertifikat-fullf칮rt친r-id"

        SertifikatTypeaheadId ->
            "sertifikat-typeahead-id"

        Utl칮per칀rId ->
            "sertifikat-utl칮per친r-id"


viewBrukerInput : Model -> Html Msg
viewBrukerInput (Model model) =
    model
        |> modelTilBrukerInput
        |> BrukerInput.toHtml


modelTilBrukerInput : ModelInfo -> BrukerInput Msg
modelTilBrukerInput model =
    if MeldingsLogg.visBrukerInput model.seksjonsMeldingsLogg then
        case model.aktivSamtale of
            RegistrerSertifikatFelt visFeilmelding typeaheadModel ->
                BrukerInput.typeaheadMedG친VidereKnapp VilRegistrereSertifikat
                    (typeaheadModel
                        |> feilmeldingTypeahead
                        |> maybeHvisTrue visFeilmelding
                        |> Typeahead.toViewElement SertifikatTypeahead.label typeaheadModel
                        |> FrontendModuler.Typeahead.map TypeaheadMsg
                    )

            RegistrerUtsteder input ->
                BrukerInput.inputMedG친VidereKnapp VilRegistrereUtsteder
                    (input.utsteder
                        |> Input.input { label = "Utsteder", msg = OppdatererUtsteder }
                        |> Input.withOnEnter VilRegistrereUtsteder
                        |> Input.withId (inputIdTilString UtstederId)
                    )

            RegistrerFullf칮rtM친ned _ ->
                BrukerInput.m친nedKnapper Fullf칮rtM친nedValgt

            RegistrerFullf칮rt칀r fullf칮rtDatoInfo ->
                BrukerInput.inputMedG친VidereKnapp VilRegistrereFullf칮rt칀r
                    (fullf칮rtDatoInfo.fullf칮rt칀r
                        |> Input.input { label = "칀r", msg = OppdatererFullf칮rt칀r }
                        |> Input.withClass "aar"
                        |> Input.withWrapperClass "친r-wrapper"
                        |> Input.withOnEnter VilRegistrereFullf칮rt칀r
                        |> Input.withOnBlur 칀rMisterFokus
                        |> Input.withId (inputIdTilString Fullf칮rt칀rId)
                        |> Input.withMaybeFeilmelding
                            (fullf칮rtDatoInfo.fullf칮rt칀r
                                |> Dato.feilmelding칀r
                                |> maybeHvisTrue fullf칮rtDatoInfo.visFeilmeldingFullf칮rt칀r
                            )
                        |> Input.withErObligatorisk
                    )

            Sp칮rOmUtl칮psdatoFinnes _ ->
                BrukerInput.knapper Flytende
                    [ Knapp.knapp VilRegistrereUtl칮perM친ned "Ja, sertifiseringen utl칮per"
                    , Knapp.knapp VilIkkeRegistrereUtl칮pesdato "Nei, sertifiseringen utl칮per ikke"
                    ]

            RegistrerUtl칮perM친ned _ ->
                BrukerInput.m친nedKnapper Utl칮perM친nedValgt

            RegistrerUtl칮per칀r utl칮psdatoInfo ->
                BrukerInput.inputMedG친VidereKnapp VilRegistrereUtl칮per칀r
                    (utl칮psdatoInfo.utl칮per칀r
                        |> Input.input { label = "칀r", msg = OppdatererUtl칮per칀r }
                        |> Input.withClass "aar"
                        |> Input.withWrapperClass "친r-wrapper"
                        |> Input.withOnEnter VilRegistrereUtl칮per칀r
                        |> Input.withOnBlur 칀rMisterFokus
                        |> Input.withId (inputIdTilString Utl칮per칀rId)
                        |> Input.withMaybeFeilmelding ((Dato.feilmelding칀r >> maybeHvisTrue utl칮psdatoInfo.visFeilmeldingUtl칮per칀r) utl칮psdatoInfo.utl칮per칀r)
                        |> Input.withErObligatorisk
                    )

            VisOppsummering _ _ ->
                viewBekreftOppsummering

            EndreOpplysninger typeaheadModel skjema ->
                BrukerInput.skjema { lagreMsg = VilLagreEndretSkjema, lagreKnappTekst = "Lagre endringer" }
                    [ skjema
                        |> Skjema.feilmeldingSertifikatFelt
                        |> Typeahead.view SertifikatTypeahead.label typeaheadModel
                        |> Html.map TypeaheadMsg
                    , skjema
                        |> Skjema.utsteder
                        |> Input.input { label = "Utsteder", msg = Utsteder >> SkjemaEndret }
                        |> Input.toHtml
                    , div [ class "DatoInput-fra-til-rad" ]
                        [ DatoInput.datoInput
                            { label = "N친r fullf칮rte du sertifiseringen?"
                            , onM친nedChange = Fullf칮rtM친ned >> SkjemaEndret
                            , m친ned = Skjema.fullf칮rtM친ned skjema
                            , on칀rChange = Fullf칮rt칀r >> SkjemaEndret
                            , 친r = Skjema.fullf칮rt칀r skjema
                            }
                            |> DatoInput.withMaybeFeilmelding칀r (Skjema.feilmeldingFullf칮rt칀r skjema)
                            |> DatoInput.withOnBlur칀r (SkjemaEndret Fullf칮rt칀rMistetFokus)
                            |> DatoInput.toHtml
                        , if not (Skjema.utl칮perIkke skjema) then
                            DatoInput.datoInput
                                { label = "N친r utl칮per sertifiseringen?"
                                , onM친nedChange = Utl칮perM친ned >> SkjemaEndret
                                , m친ned = Skjema.utl칮perM친ned skjema
                                , on칀rChange = Utl칮per칀r >> SkjemaEndret
                                , 친r = Skjema.utl칮per칀r skjema
                                }
                                |> DatoInput.withMaybeFeilmelding칀r (Skjema.feilmeldingUtl칮per칀r skjema)
                                |> DatoInput.withOnBlur칀r (SkjemaEndret Utl칮per칀rMistetFokus)
                                |> DatoInput.toHtml

                          else
                            text ""
                        ]
                    , skjema
                        |> Skjema.utl칮perIkke
                        |> Checkbox.checkbox "Sertifiseringen utl칮per ikke" (SkjemaEndret Utl칮perIkkeToggled)
                        |> Checkbox.withClass "blokk-m"
                        |> Checkbox.toHtml
                    ]

            BekreftSlettingAvP친begynt _ ->
                BrukerInput.knapper Flytende
                    [ Knapp.knapp BekrefterSlettP친begynt "Ja, jeg vil slette"
                    , Knapp.knapp AngrerSlettP친begynt "Nei, jeg vil ikke slette"
                    ]

            LagrerSkjema _ lagreStatus ->
                if LagreStatus.lagrerEtterUtlogging lagreStatus then
                    LoggInnLenke.viewLoggInnLenke

                else
                    BrukerInput.utenInnhold

            LagringFeilet error _ ->
                case ErrorH친ndtering.operasjonEtterError error of
                    GiOpp ->
                        BrukerInput.knapper Flytende
                            [ Knapp.knapp FerdigMedSertifikat "G친 videre"
                            ]

                    Pr칮vP친Nytt ->
                        BrukerInput.knapper Flytende
                            [ Knapp.knapp VilLagreSertifikat "Pr칮v igjen"
                            , Knapp.knapp FerdigMedSertifikat "G친 videre"
                            ]

                    LoggInn ->
                        LoggInnLenke.viewLoggInnLenke

            VenterP친AnimasjonF칮rFullf칮ring _ _ ->
                BrukerInput.utenInnhold

    else
        BrukerInput.utenInnhold


viewBekreftOppsummering : BrukerInput Msg
viewBekreftOppsummering =
    BrukerInput.knapper Kolonne
        [ Knapp.knapp VilLagreSertifikat "Ja, det er riktig"
        , Knapp.knapp VilEndreOpplysninger "Nei, jeg vil endre"
        , Knapp.knapp VilSletteP친begynt "Nei, jeg vil slette"
        ]


maybeHvisTrue : Bool -> Maybe a -> Maybe a
maybeHvisTrue bool maybe =
    if bool then
        maybe

    else
        Nothing


postEllerPutSertifikat : (Result Error (List Sertifikat) -> msg) -> Skjema.ValidertSertifikatSkjema -> Cmd msg
postEllerPutSertifikat msgConstructor skjema =
    case Skjema.id skjema of
        Just id ->
            Api.putSertifikat msgConstructor skjema id

        Nothing ->
            Api.postSertifikat msgConstructor skjema



--- INIT ---


init : DebugStatus -> FerdigAnimertMeldingsLogg -> List Sertifikat -> ( Model, Cmd Msg )
init debugStatus gammelMeldingsLogg sertifikatListe =
    let
        aktivSamtale =
            RegistrerSertifikatFelt False initSamtaleTypeahead
    in
    ( Model
        { seksjonsMeldingsLogg =
            MeldingsLogg.leggTilSp칮rsm친l (samtaleTilMeldingsLogg aktivSamtale) (tilMeldingsLogg gammelMeldingsLogg)
        , aktivSamtale = aktivSamtale
        , sertifikatListe = sertifikatListe
        , debugStatus = debugStatus
        }
    , lagtTilSp칮rsm친lCmd debugStatus
    )


subscriptions : Model -> Sub Msg
subscriptions (Model model) =
    Sub.batch
        [ Browser.Events.onVisibilityChange WindowEndrerVisibility
        , model.seksjonsMeldingsLogg
            |> SamtaleAnimasjon.subscriptions
            |> Sub.map SamtaleAnimasjonMsg
        ]
